import "pkg:/source/api/baserequest.bs"
import "pkg:/source/api/MoonfinPlugin.bs"
import "pkg:/source/api/userauth.bs"
import "pkg:/source/enums/LiveTVScreen.bs"
import "pkg:/source/enums/String.bs"
import "pkg:/source/utils/JellyseerrUtils.bs"
import "pkg:/source/utils/misc.bs"
import "pkg:/source/utils/settingsSync.bs"

namespace session
    sub Init()
        m.global.addFields({
            session: {
                "memoryLevel": "normal",
                server: {},
                user: {
                    Configuration: {},
                    Policy: {},
                    settings: {},
                    lastRunVersion: invalid
                }
            }
        })
        session.user.settings.SaveDefaults()
    end sub

    sub Delete()
        session.server.Delete()
        session.user.Logout()
    end sub

    sub Update(key as string, value = {} as object)
        if key = "" or (key <> "user" and key <> "server") or value = invalid
            return
        end if
        tmpSession = m.global.session
        tmpSession.AddReplace(key, value)
        m.global.setFields({ session: tmpSession })
    end sub

    namespace server
        sub Delete()
            session.Update("server")
        end sub

        sub Update(key as string, value as dynamic)
            if key = "" or value = invalid then return
            tmpSessionServer = m.global.session.server
            tmpSessionServer[key] = value
            session.Update("server", tmpSessionServer)
        end sub

        function UpdateURL(value as string) as boolean
            if value = "" then return false
            session.server.Update("url", value)

            success = session.server.Populate()
            if not success
                session.server.Delete()
            end if

            return success
        end function

        function Populate() as boolean
            if m.global.session.server.url = invalid or m.global.session.server.url = "" then return false

            myServerInfo = ServerInfo()
            if myServerInfo.id = invalid then return false

            tmpSessionServer = m.global.session.server
            tmpSessionServer.AddReplace("id", myServerInfo.Id)
            tmpSessionServer.AddReplace("name", myServerInfo.ServerName)
            tmpSessionServer.AddReplace("localURL", myServerInfo.LocalAddress)
            tmpSessionServer.AddReplace("os", myServerInfo.OperatingSystem)
            tmpSessionServer.AddReplace("startupWizardCompleted", myServerInfo.StartupWizardCompleted)
            tmpSessionServer.AddReplace("version", myServerInfo.Version)
            tmpSessionServer.AddReplace("hasError", myServerInfo.error)

            isServerHTTPS = false
            if tmpSessionServer.url.left(8) = "https://" then isServerHTTPS = true
            tmpSessionServer.AddReplace("isHTTPS", isServerHTTPS)
            isLocalServerHTTPS = false
            if myServerInfo.LocalAddress <> invalid and myServerInfo.LocalAddress.left(8) = "https://" then isLocalServerHTTPS = true
            tmpSessionServer.AddReplace("isLocalHTTPS", isLocalServerHTTPS)

            session.Update("server", tmpSessionServer)
            return true
        end function
    end namespace

    namespace user

        sub Update(key as string, value as dynamic)
            if key = "" or value = invalid then return

            tmpSessionUser = m.global.session.user
            tmpSessionUser[key] = value

            if LCase(key) = "name"
                regex = CreateObject("roRegex", "[^a-zA-Z0-9\-\_]", string.EMPTY)
                tmpSessionUser["friendlyName"] = regex.ReplaceAll(value, string.EMPTY)
            end if

            session.Update("user", tmpSessionUser)

            if LCase(key) = "name"
                session.user.SetServerDeviceName()
            end if
        end sub

        sub Login(userData as object, saveCredentials = false as boolean)
            if userData = invalid or userData.id = invalid then return

            tmpSession = m.global.session
            oldUserSettings = tmpSession.user.settings
            if userData.json = invalid
                myAuthToken = tmpSession.user.authToken
                tmpSession.AddReplace("user", userData)
                tmpSession.user.AddReplace("authToken", myAuthToken)
            else
                tmpSession.AddReplace("user", userData.json.User)
                tmpSession.user.AddReplace("authToken", userData.json.AccessToken)
            end if

            regex = CreateObject("roRegex", "[^a-zA-Z0-9\-\_]", string.EMPTY)
            friendlyName = regex.ReplaceAll(tmpSession.user.name, string.EMPTY)
            tmpSession.user.AddReplace("friendlyName", friendlyName)
            tmpSession.user.AddReplace("settings", oldUserSettings)
            session.Update("user", tmpSession.user)

            lastRunVersion = get_user_setting("LastRunVersion")
            if isValid(lastRunVersion)
                session.user.Update("LastRunVersion", lastRunVersion)
            end if

            userSettings = RegistryReadAll(tmpSession.user.id)

            tmpSettingArray = m.global.session.user.settings
            for each setting in userSettings
                if setting <> "token"
                    tmpSettingArray[setting] = toBoolean(userSettings[setting])
                end if
            end for

            tmpSettingArray["serverId"] = toBoolean(m.global.session.server.id)
            if isValid(m.global.session.server.url)
                tmpSettingArray["serverUrl"] = toBoolean(LCase(m.global.session.server.url))
            end if
            if saveCredentials
                tmpSettingArray["token"] = toBoolean(tmpSession.user.authToken)
                tmpSettingArray["username"] = toBoolean(tmpSession.user.name)
            end if

            session.user.Update("settings", tmpSettingArray)

            reg = CreateObject("roRegistrySection", tmpSession.user.id)
            reg.write("serverId", m.global.session.server.id)
            if isValid(m.global.session.server.url)
                reg.write("serverUrl", LCase(m.global.session.server.url))
            end if
            if saveCredentials
                reg.write("token", tmpSession.user.authToken)
                reg.write("username", tmpSession.user.name)
            end if
            reg.flush()

            if m.global.session.user.settings["global.rememberme"]
                set_setting("active_user", tmpSession.user.id)
            end if

            user = CreateObject("roSGNode", "UserData")
            user.json = tmpSession
            user.callFunc("saveToRegistry")

            session.user.SetServerDeviceName()
            session.user.LoadUserPreferences()
            HandleJellyseerrUserSwitch()
            session.user.CheckPluginStatus()
            settingsSync.PullFromServer()
        end sub

        ' Sets the global server device name value used by the API
        sub SetServerDeviceName()
            localGlobal = m.global
            deviceName = localGlobal.device.id

            if isChainValid(localGlobal, "session.user.friendlyName")
                deviceName = deviceName + localGlobal.session.user.friendlyName
            end if

            if localGlobal.device.serverDeviceName <> deviceName
                tmpDevice = localGlobal.device
                tmpDevice.AddReplace("serverDeviceName", deviceName)
                m.global.setFields({ device: tmpDevice })
            end if
        end sub

        sub CheckPluginStatus()
            result = moonfinPlugin.Ping()

            pluginInfo = { installed: false }
            if isValid(result)
                pluginInfo = {
                    installed: true,
                    version: result.version ?? "unknown",
                    settingsSyncEnabled: result.settingsSyncEnabled ?? false,
                    mdblistAvailable: result.mdblistAvailable ?? false,
                    tmdbAvailable: result.tmdbAvailable ?? false,
                    jellyseerrEnabled: result.jellyseerrEnabled ?? false,
                    jellyseerrVariant: result.variant ?? "seerr",
                    defaultSettings: result.defaultSettings ?? invalid
                }

            end if

            tmpSession = m.global.session
            tmpSession.AddReplace("pluginInfo", pluginInfo)
            m.global.setFields({ session: tmpSession })
        end sub

        ' Load and parse Display Settings from server
        sub LoadUserPreferences()
            id = m.global.session.user.id
            ' Currently using client "emby", which is what website uses so we get same Display prefs as web.
            ' May want to change to specific Roku display settings
            url = Substitute("DisplayPreferences/usersettings?userId={0}&client=emby", id)
            resp = APIRequest(url)
            jsonResponse = getJson(resp)

            if isValid(jsonResponse) and isValid(jsonResponse.CustomPrefs)
                tmpSetting = jsonResponse.CustomPrefs.useEpisodeImagesInNextUpAndResume
                if isValid(tmpSetting)
                    tmpConfig = m.global.session.user.Configuration
                    tmpConfig.AddReplace("useEpisodeImagesInNextUpAndResume", toBoolean(tmpSetting))
                    session.user.Update("Configuration", tmpConfig)
                end if

                session.user.SaveUserGuideSettings(jsonResponse.CustomPrefs)

                ' Check if we need to migrate old home section values
                needsMigration = false
                originalPrefs = jsonResponse.CustomPrefs
                if originalPrefs.doesExist("homesection0") and originalPrefs["homesection0"] = "smalllibrarytiles"
                    needsMigration = true
                end if
                if originalPrefs.doesExist("homesection6") and originalPrefs["homesection6"] = "favorites"
                    needsMigration = true
                end if

                session.user.SaveUserHomeSections(jsonResponse.CustomPrefs)

                ' If migration occurred, save back to server
                if needsMigration
                    url = Substitute("DisplayPreferences/usersettings?userId={0}&client=emby", m.global.session.user.id)
                    resp = APIRequest(url)
                    PostJson(resp, FormatJson(jsonResponse.CustomPrefs))
                end if
            else
                ' User has no custom prefs. Save default home section values.
                session.user.SaveUserHomeSections({
                    homesection0: "resume",
                    homesection1: "recentlyreleased",
                    homesection2: "latestmedia",
                    homesection3: "none",
                    homesection4: "none",
                    homesection5: "none",
                    homesection6: "none",
                    homesection7: "none"
                })
            end if
        end sub

        ' Saves user's Live TV guide settings as Roku guide user settings.
        sub SaveUserGuideSettings(customPrefs as object)
            displayLive = "true"
            displayRepeat = "false"
            displayScreen = LiveTVScreen.Guide

            if isValid(customPrefs)
                userPreferenceDisplayLive = customPrefs["guide-indicator-live"]
                userPreferenceDisplayRepeat = customPrefs["guide-indicator-repeat"]
                userPreferenceDisplayScreen = customPrefs["landing-livetv"]

                if isValidAndNotEmpty(userPreferenceDisplayLive)
                    displayLive = userPreferenceDisplayLive
                end if
                if isValidAndNotEmpty(userPreferenceDisplayRepeat)
                    displayRepeat = userPreferenceDisplayRepeat
                end if
                if isStringEqual(userPreferenceDisplayScreen, LiveTVScreen.CHANNELS)
                    ' Live TV has five options but Roku only supports guide and channels in this context
                    displayScreen = userPreferenceDisplayScreen
                end if

            end if

            tmpSettings = m.global.session.user.settings
            tmpSettings["ui.guide.indicator.live"] = toBoolean(displayLive)
            tmpSettings["ui.guide.indicator.repeat"] = toBoolean(displayRepeat)
            tmpSettings["display.livetv.landing"] = toBoolean(displayScreen)
            session.user.Update("settings", tmpSettings)

            userId = m.global.session.user.id
            if userId <> invalid
                reg = CreateObject("roRegistrySection", userId)
                reg.write("ui.guide.indicator.live", displayLive)
                reg.write("ui.guide.indicator.repeat", displayRepeat)
                reg.write("display.livetv.landing", displayScreen)
                reg.flush()
            end if
        end sub

        ' Saves user's web client home sections as Roku user settings.
        ' Handles unsupported sections and ignores duplicates.
        sub SaveUserHomeSections(customPrefs as object)
            userPreferences = customPrefs
            rowTypes = []

            tmpSettings = m.global.session.user.settings
            userId = m.global.session.user.id
            reg = invalid
            if userId <> invalid
                reg = CreateObject("roRegistrySection", userId)
            end if

            defaultSections = {
                homesection0: "resume",
                homesection1: "recentlyreleased",
                homesection2: "latestmedia",
                homesection3: "none",
                homesection4: "none",
                homesection5: "none",
                homesection6: "none",
                homesection7: "none"
            }

            ' Migration: Remove featuredmedia from home sections if present
            ' (it's now controlled by ui.home.mediaBarEnabled toggle)
            for i = 0 to 7
                sectionKey = "homesection" + i.toStr()
                if userPreferences.doesExist(sectionKey)
                    if LCase(userPreferences[sectionKey]) = "featuredmedia"
                        userPreferences[sectionKey] = "none"
                    end if
                end if
            end for

            ' Migrate existing users: If homesection0 is set to old default "smalllibrarytiles", change it to "resume"
            if userPreferences.doesExist("homesection0")
                if userPreferences["homesection0"] = "smalllibrarytiles"
                    userPreferences["homesection0"] = "resume"
                end if
            end if

            ' Migrate existing users: If homesection6 is set to old default "favorites", change it to "none"
            if userPreferences.doesExist("homesection6")
                if userPreferences["homesection6"] = "favorites"
                    userPreferences["homesection6"] = "none"
                end if
            end if

            ' If user has no section preferences from server, use Moonfin defaults
            if not userPreferences.doesExist("homesection0")
                userPreferences = defaultSections
            end if

            for i = 0 to 7
                homeSectionKey = "homesection" + i.toStr()

                if not userPreferences.DoesExist(homeSectionKey)
                    userPreferences.AddReplace(homeSectionKey, "none")
                end if

                rowType = LCase(userPreferences[homeSectionKey])
                if not isValid(rowType) then rowType = "none"

                ' Small size library item buttons - Currently unsupported, use small library titles
                if rowType = "librarybuttons"
                    rowType = "smalllibrarytiles"
                end if

                ' None is the only section type allowed to have duplicates
                ' For all other types, only accept the 1st entry
                if inArray(rowTypes, rowType)
                    tmpSettings[homeSectionKey] = "none"
                    if isValid(reg) then reg.write(homeSectionKey, "none")
                else
                    tmpSettings[homeSectionKey] = toBoolean(rowType)
                    if isValid(reg) then reg.write(homeSectionKey, rowType)

                    if rowType <> "none"
                        rowTypes.push(rowType)
                    end if
                end if
            end for

            if isValid(reg) then reg.flush()
            session.user.Update("settings", tmpSettings)
        end sub

        sub Logout()
            session.Update("user", {
                Configuration: {},
                Policy: {},
                settings: {}
            })
            session.user.settings.SaveDefaults()
        end sub

        namespace settings
            sub Delete(name as string)
                if name = "" then return
                tmpSettingArray = m.global.session.user.settings
                tmpSettingArray.Delete(name)
                session.user.Update("settings", tmpSettingArray)
            end sub

            function Read(name as string) as dynamic
                if name = "" then return invalid
                return m.global.session.user.settings[name]
            end function

            sub SaveDefaults()
                configTree = GetConfigTree()
                if configTree = invalid then return

                tmpSettings = m.global.session.user.settings
                session.user.settings.CollectDefaults(configTree, tmpSettings)
                session.user.Update("settings", tmpSettings)

                session.user.settings.LoadGlobals()
                session.user.SetServerDeviceName()
            end sub

            sub CollectDefaults(items as object, tmpSettings as object)
                for each item in items
                    if item.settingName <> invalid and item.default <> invalid
                        if not tmpSettings.doesExist(item.settingName)
                            tmpSettings[item.settingName] = toBoolean(item.default)
                        end if
                    end if
                    if item.children <> invalid and item.children.Count() > 0
                        session.user.settings.CollectDefaults(item.children, tmpSettings)
                    end if
                end for
            end sub

            sub LoadGlobals()
                jfRegistry = RegistryReadAll("Jellyfin")
                if not isValid(jfRegistry) then return

                tmpSettings = m.global.session.user.settings
                for each item in jfRegistry
                    if Left(item, 7) = "global."
                        tmpSettings[item] = toBoolean(jfRegistry[item])
                    end if
                end for
                session.user.Update("settings", tmpSettings)
            end sub

            sub Save(name as string, value as string)
                if name = invalid or value = invalid then return
                tmpSettingArray = m.global.session.user.settings

                tmpSettingArray[name] = toBoolean(value)

                session.user.Update("settings", tmpSettingArray)
            end sub
        end namespace
    end namespace
end namespace
