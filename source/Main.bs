import "pkg:/components/manager/ViewCreator.bs"
import "pkg:/source/enums/ItemType.bs"
import "pkg:/source/enums/String.bs"

sub Main (args as dynamic) as void
    ' The main function that runs when the application is launched.
    m.screen = CreateObject("roSGScreen")
    m.port = CreateObject("roMessagePort")
    m.screen.setMessagePort(m.port)

    ' Set global constants
    setConstants()

    ' Set any initial Global Variables
    m.global = m.screen.getGlobalNode()
    SaveAppToGlobal()
    SaveDeviceToGlobal()

    session.Init()

    m.scene = m.screen.CreateScene("BaseScene")

    ' Set default background color
    m.scene.backgroundColor = chainLookupReturn(m.global.session, "user.settings.colorBackground", ColorPalette.VIEWBACKGROUND)

    m.screen.show() ' vscode_rale_tracker_entry
    'vscode_rdb_on_device_component_entry

    playstateTask = CreateObject("roSGNode", "PlaystateTask")
    playstateTask.id = "playstateTask"

    sceneManager = CreateObject("roSGNode", "SceneManager")
    sceneManager.observeField("dataReturned", m.port)
    sceneManager.observeField("buttonSelected", m.port)

    overhang = m.scene.findNode("overhang")
    overhang.observeField("itemSelected", m.port)

    m.global.addFields({ app_loaded: false, playstateTask: playstateTask, sceneManager: sceneManager })
    m.global.addFields({ queueManager: CreateObject("roSGNode", "QueueManager") })
    m.global.addFields({ audioPlayer: CreateObject("roSGNode", "AudioPlayer") })
    m.global.addFields({ jumpTo: {} })
    m.global.addFields({ fallbackFont: string.EMPTY })

    m.global.observeField("jumpTo", m.port)

    audioMiniPlayer = m.scene.findNode("audioMiniPlayer")
    if isValid(audioMiniPlayer)
        audioMiniPlayer.callFunc("setup")
    end if

    app_start:
    ' Hide both nav elements before login to prevent flash/overlap
    overhang.visible = false
    overhang.isVisible = false
    sidebar = m.scene.findNode("sidebar")
    if isValid(sidebar) then sidebar.visible = false

    if not LoginFlow() then return

    MigrateJellyseerrConfig()
    MigrateSettings()

    navPosition = get_user_setting("navbar.position")
    if isValid(sidebar)
        sidebar.observeField("itemSelected", m.port)
    end if
    if isStringEqual(navPosition, "left") and isValid(sidebar)
        sidebar.visible = true
    else
        overhang.visible = true
        overhang.isVisible = true
    end if

    sceneManager.callFunc("refreshNavReference")

    setUserColors()

    sceneManager.callFunc("clearScenes")

    group = CreateHomeGroup()
    group.callFunc("loadLibraries")
    stopLoadingSpinner()
    sceneManager.callFunc("pushScene", group)

    m.scene.observeField("exit", m.port)

    ' Download and store the fallback font to tmp:/
    ' This font may be used for CJK subtitles
    ' TODO: Move to Task to avoid threading errors
    ' downloadFallbackFont()

    ' Refresh overhang title font so it can change to fallback if desired
    if isValid(overhang)
        titleLabel = overhang.findNode("overlayTitle")
        if isValid(titleLabel)
            titleLabel.font = "font:LargeSystemFont"
        end if
    end if

    if isValid(audioMiniPlayer)
        songLabel = audioMiniPlayer.findNode("song")
        if isValid(songLabel)
            songLabel.font = "font:MediumSystemFont"
        end if
    end if

    ' Delete any old library filters
    clearOldLibraryFilters()

    displayWhatsNewPopup(args)
    checkForUpdates()

    ' Handle input messages
    input = CreateObject("roInput")
    input.SetMessagePort(m.port)

    device = CreateObject("roDeviceInfo")
    device.setMessagePort(m.port)
    device.EnableScreensaverExitedEvent(true)
    device.EnableAudioGuideChangedEvent(true)
    device.EnableLowGeneralMemoryEvent(true)
    device.EnableCodecCapChangedEvent(true)

    ' Check if we were sent content to play with the startup command (Deep Link)
    onDeepLinkingEvent(args)

    while true
        msg = wait(0, m.port)

        if type(msg) = "roSGScreenEvent" and msg.isScreenClosed()
            return
        end if

        if isNodeEvent(msg, "exit")
            return
        end if

        if isNodeEvent(msg, "jumpTo")
            onJumpToEvent(msg)
        else if isNodeEvent(msg, "closeSidePanel")
            onCloseSidePanelEvent()
        else if isNodeEvent(msg, "quickPlayNode")
            onQuickPlayEvent(msg)
        else if isNodeEvent(msg, "refreshSeasonDetailsData")
            onRefreshSeasonDetailsDataEvent()
        else if isNodeEvent(msg, "refreshMovieDetailsData")
            onRefreshMovieDetailsDataEvent()
        else if isNodeEvent(msg, "refreshEpisodeDetailsData")
            onRefreshEpisodeDetailsDataEvent()
        else if isNodeEvent(msg, "refreshSeriesDetailsData")
            onRefreshSeriesDetailsDataEvent()
        else if isNodeEvent(msg, "selectedItem")
            onSelectedItemEvent(msg)
        else if isNodeEvent(msg, "shuffleSelectedItem")
            onSelectedItemEvent(msg)
        else if isNodeEvent(msg, "movieSelected")
            onMovieSelectedEvent(msg)
        else if isNodeEvent(msg, "seriesSelected")
            onSeriesSelectedEvent(msg)
        else if isNodeEvent(msg, "seasonSelected")
            onSeasonSelectedEvent(msg)
        else if isNodeEvent(msg, "playSeriesFromStart")
            onPlaySeriesFromStartEvent(msg)
        else if isNodeEvent(msg, "playSeasonFromStart")
            onPlaySeasonFromStartEvent(msg)
        else if isNodeEvent(msg, "musicAlbumSelected")
            onMusicAlbumSelectedEvent(msg)
        else if isNodeEvent(msg, "selectedAlbum")
            onSelectedAlbumEvent(msg)
        else if isNodeEvent(msg, "appearsOnSelected")
            onAppearsOnSelectedEvent(msg)
        else if isNodeEvent(msg, "similarArtistSelected")
            onSimilarArtistSelectedEvent(msg)
        else if isNodeEvent(msg, "playSong")
            onPlaySongEvent(msg)
        else if isNodeEvent(msg, "playAlbumSelected")
            onPlayAlbumEvent(msg)
        else if isNodeEvent(msg, "shuffleAlbumSelected")
            onShuffleAlbumEvent(msg)
        else if isNodeEvent(msg, "subtitleToDelete")
            onSubtitleToDeleteEvent(msg)
        else if isNodeEvent(msg, "subtitleSearchButtonSelected")
            onSubtitleSearchButtonSelectedEvent()
        else if isNodeEvent(msg, "subtitleLanguageButtonSelected")
            onSubtitleLanguageButtonSelectedEvent()
        else if isNodeEvent(msg, "playlistItemSelected")
            onPlaylistItemSelectedEvent(msg)
        else if isNodeEvent(msg, "trackSelected")
            onMusicTrackSelectedEvent(msg)
        else if isNodeEvent(msg, "playAll")
            node = msg.getRoSGNode()
            if node.isSubType("MusicTrackListView")
                onMusicTrackPlayAllEvent(msg)
            else
                onPlaylistPlayAllEvent(msg)
            end if
        else if isNodeEvent(msg, "shufflePlay") and (msg.getRoSGNode().isSubType("PlaylistDetails") or msg.getRoSGNode().isSubType("MusicTrackListView"))
            onPlaylistShuffleEvent(msg)
        else if isNodeEvent(msg, "playArtistSelected")
            onPlayArtistSelectedEvent(msg)
        else if isNodeEvent(msg, "instantMixSelected")
            onInstantMixSelectedEvent(msg)
        else if isNodeEvent(msg, "search_value")
            onSearch_valueEvent(msg)
        else if isNodeEvent(msg, "itemSelected")
            node = msg.getRoSGNode()
            if isValid(node) and (node.isSubType("JFOverhang") or node.id = "sidebar")
                onOverhangItemSelectedEvent(msg)
            else
                onItemSelectedEvent(msg)
            end if
        else if isNodeEvent(msg, "buttonSelected")
            onButtonSelectedEvent(msg)
        else if isNodeEvent(msg, "content")
            onContentEvent(msg)
        else if isNodeEvent(msg, "updateAvailable")
            onUpdateAvailableEvent(msg)
        else if isNodeEvent(msg, "wasClosed")
            onUpdateDialogClosed(msg)
        else if isNodeEvent(msg, "userMenuOptionSelected")
            button = msg.getRoSGNode()
            group = sceneManager.callFunc("getActiveScene")
            if isStringEqual(button.id, "change_server")
                unset_setting("server")
                session.server.Delete()
                SignOut(false)
                setUserColors()
                sceneManager.callFunc("clearScenes")
                goto app_start
            else if isStringEqual(button.id, "change_user")
                SignOut(false)
                ' Reset colors to defaults
                setUserColors()
                sceneManager.callFunc("clearScenes")
                goto app_start
            else if isStringEqual(button.id, "sign_out")
                SignOut()
                setUserColors()
                sceneManager.callFunc("clearScenes")
                goto app_start
            end if
        else if isNodeEvent(msg, "state")
            onStateEvent(msg)
        else if type(msg) = "roDeviceInfoEvent"
            onRoDeviceInfoEvent(msg)
        else if type(msg) = "roInputEvent"
            if msg.IsInput()
                info = msg.GetInfo()
                onDeepLinkingEvent(info)
            end if
        else if isNodeEvent(msg, "returnData")
            onReturnDataEvent(msg)
        else if isNodeEvent(msg, "dataReturned")
            onDataReturnedEvent(msg)
        end if
    end while
end sub

sub downloadFallbackFont()
    configEncoding = api.system.GetConfigurationByName("encoding")

    if isChainValid(configEncoding, "EnableFallbackFont")
        if configEncoding.EnableFallbackFont
            re = CreateObject("roRegex", "Name.:.(.*?).,.Size", "s")
            filename = APIRequest("FallbackFont/Fonts").GetToString()
            if isValid(filename)
                filename = re.match(filename)
                if isValidAndNotEmpty(filename)
                    filename = filename[1]
                    APIRequest("FallbackFont/Fonts/" + filename).gettofile("tmp:/font")
                    m.global.fallbackFont = "tmp:/font"
                end if
            end if
        end if
    end if
end sub

sub clearOldLibraryFilters()
    forgetFilters = m.global.session.user.settings["itemgrid.forgetFilters"] ?? true
    if not forgetFilters then return

    for each settingKeys in m.global.session.user.settings.keys()
        if isStringEqual(left(settingKeys, 8), "display.")
            if isStringEqual(right(settingKeys, 7), ".filter") or isStringEqual(right(settingKeys, 14), ".filterOptions")
                m.global.session.user.settings.delete(settingKeys)
                unset_user_setting(settingKeys)
            end if
        end if
    end for
end sub

sub displayWhatsNewPopup(args)
    ' Bypass What's New popup if server is Jellyfin demo
    if isStringEqual(m.global.session.server.url, "https://demo.jellyfin.org/stable")
        set_user_setting("LastRunVersion", m.global.app.version)
    end if

    ' Bypass What's New popup if deep linking arguments were passed
    if isValidAndNotEmpty(args.mediaType) and isValidAndNotEmpty(args.contentId)
        set_user_setting("LastRunVersion", m.global.app.version)
    end if

    ' Has the current user run this version before?
    usersLastRunVersion = m.global.session.user.settings.lastRunVersion
    if ChannelVersionUpdated(usersLastRunVersion)
        set_user_setting("LastRunVersion", m.global.app.version)
        ' show what's new popup
        if m.global.session.user.settings["load.allowwhatsnew"]
            m.global.sceneManager.callFunc("whatsNewDialog")
        end if
    end if
end sub

sub checkForUpdates()
    versionCheckTask = CreateObject("roSGNode", "CheckVersionTask")
    versionCheckTask.currentVersion = m.global.app.version
    versionCheckTask.observeField("updateAvailable", m.port)
    versionCheckTask.control = "RUN"
    m.versionCheckTask = versionCheckTask
end sub

sub onUpdateAvailableEvent(msg as object)
    task = msg.getRoSGNode()

    if task.updateAvailable = true
        dialog = CreateObject("roSGNode", "UpdateNotificationDialog")
        dialog.latestVersion = task.latestVersion
        dialog.currentVersion = task.currentVersion
        dialog.releaseNotes = task.releaseNotes
        dialog.observeField("wasClosed", m.port)

        m.scene.appendChild(dialog)
        m.updateDialog = dialog
    end if
end sub

sub onUpdateDialogClosed(_obj as object)
    if isValid(m.updateDialog)
        m.scene.removeChild(m.updateDialog)
        m.updateDialog = invalid

        group = m.global.sceneManager.callFunc("getActiveScene")
        if isValid(group)
            if isValid(group.lastFocus)
                group.lastFocus.setFocus(true)
            else
                group.setFocus(true)
            end if
        end if
    end if
end sub

sub setUserColors()
    setBackgroundColor()
    setOverhangColors()
end sub

sub setBackgroundColor()
    m.scene.backgroundColor = chainLookupReturn(m.global.session, "user.settings.colorBackground", ColorPalette.VIEWBACKGROUND)
end sub

sub setOverhangColors()
    activeNav = getActiveNav(m.scene)
    if not isValid(activeNav) then return

    overlayCurrentUserSelection = activeNav.findNode("overlayCurrentUserSelection")
    if isValid(overlayCurrentUserSelection)
        overlayCurrentUserSelection.blendColor = chainLookupReturn(m.global.session, "user.settings.colorCursor", ColorPalette.HIGHLIGHT)
    end if

    overlayCurrentUser = activeNav.findNode("overlayCurrentUser")
    if isValid(overlayCurrentUser)
        overlayCurrentUser.color = chainLookupReturn(m.global.session, "user.settings.colorHomeUsername", ColorPalette.WHITE)
    end if
end sub

' Migrate user settings from previous versions.
' Each migration is idempotent â€” safe to run multiple times.
sub MigrateSettings()
    ' colorDialogBorderLine: Fix truncated hex value from old default
    borderColor = get_user_setting("colorDialogBorderLine")
    if isValid(borderColor) and borderColor = "#CCFFF"
        set_user_setting("colorDialogBorderLine", "#CCFFFF")
    end if
end sub
