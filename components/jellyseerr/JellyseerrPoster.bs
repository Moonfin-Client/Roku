import "pkg:/source/enums/ColorPalette.bs"
import "pkg:/source/utils/misc.bs"

sub init()
    m.poster = m.top.findNode("poster")
    m.title = m.top.findNode("title")
    m.backdrop = m.top.findNode("backdrop")
    m.backdrop.color = ColorPalette.DARKGREY
    m.scaleGroup = m.top.findNode("scaleGroup")
    m.focusAnimation = m.top.findNode("focusAnimation")
    m.unfocusAnimation = m.top.findNode("unfocusAnimation")
end sub

sub itemContentChanged()
    itemData = m.top.itemContent

    if not isValid(itemData) then return

    ' Set title from either title or name field
    if isValid(itemData.title)
        m.title.text = itemData.title
    else if isValid(itemData.name)
        m.title.text = itemData.name
    else
        m.title.text = "Unknown"
    end if

    ' Set poster image
    if isValid(itemData.hdPosterUrl)
        m.poster.uri = itemData.hdPosterUrl
        m.backdrop.visible = false
    else if isValid(itemData.sdPosterUrl)
        m.poster.uri = itemData.sdPosterUrl
        m.backdrop.visible = false
    else
        m.backdrop.visible = true
    end if
end sub

sub focusChanged()
    ' Enable/disable scrolling text
    m.title.repeatCount = m.top.itemHasFocus ? - 1 : 0

    ' Handle focus/unfocus animations
    if m.top.itemHasFocus
        if isValid(m.focusAnimation)
            m.unfocusAnimation.control = "stop"
            m.focusAnimation.control = "start"
        end if
    else
        if isValid(m.unfocusAnimation)
            m.focusAnimation.control = "stop"
            m.unfocusAnimation.control = "start"
        end if
    end if
end sub
