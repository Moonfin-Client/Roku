import "pkg:/source/enums/ColorPalette.bs"
import "pkg:/source/enums/KeyCode.bs"
import "pkg:/source/enums/PosterLoadStatus.bs"
import "pkg:/source/enums/TaskControl.bs"
import "pkg:/source/utils/config.bs"
import "pkg:/source/utils/misc.bs"
import "pkg:/source/utils/multiserver.bs"

const SIDEBAR_COLLAPSED_WIDTH = 90
const SIDEBAR_EXPANDED_WIDTH = 350
const SIDEBAR_HEIGHT = 1080
const ICON_SIZE = 40
const ITEM_HEIGHT = 56
const ITEM_GAP = 6
const ITEM_LEFT_PAD = 25
const LABEL_LEFT = 78
const FOCUS_PILL_PAD_H = 5
const FOCUS_PILL_PAD_V = 4
const NAV_START_Y = 100

sub init()
    m.top.id = "sidebar"

    m.bgSolid = m.top.findNode("sidebarBgSolid")
    m.bgEdge = m.top.findNode("sidebarBgEdge")

    m.focusPill = m.top.findNode("focusPill")
    m.focusPillAnim = m.top.findNode("focusPillAnim")
    m.focusPillPosInterp = m.top.findNode("focusPillPosInterp")
    m.focusPillWidthInterp = m.top.findNode("focusPillWidthInterp")

    m.expandAnim = m.top.findNode("expandAnim")
    m.collapseAnim = m.top.findNode("collapseAnim")
    m.bgSolidOpacityExpand = m.top.findNode("bgSolidOpacityExpand")
    m.bgEdgeOpacityExpand = m.top.findNode("bgEdgeOpacityExpand")
    m.bgSolidOpacityCollapse = m.top.findNode("bgSolidOpacityCollapse")
    m.bgEdgeOpacityCollapse = m.top.findNode("bgEdgeOpacityCollapse")

    m.slideUpAnim = m.top.findNode("slideUp")
    m.slideDownAnim = m.top.findNode("slideDown")
    m.sidebarContent = m.top.findNode("sidebarContent")

    m.userAvatar = m.top.findNode("userAvatar")
    m.userHighlight = m.top.findNode("userHighlight")
    m.userName = m.top.findNode("userName")

    m.navItemsGroup = m.top.findNode("navItemsGroup")
    m.navScrollClip = m.top.findNode("navScrollClip")
    m.scrollAnim = m.top.findNode("scrollAnim")
    m.scrollPosInterp = m.top.findNode("scrollPosInterp")
    m.footerSection = m.top.findNode("footerSection")
    m.sidebarClock = m.top.findNode("sidebarClock")

    m.isExpanded = false
    m.scrollY = 0
    m.selectedIndex = 0
    m.avatarSelected = false
    m.sidebarHasFocus = false
    m.firstFocusShow = true
    m.navItems = []
    m.libraryData = []
    m.libraryItems = []
    m.librariesExpanded = false
    m.multiServerTask = invalid
    m.lastNavbarSettings = { shuffle: invalid, favorites: invalid, genres: invalid, libraries: invalid, jellyseerr: invalid }

    m.bgOpacity = 0.88
    m.bgColor = "0x000000FF"

    m.homeItem = createNavItem("home", "pkg:/images/icons/home.png", "Home")
    m.searchItem = createNavItem("search", "pkg:/images/icons/search-light.png", "Search")
    m.shuffleItem = createNavItem("shuffle", "pkg:/images/icons/shuffle.png", "Shuffle", "0xFFFFFF")
    m.favoritesItem = createNavItem("favorites", "pkg:/images/icons/heart.png", "Favorites", "0xFFFFFF")
    m.genresItem = createNavItem("genres", "pkg:/images/icons/masks.png", "Genres", "0xFFFFFF")
    m.jellyseerrItem = createNavItem("jellyseerr", "pkg:/images/icons/jellyseer_jellyfish.png", "Discover")
    m.librariesItem = createNavItem("libraries", "pkg:/images/icons/library.png", "Libraries")
    m.settingsItem = createNavItem("settings", "pkg:/images/icons/settings.png", "Settings")

    loadNavbarSettings()
    m.global.observeField("session", "onSessionChanged")
    m.global.observeField("overlaySettingsChanged", "onOverlaySettingsChanged")
    m.top.observeField("focusedChild", "onFocusChanged")

    rebuildNavItems()
    applyOverlaySettings()
    positionFooter()
end sub

function createNavItem(id as string, iconUri as string, labelText as string, blendColor = "" as string) as object
    btn = createObject("roSGNode", "Group")
    btn.id = id + "Btn"

    icon = createObject("roSGNode", "Poster")
    icon.uri = iconUri
    icon.width = ICON_SIZE
    icon.height = ICON_SIZE
    icon.loadDisplayMode = "scaleToFit"
    icon.translation = [ITEM_LEFT_PAD, (ITEM_HEIGHT - ICON_SIZE) / 2]
    icon.opacity = 0.6
    if blendColor <> "" then icon.blendColor = blendColor
    btn.appendChild(icon)

    label = createObject("roSGNode", "Label")
    label.text = labelText
    label.font = "font:SmallBoldSystemFont"
    label.color = "#FFFFFF"
    label.height = ITEM_HEIGHT
    label.vertAlign = "center"
    label.translation = [LABEL_LEFT, 0]
    label.visible = false
    label.opacity = 0.6
    btn.appendChild(label)

    return { id: id, btn: btn, icon: icon, label: label, isLibChild: false }
end function

function createLibraryChildItem(id as string, labelText as string) as object
    btn = createObject("roSGNode", "Group")
    btn.id = "lib_" + id

    label = createObject("roSGNode", "Label")
    label.text = labelText
    label.font = "font:SmallBoldSystemFont"
    label.color = "#FFFFFF"
    label.height = ITEM_HEIGHT
    label.vertAlign = "center"
    label.translation = [LABEL_LEFT, 0]
    label.visible = false
    label.opacity = 0.5
    btn.appendChild(label)

    dot = createObject("roSGNode", "Poster")
    dot.uri = "pkg:/images/icons/circle.png"
    dot.width = 10
    dot.height = 10
    dot.translation = [ITEM_LEFT_PAD + (ICON_SIZE - 10) / 2, (ITEM_HEIGHT - 10) / 2]
    dot.opacity = 0.4
    btn.appendChild(dot)

    return { id: "lib_" + id, btn: btn, icon: dot, label: label, isLibChild: true }
end function

sub rebuildNavItems()
    m.navItemsGroup.removeChildrenIndex(m.navItemsGroup.getChildCount(), 0)

    m.navItems = []
    m.navItems.push(m.homeItem)
    m.navItems.push(m.searchItem)

    if m.shuffleButtonEnabled then m.navItems.push(m.shuffleItem)
    if m.favoritesButtonEnabled then m.navItems.push(m.favoritesItem)
    if m.genresButtonEnabled then m.navItems.push(m.genresItem)

    jellyseerrEnabled = m.global.session.user.settings["jellyseerr.enabled"]
    if jellyseerrEnabled = true then m.navItems.push(m.jellyseerrItem)

    if m.librariesEnabled then m.navItems.push(m.librariesItem)

    if m.librariesExpanded
        for each libItem in m.libraryItems
            m.navItems.push(libItem)
        end for
    end if

    m.navItems.push(m.settingsItem)

    for each item in m.navItems
        m.navItemsGroup.appendChild(item.btn)
    end for

    if m.selectedIndex >= m.navItems.count() then m.selectedIndex = m.navItems.count() - 1
    if m.selectedIndex < 0 then m.selectedIndex = 0

    layoutSidebar()
end sub

sub layoutSidebar()
    yCursor = 0

    for i = 0 to m.navItems.count() - 1
        item = m.navItems[i]
        isFocused = (not m.avatarSelected and i = m.selectedIndex and m.sidebarHasFocus)

        if isValid(item.icon)
            item.icon.opacity = isFocused ? 1.0 : 0.6
        end if

        if item.isLibChild
            item.label.visible = m.isExpanded
            item.label.opacity = isFocused ? 1.0 : 0.5
        else
            item.label.visible = m.isExpanded
            item.label.opacity = isFocused ? 1.0 : 0.6
        end if

        item.btn.translation = [0, yCursor]
        yCursor += ITEM_HEIGHT + ITEM_GAP
    end for

    if m.avatarSelected
        highlightUser()
    else
        dehighlightUser()
    end if

    scrollToFocusedItem()
    positionFooter()
    positionFocusPill()
end sub

const NAV_VISIBLE_HEIGHT = 830

sub scrollToFocusedItem()
    if m.avatarSelected or m.selectedIndex < 0 or m.selectedIndex >= m.navItems.count() then return

    itemTop = m.selectedIndex * (ITEM_HEIGHT + ITEM_GAP)
    itemBottom = itemTop + ITEM_HEIGHT

    totalContentHeight = m.navItems.count() * (ITEM_HEIGHT + ITEM_GAP)

    ' If all content fits, no scrolling needed
    if totalContentHeight <= NAV_VISIBLE_HEIGHT
        if m.scrollY <> 0
            m.scrollY = 0
            m.navItemsGroup.translation = [0, 0]
        end if
        return
    end if

    targetScroll = m.scrollY
    viewPad = 10

    ' If the focused item is above the visible area, scroll up
    if itemTop + targetScroll < viewPad
        targetScroll = - (itemTop - viewPad)
    end if

    ' If the focused item is below the visible area, scroll down
    if itemBottom + targetScroll > NAV_VISIBLE_HEIGHT - viewPad
        targetScroll = - (itemBottom - NAV_VISIBLE_HEIGHT + viewPad)
    end if

    ' Clamp scroll bounds
    maxScroll = 0
    minScroll = - (totalContentHeight - NAV_VISIBLE_HEIGHT)
    if minScroll > 0 then minScroll = 0
    if targetScroll > maxScroll then targetScroll = maxScroll
    if targetScroll < minScroll then targetScroll = minScroll

    if targetScroll <> m.scrollY
        m.scrollY = targetScroll
        if isValid(m.scrollPosInterp) and isValid(m.scrollAnim)
            currentPos = m.navItemsGroup.translation
            m.scrollPosInterp.keyValue = [[currentPos[0], currentPos[1]], [0, targetScroll]]
            m.scrollAnim.control = "start"
        else
            m.navItemsGroup.translation = [0, targetScroll]
        end if
    end if
end sub

sub positionFooter()
    if m.navItems.count() = 0 then return

    ' Calculate where nav content actually ends (before scrolling)
    totalContentHeight = m.navItems.count() * (ITEM_HEIGHT + ITEM_GAP)
    navEndY = NAV_START_Y + totalContentHeight

    ' If content overflows the scroll area, footer sits below the clip
    maxNavBottom = NAV_START_Y + NAV_VISIBLE_HEIGHT
    if navEndY > maxNavBottom then navEndY = maxNavBottom

    footerY = navEndY + 20
    maxFooterY = SIDEBAR_HEIGHT - 140
    if footerY > maxFooterY then footerY = maxFooterY

    m.footerSection.translation = [0, footerY]
end sub

sub positionFocusPill()
    if m.avatarSelected or not m.sidebarHasFocus
        m.focusPill.visible = false
        return
    end if

    if m.selectedIndex < 0 or m.selectedIndex >= m.navItems.count()
        m.focusPill.visible = false
        return
    end if

    ' Item position within the scroll area, adjusted for scroll offset
    itemY = m.scrollY + (m.selectedIndex * (ITEM_HEIGHT + ITEM_GAP))

    pillX = FOCUS_PILL_PAD_H
    pillY = itemY - FOCUS_PILL_PAD_V
    pillWidth = m.isExpanded ? SIDEBAR_EXPANDED_WIDTH - (FOCUS_PILL_PAD_H * 2) : SIDEBAR_COLLAPSED_WIDTH - (FOCUS_PILL_PAD_H * 2)
    pillHeight = ITEM_HEIGHT + (FOCUS_PILL_PAD_V * 2)

    if m.firstFocusShow or not m.focusPill.visible
        m.focusPill.translation = [pillX, pillY]
        m.focusPill.width = pillWidth
        m.focusPill.height = pillHeight
        m.focusPill.visible = true
        m.firstFocusShow = false
    else
        currentPos = m.focusPill.translation
        currentWidth = m.focusPill.width

        if isValid(m.focusPillPosInterp) and isValid(m.focusPillWidthInterp) and isValid(m.focusPillAnim)
            m.focusPillPosInterp.keyValue = [[currentPos[0], currentPos[1]], [pillX, pillY]]
            m.focusPillWidthInterp.keyValue = [currentWidth, pillWidth]
            m.focusPillAnim.control = "start"
        else
            m.focusPill.translation = [pillX, pillY]
            m.focusPill.width = pillWidth
        end if

        m.focusPill.height = pillHeight
        m.focusPill.visible = true
    end if
end sub

sub expandSidebar()
    if m.isExpanded then return
    m.isExpanded = true

    m.collapseAnim.control = "stop"

    m.bgSolidOpacityExpand.keyValue = [m.bgSolid.opacity, m.bgOpacity]
    m.bgEdgeOpacityExpand.keyValue = [m.bgEdge.opacity, m.bgOpacity]
    m.expandAnim.control = "start"

    m.userName.visible = true
    for each item in m.navItems
        item.label.visible = true
    end for

    positionFocusPill()
    positionFooter()
end sub

sub collapseSidebar()
    if not m.isExpanded then return
    m.isExpanded = false

    m.expandAnim.control = "stop"

    m.bgSolidOpacityCollapse.keyValue = [m.bgSolid.opacity, 0]
    m.bgEdgeOpacityCollapse.keyValue = [m.bgEdge.opacity, 0]
    m.collapseAnim.control = "start"

    m.userName.visible = false
    for each item in m.navItems
        item.label.visible = false
    end for

    positionFocusPill()
    positionFooter()
end sub

sub onFocusChanged()
    if m.top.hasFocus() and isValid(m.navItemsGroup)
        m.navItemsGroup.setFocus(true)
    end if

    hasFocus = m.top.isInFocusChain()
    if hasFocus <> m.sidebarHasFocus
        m.sidebarHasFocus = hasFocus
        group = m.global.sceneManager.callFunc("getActiveScene")
        if hasFocus and isValid(group)
            if group.focusedChild <> invalid
                focused = group.focusedChild
                while focused.hasFocus() = false and focused.focusedChild <> invalid
                    focused = focused.focusedChild
                end while
                if not focused.isSameNode(m.top) and not focused.isSubType("Sidebar")
                    group.lastFocus = focused
                end if
            end if
            expandSidebar()
        else
            collapseSidebar()
            if m.librariesExpanded then collapseLibraries()
        end if
        layoutSidebar()
    end if
end sub

sub loadLibraries()
    m.libraryData = []
    m.libraryItems = []
    m.librariesExpanded = false

    if isMultiServerEnabled()
        sessions = getAllServerSessions()
        if sessions.count() > 0
            loadMultiServerLibraries(sessions)
            return
        end if
    end if

    m.loadLibrariesTask = CreateObject("roSGNode", "LoadLibrariesTask")
    m.loadLibrariesTask.userId = m.global.session.user.id
    m.loadLibrariesTask.observeFieldScoped("result", "onLoadLibrariesComplete")
    m.loadLibrariesTask.control = TaskControl.RUN
end sub

sub onLoadLibrariesComplete(msg as object)
    node = msg.getRoSGNode()
    if not isValid(node) then return
    node.unobserveFieldScoped("result")

    data = node.result
    if not isChainValid(data, "items") then return

    for each item in data.LookupCI("items")
        libData = {
            id: item.LookupCI("Id"),
            name: item.LookupCI("name"),
            type: item.LookupCI("Type"),
            collectionType: item.LookupCI("CollectionType")
        }
        m.libraryData.push(libData)
        m.libraryItems.push(createLibraryChildItem(libData.id, libData.name))
    end for

    rebuildNavItems()
end sub

sub loadMultiServerLibraries(sessions as object)
    if not isValid(m.multiServerTask)
        m.multiServerTask = CreateObject("roSGNode", "MultiServerTask")
    else
        m.multiServerTask.callFunc("reset")
    end if

    m.multiServerTask.sessions = sessions
    m.multiServerTask.endpoint = "/Users/{userId}/Views"
    m.multiServerTask.params = {}
    m.multiServerTask.addFields({ metadata: { multipleServers: (sessions.count() > 1) } })
    m.multiServerTask.observeField("isComplete", "onMultiServerLibrariesLoaded")
    m.multiServerTask.control = TaskControl.RUN
end sub

sub onMultiServerLibrariesLoaded(msg as object)
    node = msg.getRoSGNode()
    if not isValid(node) then return
    node.unobserveField("isComplete")

    results = node.results
    if not isValidAndNotEmpty(results) then return

    uniqueServers = {}
    for each item in results
        if isValidAndNotEmpty(item._serverId) then uniqueServers[item._serverId] = true
    end for
    multipleServers = (uniqueServers.count() > 1)

    allLibs = []
    for each item in results
        if not isValidAndNotEmpty(item.Id) or not isValidAndNotEmpty(item.Name) then continue for

        serverSuffix = ""
        if multipleServers and isValidAndNotEmpty(item._serverName)
            serverSuffix = " (" + item._serverName + ")"
        end if

        allLibs.push({
            id: item.Id, name: item.Name,
            type: item.LookupCI("Type"), collectionType: item.LookupCI("CollectionType"),
            serverUrl: item._serverUrl, userId: item._userId, authToken: item._authToken,
            serverId: item._serverId, serverName: item._serverName, serverSuffix: serverSuffix
        })
    end for

    sortedLibs = sortLibrariesAlphabetically(allLibs)
    for each lib in sortedLibs
        m.libraryData.push(lib)
        displayName = lib.name
        if not isValidAndNotEmpty(displayName) then displayName = "Library"
        displayName += lib.serverSuffix
        m.libraryItems.push(createLibraryChildItem(lib.id, displayName))
    end for

    rebuildNavItems()
end sub

function sortLibrariesAlphabetically(libraries as object) as object
    if libraries.count() <= 1 then return libraries
    sorted = []
    for each lib in libraries
        sorted.push(lib)
    end for
    for i = 0 to sorted.count() - 2
        for j = 0 to sorted.count() - i - 2
            n1 = LCase(sorted[j].name + sorted[j].serverSuffix)
            n2 = LCase(sorted[j + 1].name + sorted[j + 1].serverSuffix)
            if n1 > n2
                temp = sorted[j]
                sorted[j] = sorted[j + 1]
                sorted[j + 1] = temp
            end if
        end for
    end for
    return sorted
end function

sub expandLibraries()
    if m.librariesExpanded or m.libraryItems.count() = 0 then return
    m.librariesExpanded = true
    rebuildNavItems()
end sub

sub collapseLibraries()
    if not m.librariesExpanded then return
    m.librariesExpanded = false
    rebuildNavItems()
end sub

sub manageLibraryExpansion()
    if not m.librariesEnabled or m.libraryItems.count() = 0 then return
    if m.avatarSelected
        if m.librariesExpanded then collapseLibraries()
        return
    end if

    libIdx = -1
    for i = 0 to m.navItems.count() - 1
        if m.navItems[i].id = "libraries"
            libIdx = i
            exit for
        end if
    end for
    if libIdx < 0 then return

    inLibArea = (m.selectedIndex >= libIdx and m.selectedIndex < m.navItems.count() - 1)

    if inLibArea and not m.librariesExpanded
        expandLibraries()
    else if not inLibArea and m.librariesExpanded
        collapseLibraries()
    end if
end sub

sub loadNavbarSettings()
    m.shuffleButtonEnabled = parseBoolSetting(m.global.session.user.settings["navbar.show_shuffle"], m.global.session.user.settings["show_shuffle_button"], false)
    m.favoritesButtonEnabled = parseBoolSetting(m.global.session.user.settings["navbar.show_favorites"], invalid, false)
    m.genresButtonEnabled = parseBoolSetting(m.global.session.user.settings["navbar.show_genres"], invalid, false)
    m.librariesEnabled = parseBoolSetting(m.global.session.user.settings["navbar.show_libraries"], invalid, false)

    m.lastNavbarSettings = {
        shuffle: m.global.session.user.settings["navbar.show_shuffle"],
        favorites: m.global.session.user.settings["navbar.show_favorites"],
        genres: m.global.session.user.settings["navbar.show_genres"],
        libraries: m.global.session.user.settings["navbar.show_libraries"],
        jellyseerr: m.global.session.user.settings["jellyseerr.enabled"]
    }
end sub

function parseBoolSetting(primary as dynamic, fallback = invalid as dynamic, defaultVal = false as boolean) as boolean
    val = primary
    if not isValid(val) then val = fallback
    if not isValid(val) then return defaultVal

    if type(val) = "String" or type(val) = "roString"
        return (LCase(val) = "true")
    else if type(val) = "Integer" or type(val) = "roInt"
        return (val = 1)
    end if
    return (val = true)
end function

sub onSessionChanged()
    currentSettings = {
        shuffle: m.global.session.user.settings["navbar.show_shuffle"],
        favorites: m.global.session.user.settings["navbar.show_favorites"],
        genres: m.global.session.user.settings["navbar.show_genres"],
        libraries: m.global.session.user.settings["navbar.show_libraries"],
        jellyseerr: m.global.session.user.settings["jellyseerr.enabled"]
    }
    if currentSettings.shuffle <> m.lastNavbarSettings.shuffle or currentSettings.favorites <> m.lastNavbarSettings.favorites or currentSettings.genres <> m.lastNavbarSettings.genres or currentSettings.libraries <> m.lastNavbarSettings.libraries or currentSettings.jellyseerr <> m.lastNavbarSettings.jellyseerr
        loadNavbarSettings()
        rebuildNavItems()
    end if
    applyOverlaySettings()
end sub

sub onOverlaySettingsChanged(_event as object)
    applyOverlaySettings()
end sub

sub applyOverlaySettings()
    if not isValid(m.global) or not isValid(m.global.session) then return

    opacity = 0.88
    if isValid(m.global.session.user) and isValid(m.global.session.user.settings)
        opacitySetting = m.global.session.user.settings["ui.overlayOpacity"]
        if isValid(opacitySetting)
            opacity = opacitySetting.toFloat()
            if opacity < 0.5 then opacity = 0.5
        end if
    end if

    color = "0x000000FF"
    if isValid(m.global.session.user) and isValid(m.global.session.user.settings)
        colorSetting = m.global.session.user.settings["ui.overlayColor"]
        if isValid(colorSetting) and colorSetting <> "" then color = colorSetting + "FF"
    end if

    m.bgOpacity = opacity
    m.bgColor = color

    m.bgSolid.color = color
    m.bgEdge.blendColor = color

    if m.isExpanded
        m.bgSolid.opacity = m.bgOpacity
        m.bgEdge.opacity = m.bgOpacity
    end if
end sub

sub updateShuffleButtonVisibility()
    rebuildNavItems()
end sub

sub onVisibleChange()
    if m.top.disableMoveAnimation
        if isValid(m.sidebarContent) then m.sidebarContent.translation = [0, 0]
        return
    end if
    if m.top.isVisible
        m.slideDownAnim.control = "start"
    else
        m.slideUpAnim.control = "start"
    end if
end sub

sub updateUser()
    if isValid(m.userName) then m.userName.text = m.top.currentUser
end sub

sub updateUserProfileImage()
    m.userAvatar.observeField("loadStatus", "onPosterLoadStatusChanged")
    if isValid(m.userAvatar) then m.userAvatar.uri = m.top.currentUserProfileImage
end sub

sub onPosterLoadStatusChanged()
    if m.userAvatar.loadStatus <> PosterLoadStatus.LOADING
        m.userAvatar.unobserveField("loadStatus")
    end if
    if m.userAvatar.loadStatus = PosterLoadStatus.FAILED
        if m.userAvatar.loadWidth = 0 then m.userAvatar.uri = "pkg:/images/baseline_person_white_48dp.png"
    end if
end sub

sub highlightUser()
    if isValid(m.userHighlight)
        m.userHighlight.visible = true
        m.userHighlight.blendColor = chainLookupReturn(m.global.session, "user.settings.colorCursor", ColorPalette.HIGHLIGHT)
    end if
end sub

sub dehighlightUser()
    if isValid(m.userHighlight)
        m.userHighlight.visible = false
    end if
end sub

function onKeyEvent(key as string, press as boolean) as boolean
    if not press then return false

    maxIndex = m.navItems.count() - 1

    if key = KeyCode.UP
        if m.avatarSelected
            return true
        else if m.selectedIndex = 0
            m.avatarSelected = true
            m.selectedIndex = -1
            manageLibraryExpansion()
            layoutSidebar()
            return true
        else
            m.selectedIndex = m.selectedIndex - 1
            manageLibraryExpansion()
            layoutSidebar()
            return true
        end if

    else if key = KeyCode.DOWN
        if m.avatarSelected
            m.avatarSelected = false
            m.selectedIndex = 0
            layoutSidebar()
            return true
        else if m.selectedIndex < maxIndex
            m.selectedIndex = m.selectedIndex + 1
            manageLibraryExpansion()
            layoutSidebar()
            return true
        end if

    else if key = KeyCode.RIGHT
        return handleRightKey()

    else if key = KeyCode.OK
        return handleOKKey()
    end if

    return false
end function

function handleRightKey() as boolean
    group = m.global.sceneManager.callFunc("getActiveScene")
    if isValid(group) and isValid(group.lastFocus)
        dehighlightUser()
        group.lastFocus.setFocus(true)
        return true
    end if
    return false
end function

function handleOKKey() as boolean
    if m.avatarSelected
        group = m.global.sceneManager.callFunc("getActiveScene")
        panel = group.findNode("options")
        if isValid(panel)
            group.lastFocus = m.top
            panel.visible = true
            panel.findNode("panelList").setFocus(true)
        end if
        dehighlightUser()
        return true
    end if

    if m.selectedIndex < 0 or m.selectedIndex >= m.navItems.count() then return false

    item = m.navItems[m.selectedIndex]

    if item.id = "home"
        while m.global.sceneManager.callFunc("getSceneCount") > 1
            m.global.sceneManager.callFunc("popScene")
        end while
        m.top.itemSelected = "home"
        return true

    else if item.id = "search"
        m.top.itemSelected = "search"
        return true

    else if item.id = "shuffle"
        m.top.shufflePressed = not m.top.shufflePressed
        return true

    else if item.id = "favorites"
        m.top.favoritesPressed = not m.top.favoritesPressed
        return true

    else if item.id = "genres"
        m.top.genresPressed = not m.top.genresPressed
        return true

    else if item.id = "jellyseerr"
        jellyseerrDiscovery = CreateObject("roSGNode", "JellyseerrDiscoveryScreen")
        m.global.sceneManager.callFunc("pushScene", jellyseerrDiscovery)
        return true

    else if item.id = "settings"
        m.global.sceneManager.callFunc("settings")
        return true

    else if item.id = "libraries"
        if m.librariesExpanded
            collapseLibraries()
        else
            expandLibraries()
            if m.libraryItems.count() > 0
                m.selectedIndex = m.selectedIndex + 1
            end if
            layoutSidebar()
        end if
        return true

    else if item.isLibChild
        libIndex = getLibraryDataIndex(item.id)
        if libIndex >= 0 and libIndex < m.libraryData.count()
            libInfo = m.libraryData[libIndex]
            libraryNode = createObject("roSGNode", "LibraryData")
            libraryNode.id = libInfo.id
            libraryNode.libraryId = libInfo.id
            libraryNode.title = libInfo.name
            libraryNode.collectionType = libInfo.collectionType

            if isValidAndNotEmpty(libInfo.serverUrl)
                libraryNode.serverUrl = libInfo.serverUrl
                libraryNode.userId = libInfo.userId
                libraryNode.authToken = libInfo.authToken
                libraryNode.serverId = libInfo.serverId
                libraryNode.serverName = libInfo.serverName
            end if

            m.top.librarySelected = libraryNode
            return true
        end if
    end if

    return false
end function

function getLibraryDataIndex(itemId as string) as integer
    actualId = itemId.replace("lib_", "")
    for i = 0 to m.libraryData.count() - 1
        if m.libraryData[i].id = actualId then return i
    end for
    return -1
end function


