import "pkg:/source/utils/misc.bs"

sub init()
    m.top.functionName = "checkVersion"
end sub

sub checkVersion()
    currentVersion = m.top.currentVersion
    if not isValidAndNotEmpty(currentVersion) then return

    url = "https://api.github.com/repos/Moonfin-Client/Roku/releases/latest"

    request = CreateObject("roUrlTransfer")
    request.SetUrl(url)
    request.SetCertificatesFile("common:/certs/ca-bundle.crt")
    request.InitClientCertificates()
    request.AddHeader("Accept", "application/vnd.github+json")
    request.AddHeader("User-Agent", "Moonfin-Roku-Client")

    port = CreateObject("roMessagePort")
    request.SetPort(port)

    if request.AsyncGetToString()
        msg = wait(10000, port)

        if type(msg) = "roUrlEvent"
            if msg.GetResponseCode() = 200
                responseString = msg.GetString()
                latestRelease = ParseJson(responseString)

                if isValid(latestRelease) and isValid(latestRelease.tag_name)
                    latestVersion = latestRelease.tag_name

                    if left(latestVersion, 1) = "v"
                        latestVersion = mid(latestVersion, 2)
                    end if

                    m.top.latestVersion = latestVersion

                    if isValid(latestRelease.html_url)
                        m.top.releaseUrl = latestRelease.html_url
                    end if

                    if isValid(latestRelease.body)
                        m.top.releaseNotes = latestRelease.body
                    end if

                    if compareVersions(currentVersion, latestVersion) < 0
                        m.top.updateAvailable = true
                    else
                        m.top.updateAvailable = false
                    end if
                end if
            end if
        end if
    end if
end sub

function compareVersions(current as string, latest as string) as integer
    currentParts = current.split(".")
    latestParts = latest.split(".")

    maxParts = currentParts.count()
    if latestParts.count() > maxParts then maxParts = latestParts.count()

    for i = 0 to maxParts - 1
        currentPart = 0
        latestPart = 0

        if i < currentParts.count()
            currentPart = val(currentParts[i])
        end if

        if i < latestParts.count()
            latestPart = val(latestParts[i])
        end if

        if currentPart < latestPart
            return -1
        else if currentPart > latestPart
            return 1
        end if
    end for

    return 0
end function
