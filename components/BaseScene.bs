import "pkg:/source/enums/ColorPalette.bs"
import "pkg:/source/enums/KeyCode.bs"
import "pkg:/source/utils/misc.bs"

sub init()
    m.top.backgroundURI = ""
    m.spinner = m.top.findNode("spinner")
end sub

' Triggered when the isLoading boolean component field is changed
sub isLoadingChanged()
    m.spinner.visible = m.top.isLoading
end sub

sub onJumpToChange()
    if not isValidAndNotEmpty(m.top.jumpTo) then return
    if not isValid(m.top.jumpTo.selectiontype) then return

    m.global.jumpTo = m.top.jumpTo
    m.top.jumpTo = {}
end sub

' Triggered when the disableRemote boolean component field is changed
sub disableRemoteChanged()
    if m.top.disableRemote
        dialog = createObject("roSGNode", "ProgressDialog")
        dialog.id = "invisibiledialog"
        dialog.visible = false
        dialog.opacity = 0
        m.top.dialog = dialog
    else
        if isValid(m.top.dialog)
            m.top.dialog.close = true
        end if
    end if
end sub

function onKeyEvent(key as string, press as boolean) as boolean
    if not press then return false

    if key = "back"
        m.global.sceneManager.callFunc("popScene")
        return true
    end if

    if key = "options"
        group = m.global.sceneManager.callFunc("getActiveScene")
        if isValid(group) and isValid(group.optionsAvailable) and group.optionsAvailable
            group.lastFocus = group.focusedChild

            scene = m.top.getScene()
            if isValid(scene)
                activeNav = getActiveNav(scene)
                if isValid(activeNav)
                    overlayCurrentUserSelection = activeNav.findNode("overlayCurrentUserSelection")
                    if isValid(overlayCurrentUserSelection) and overlayCurrentUserSelection.visible
                        group.lastFocus = activeNav
                    end if
                end if
            end if

            if group.isSubType("Home") then return true

            panel = group.findNode("options")
            panel.visible = true
            panel.findNode("panelList").setFocus(true)

        end if
        return true
    end if

    if key = KeyCode.REPLAY
        focusAudioMiniPlayer()
        return true
    end if

    if key = KeyCode.LEFT
        scene = m.top.getScene()
        if isValid(scene)
            sidebar = scene.findNode("sidebar")
            if isValid(sidebar) and sidebar.visible
                sidebar.setFocus(true)
                return true
            end if
        end if
    end if

    return false
end function
