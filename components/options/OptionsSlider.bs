import "pkg:/source/enums/KeyCode.bs"
import "pkg:/source/utils/misc.bs"

sub init()
    m.top.visible = false

    m.panel = m.top.findNode("panel")
    m.panel.panelSize = "small"
    m.panel.focusable = true
    m.panel.hasNextPanel = false
    m.panel.leftOnly = true

    positionPanel()

    list = m.top.findNode("panelList")
    m.panel.list = list

    m.top.observeField("visible", "onVisibleChanged")
end sub

sub positionPanel()
    scene = m.top.getScene()
    sidebar = invalid
    if isValid(scene) then sidebar = scene.findNode("sidebar")

    if isValid(sidebar) and sidebar.visible
        m.panel.leftPosition = 350
    else
        m.panel.leftPosition = 1250
    end if
end sub

sub onVisibleChanged()
    if m.top.visible then positionPanel()
end sub

sub setFields()
    options = m.top.options
    buttons = m.top.buttons
    row = m.top.findNode("fieldList")

    row.clear()
    row.appendChildren(options)
    row.appendChildren(buttons)
end sub

function onKeyEvent(key as string, press as boolean) as boolean
    if not press then return false

    if key = "options" or key = "back" or key = KeyCode.LEFT
        m.top.visible = false
        m.top.closeSidePanel = true
        return true
    else if key = "OK"
        list = m.top.findNode("panelList")
        data = list.content.getChild(list.itemFocused)
        data.userMenuOptionSelected = true
        return true
    end if

    return false
end function
