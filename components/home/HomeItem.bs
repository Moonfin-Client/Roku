import "pkg:/source/api/baserequest.bs"
import "pkg:/source/api/Image.bs"
import "pkg:/source/enums/AnimationControl.bs"
import "pkg:/source/enums/ColorPalette.bs"
import "pkg:/source/enums/ItemType.bs"
import "pkg:/source/enums/PosterLoadStatus.bs"
import "pkg:/source/enums/SeriesStatus.bs"
import "pkg:/source/enums/String.bs"
import "pkg:/source/utils/config.bs"
import "pkg:/source/utils/misc.bs"

sub init()
    findNodes()
    m.itemPoster.observeField("loadStatus", "onPosterLoadStatusChanged")
    setColors()

    ' Track focus change timing to detect and prevent oscillations
    m.lastFocusChangeTime = 0
    m.focusChangeCount = 0
    m.isInOscillation = false
    m.lastStableFocusState = false
    m.initTime = UpTime(0)
    m.initCooldownPeriod = 0.8 ' Block animations for 800ms after creation (increased to handle focus restoration)
end sub

sub setColors()
    if not isAllValid([m.itemProgress, m.itemProgressBackground, m.backdrop]) then findNodes()

    if isValid(m.itemProgress)
        m.itemProgress.color = chainLookupReturn(m.global.session, "user.settings.colorHomeRowItemProgress", ColorPalette.TRIADBLUE)
    end if

    if isValid(m.itemProgressBackground)
        m.itemProgressBackground.color = chainLookupReturn(m.global.session, "user.settings.colorHomeRowItemProgressBackground", ColorPalette.BLACK90)
    end if

    if isValid(m.backdrop)
        m.backdrop.color = chainLookupReturn(m.global.session, "user.settings.colorHomeRowItemBackground", ColorPalette.ELEMENTBACKGROUND)
    end if
end sub

sub findNodes()
    initItemText()
    initItemPoster()
    initBackdrop()
    initItemIcon()
    m.itemProgress = m.top.findNode("progress")
    m.itemProgressBackground = m.top.findNode("progressBackground")
    m.playedIndicator = m.top.findNode("playedIndicator")
    m.showProgressBarAnimation = m.top.findNode("showProgressBar")
    m.showProgressBarField = m.top.findNode("showProgressBarField")
    m.scaleGroup = m.top.findNode("scaleGroup")
    m.focusAnimation = m.top.findNode("focusAnimation")
    m.unfocusAnimation = m.top.findNode("unfocusAnimation")
end sub

sub initItemText()
    m.itemText = m.top.findNode("itemText")
    if isValid(m.itemText)
        m.itemText.color = chainLookupReturn(m.global.session, "user.settings.colorHomeRowItemTitle", ColorPalette.WHITE)
    end if
end sub

sub initItemPoster()
    m.itemPoster = m.top.findNode("itemPoster")
end sub

sub initBackdrop()
    m.backdrop = m.top.findNode("backdrop")
end sub

sub initItemIcon()
    m.itemIcon = m.top.findNode("itemIcon")
    if not isValid(m.itemIcon) then return

    m.itemIcon.blendcolor = chainLookupReturn(m.global.session, "user.settings.colorHomeMyListIcon", ColorPalette.WHITE)
end sub

sub itemContentChanged()
    itemData = m.top.itemContent

    if not isValid(itemData) then return
    localGlobal = m.global

    if isValid(m.playedIndicator)
        m.playedIndicator.data = {
            played: chainLookupReturn(itemData, "json.UserData.Played", false),
            unplayedCount: chainLookupReturn(itemData, "json.UserData.UnplayedItemCount", 0)
        }
    end if

    itemData.AddReplace("Title", itemData.LookupCI("name"))

    setColors()
    initItemIcon()
    initItemText()

    ' validate to prevent crash
    if not isValid(m.itemPoster) then initItemPoster()
    if not isValid(m.backdrop) then initBackdrop()
    if not isValid(m.itemIcon) then initItemIcon()

    m.itemPoster.width = itemData.imageWidth

    m.itemText.maxWidth = itemData.imageWidth
    m.itemText.height = 34
    m.itemText.font.size = 25
    m.itemText.horizAlign = "left"
    m.itemText.vertAlign = "bottom"

    m.backdrop.width = itemData.imageWidth

    if isAllValid([m.itemIcon, itemData.iconUrl])
        m.itemIcon.uri = itemData.iconUrl
    end if

    itemDataType = isValid(itemData.type) ? LCase(itemData.type) : ""

    if inArray([ItemType.COLLECTIONFOLDER, ItemType.USERVIEW, ItemType.CHANNEL, ItemType.FOLDER], itemDataType)
        displayCollectionInfo(itemData)
        return
    end if

    ' "Program" is from clicking on an "On Now" item on the Home Screen
    if itemDataType = ItemType.PROGRAM
        displayProgramInfo(itemData)
        return
    end if

    if inArray([ItemType.EPISODE, ItemType.RECORDING], itemDataType)
        displayEpisodeInfo(localGlobal, itemData)
        return
    end if

    if inArray([ItemType.MOVIE, ItemType.MUSICVIDEO], itemDataType)
        displayMovieInfo(itemData)
        return
    end if

    if itemDataType = ItemType.VIDEO
        displayVideoInfo(itemData)
        return
    end if

    if itemDataType = ItemType.BOXSET
        displayBoxsetInfo(itemData)
        return
    end if

    if itemDataType = ItemType.SERIES
        displaySeriesInfo(itemData)
        return
    end if

    if itemDataType = ItemType.MUSICALBUM
        displayMusicAlbumInfo(itemData as object)
        return
    end if

    if itemDataType = ItemType.AUDIOBOOK
        displayAudioBookInfo(itemData as object)
        return
    end if

    if inArray([ItemType.MUSICARTIST, ItemType.AUDIO], itemDataType)
        displayAudioInfo(itemData as object)
        return
    end if

    if itemDataType = ItemType.TVCHANNEL
        displayTVChannelInfo(itemData as object)
        return
    end if

    if itemDataType = ItemType.SEASON
        displaySeasonInfo(itemData as object)
        return
    end if

    if itemDataType = ItemType.PHOTO
        displayPhotoInfo(itemData as object)
        return
    end if

    if itemDataType = ItemType.PHOTOALBUM
        displayPhotoAlbumInfo(itemData as object)
        return
    end if

end sub

sub displayCollectionInfo(itemData as object)
    m.itemText.text = itemData.name
    if isValidAndNotEmpty(itemData.iconUrl)
        m.itemIcon.uri = itemData.iconUrl
    end if

    if isValidAndNotEmpty(itemData.widePosterURL)
        m.itemPoster.uri = itemData.widePosterURL
    else
        m.itemPoster.uri = itemData.posterURL
    end if
end sub

sub displayPhotoAlbumInfo(itemData as object)
    m.itemText.text = itemData.name
    m.itemPoster.uri = ImageURL(itemData.LookupCI("id"))
end sub

sub displayPhotoInfo(itemData as object)
    m.itemText.text = itemData.name
    m.itemPoster.uri = ImageURL(itemData.LookupCI("id"))
end sub

sub displaySeasonInfo(itemData as object)
    m.itemText.text = itemData.json.SeriesName
    m.itemPoster.uri = ImageURL(itemData.LookupCI("id"))
end sub

sub displayTVChannelInfo(itemData as object)
    m.itemText.text = itemData.name
    m.itemPoster.uri = ImageURL(itemData.LookupCI("id"))
end sub

sub displayAudioInfo(itemData as object)
    m.itemText.text = itemData.name
    m.itemPoster.uri = ImageURL(itemData.LookupCI("id"))
end sub

sub displayAudioBookInfo(itemData as object)
    if itemData.PlayedPercentage > 0
        drawProgressBar(itemData)
    end if

    m.itemText.text = itemData.name
    m.itemPoster.uri = ImageURL(itemData.LookupCI("id"))
end sub

sub displayMusicAlbumInfo(itemData as object)
    m.itemText.text = itemData.name
    m.itemPoster.uri = itemData.posterURL
end sub

sub displaySeriesInfo(itemData as object)
    m.itemText.text = itemData.name

    ' Prioritize primary image (posterURL) first, then fall back to thumbnail/widePoster
    if isValidAndNotEmpty(itemData.posterURL)
        m.itemPoster.uri = itemData.posterURL
    else if isValidAndNotEmpty(itemData.thumbnailURL)
        m.itemPoster.uri = itemData.thumbnailURL
    else if isValidAndNotEmpty(itemData.widePosterURL)
        m.itemPoster.uri = itemData.widePosterURL
    end if
end sub

sub displayBoxsetInfo(itemData as object)
    m.itemText.text = itemData.name
    m.itemPoster.uri = itemData.posterURL
end sub

sub displayVideoInfo(itemData as object)
    m.itemText.text = itemData.name

    if itemData.PlayedPercentage > 0
        drawProgressBar(itemData)
    end if

    ' For videos, prioritize thumbnail first, then fall back to primary
    if isValidAndNotEmpty(itemData.thumbnailURL)
        m.itemPoster.uri = itemData.thumbnailURL
    else if isValidAndNotEmpty(itemData.posterURL)
        m.itemPoster.uri = itemData.posterURL
    end if
end sub

sub displayMovieInfo(itemData as object)
    m.itemText.text = itemData.name

    if itemData.PlayedPercentage > 0
        drawProgressBar(itemData)
    end if

    ' If usePoster is set (e.g. Recently Added), show primary poster first
    ' Otherwise (e.g. Continue Watching), show thumbnail/backdrop first
    if itemData.usePoster = true
        if isValidAndNotEmpty(itemData.posterURL)
            m.itemPoster.uri = itemData.posterURL
        else if isValidAndNotEmpty(itemData.thumbnailURL)
            m.itemPoster.uri = itemData.thumbnailURL
        end if
    else
        if isValidAndNotEmpty(itemData.thumbnailURL)
            m.itemPoster.uri = itemData.thumbnailURL
        else if isValidAndNotEmpty(itemData.posterURL)
            m.itemPoster.uri = itemData.posterURL
        end if
    end if
end sub

sub displayProgramInfo(itemData as object)
    m.itemText.Text = itemData.json.name
    m.itemPoster.uri = itemData.usePoster ? itemData.thumbnailURL : ImageURL(itemData.json.ChannelId)
    m.itemPoster.loadDisplayMode = "scaleToFill"
end sub

sub displayEpisodeInfo(_localGlobal as object, itemData as object)
    ' Format: "Show Name - S1E2 Episode Title"
    titleText = ""

    if isChainValid(itemData, "json.SeriesName")
        titleText = itemData.json.SeriesName
    end if

    ' Add Season and Episode Number
    episodeInfo = ""
    if isChainValid(itemData, "json.ParentIndexNumber")
        episodeInfo = "S" + StrI(itemData.json.ParentIndexNumber).trim()
    end if

    if isChainValid(itemData, "json.IndexNumber")
        episodeInfo = episodeInfo + "E" + StrI(itemData.json.IndexNumber).trim()
    end if

    if episodeInfo.len() > 0
        titleText = titleText + " - " + episodeInfo
    end if

    ' Add Episode Name
    if isValidAndNotEmpty(itemData.LookupCI("name"))
        titleText = titleText + " " + itemData.LookupCI("name")
    end if

    m.itemText.text = titleText

    if isValid(itemData.LookupCI("PlayedPercentage"))
        if itemData.LookupCI("PlayedPercentage") > 0
            drawProgressBar(itemData)
        end if
    end if

    ' If usePoster is set (e.g. Next Up), show series poster first
    ' Otherwise (e.g. Continue Watching), show episode screenshot first
    if itemData.usePoster = true
        if isValidAndNotEmpty(itemData.LookupCI("posterURL"))
            m.itemPoster.uri = itemData.LookupCI("posterURL")
        else if isValidAndNotEmpty(itemData.LookupCI("widePosterURL"))
            m.itemPoster.uri = itemData.LookupCI("widePosterURL")
        else if isValidAndNotEmpty(itemData.LookupCI("thumbnailURL"))
            m.itemPoster.uri = itemData.LookupCI("thumbnailURL")
        end if
    else
        if isValidAndNotEmpty(itemData.LookupCI("thumbnailURL"))
            m.itemPoster.uri = itemData.LookupCI("thumbnailURL")
        else if isValidAndNotEmpty(itemData.LookupCI("widePosterURL"))
            m.itemPoster.uri = itemData.LookupCI("widePosterURL")
        else if isValidAndNotEmpty(itemData.LookupCI("posterURL"))
            m.itemPoster.uri = itemData.LookupCI("posterURL")
        end if
    end if
end sub

'
' Draws and animates item progress bar
sub drawProgressBar(itemData)
    if not isValid(itemData.LookupCI("imageWidth")) then return

    m.itemProgressBackground.width = itemData.LookupCI("imageWidth")
    m.itemProgressBackground.visible = true
    m.showProgressBarField.keyValue = [0, m.itemPoster.width * (itemData.PlayedPercentage / 100)]
    m.showProgressBarAnimation.control = AnimationControl.START
end sub

'
' Enable title scrolling based on item Focus
sub focusChanged()
    currentTime = UpTime(0)

    ' Block ALL animations during initialization cooldown
    timeSinceInit = currentTime - m.initTime
    if timeSinceInit < m.initCooldownPeriod
        ' Still in cooldown - just update text scrolling and exit
        m.itemText.repeatCount = m.top.itemHasFocus ? - 1 : 0
        return
    end if

    timeSinceLastChange = currentTime - m.lastFocusChangeTime

    ' Detect rapid focus oscillation (multiple changes within 200ms)
    if timeSinceLastChange < 0.2
        m.focusChangeCount++
        if m.focusChangeCount >= 2
            m.isInOscillation = true
        end if
    else
        ' Reset counter if enough time has passed
        m.focusChangeCount = 0
        if m.isInOscillation
            ' Exiting oscillation - save the stable state
            m.lastStableFocusState = m.top.itemHasFocus
        end if
        m.isInOscillation = false
    end if

    m.lastFocusChangeTime = currentTime

    ' Always update text scrolling
    m.itemText.repeatCount = m.top.itemHasFocus ? - 1 : 0

    ' Set scale animation center point
    if isValid(m.scaleGroup)
        m.scaleGroup.scaleRotateCenter = [90, 158]
    end if

    ' Only animate if NOT in oscillation
    if not m.isInOscillation
        ' Save stable state before animating
        m.lastStableFocusState = m.top.itemHasFocus

        ' Play focus/unfocus animation
        if m.top.itemHasFocus
            if isValid(m.unfocusAnimation) then m.unfocusAnimation.control = "stop"
            if isValid(m.focusAnimation) then m.focusAnimation.control = "start"
        else
            if isValid(m.focusAnimation) then m.focusAnimation.control = "stop"
            if isValid(m.unfocusAnimation) then m.unfocusAnimation.control = "start"
        end if
    end if
    ' During oscillation, do nothing - keep whatever scale we had from last stable state
    ' This prevents the visual bounce
end sub'Hide backdrop and icon when poster loaded
sub onPosterLoadStatusChanged()
    if m.itemPoster.loadStatus = PosterLoadStatus.FAILED
        thisItemType = chainLookupReturn(m.top.itemContent, "json.type", string.EMPTY)

        ' If loading the album poster image failed, try loading the artist image
        if isStringEqual(thisItemType, ItemType.MUSICALBUM)
            params = {
                tag: m.top.itemContent.json.parentbackdropimagetags,
                maxHeight: 261,
                maxWidth: 261
            }
            artistPrimaryImageURL = ImageURL(m.top.itemContent.json.parentbackdropitemid, "Primary", params)

            ' Check if we've already tried to fall back to the artist image
            if isStringEqual(m.itemPoster.uri, artistPrimaryImageURL) then return

            m.itemPoster.uri = artistPrimaryImageURL
            return
        end if

        ' If loading the episode poster image failed, try loading the series image
        if isStringEqual(thisItemType, ItemType.EPISODE)
            params = {
                tag: m.top.itemContent.json.parentbackdropimagetags,
                maxHeight: 261,
                maxWidth: 261
            }
            seriesPrimaryImageURL = ImageURL(m.top.itemContent.json.parentbackdropitemid, "Primary", params)

            ' Check if we've already tried to fall back to the series image
            if isStringEqual(m.itemPoster.uri, seriesPrimaryImageURL) then return

            m.itemPoster.uri = seriesPrimaryImageURL
            return
        end if

        ' Wide Poster failed to load, try poster url
        if isStringEqual(m.itemPoster.uri, m.top.itemContent.widePosterURL)
            m.itemPoster.uri = m.top.itemContent.posterURL
            return
        end if

        ' Thumbnail failed to load, try poster url
        if isStringEqual(m.itemPoster.uri, m.top.itemContent.thumbnailURL)
            m.itemPoster.uri = m.top.itemContent.posterURL
            return
        end if

        ' Poster url has failed. Show blank backdrop
        m.backdrop.visible = true
        m.itemIcon.visible = true

        return
    end if

    if m.itemPoster.loadStatus = PosterLoadStatus.READY and m.itemPoster.uri <> string.EMPTY
        m.backdrop.visible = false
        m.itemIcon.visible = false
    else
        m.backdrop.visible = true
        m.itemIcon.visible = true
    end if
end sub
