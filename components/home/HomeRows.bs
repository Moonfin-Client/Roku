import "pkg:/source/constants/HomeRowItemSizes.bs"
import "pkg:/source/enums/KeyCode.bs"
import "pkg:/source/enums/TaskControl.bs"
import "pkg:/source/utils/misc.bs"

const LOADING_WAIT_TIME = 2

' Helper function to get poster size based on user preference
function getPosterSize(preferWide = true as boolean) as object
    preferThumbnails = chainLookupReturn(m.global.session, "user.settings.ui.home.preferThumbnails", false)

    ' Convert to boolean if it's a string
    if type(preferThumbnails) = "String" or type(preferThumbnails) = "roString"
        preferThumbnails = (LCase(preferThumbnails) = "true")
    end if


    if preferThumbnails
        ' When prefer thumbnails is enabled, always use wide posters
        return homeRowItemSizes.WIDE_POSTER
    else
        ' When prefer thumbnails is disabled, use the preferWide parameter
        if preferWide
            return homeRowItemSizes.WIDE_POSTER
        else
            return homeRowItemSizes.MOVIE_POSTER
        end if
    end if
end function

' Helper function to check if Featured Media is enabled in any home section
function isFeaturedMediaEnabled() as boolean
    ' Get global and session user for settings
    if not isValid(m.global) then return false
    if not isValid(m.global.session) then return false

    sessionUser = m.global.session.user
    if not isValid(sessionUser) then return false

    ' Check if we should use server config or local app settings
    useServerConfig = chainLookupReturn(m.global.session, "user.settings.homeRows.useServerConfig", true)
    if type(useServerConfig) = "String" or type(useServerConfig) = "roString"
        useServerConfig = (LCase(useServerConfig) = "true")
    end if

    ' Check all 8 home sections (0-7) for "featuredmedia"
    for i = 0 to 7
        if useServerConfig
            if isValid(sessionUser.settings)
                userSection = sessionUser.settings["homesection" + i.toStr()]
            else
                userSection = invalid
            end if
        else
            userSection = get_user_setting("homeScreenSection" + i.toStr())
        end if

        if isValid(userSection)
            sectionName = LCase(userSection)
            if sectionName = "featuredmedia"
                return true
            end if
        end if
    end for

    return false
end function

sub init()
    m.scene = m.top.getScene()

    m.overhang = m.scene.findNode("overhang")
    if isValid(m.overhang)
        m.overhang.isVisible = true
    end if

    ' Hide the row counter to prevent flicker. We'll show it once loading timer fires
    m.top.showRowCounter = [false]

    m.top.content = CreateObject("roSGNode", "ContentNode")

    ' Initialize libraryData to prevent access before loading
    m.libraryData = []
    m.filteredLatest = []

    m.loadingTimer = createObject("roSGNode", "Timer")
    m.loadingTimer.duration = LOADING_WAIT_TIME
    m.loadingTimer.observeField("fire", "loadingTimerComplete")

    updateSize()

    m.top.setfocus(true)

    m.top.observeField("rowItemSelected", "itemSelected")

    ' Load the Libraries from API via task
    m.LoadLibrariesTask = createObject("roSGNode", "LoadItemsTask")
    m.LoadLibrariesTask.observeField("content", "onLibrariesLoaded")

    ' Initialize task storage - tasks will be created on-demand
    m.contentTasks = {}
end sub

sub loadLibraries()
    m.LoadLibrariesTask.control = "RUN"
end sub

' getOrCreateTask: Lazily creates and returns a LoadItemsTask for the given type
'
' @param {string} taskType - Type of task to create (e.g., "continue", "nextUp", "favorites")
' @return {object} The task node
function getOrCreateTask(taskType as string) as object
    ' Check if task already exists
    if m.contentTasks.DoesExist(taskType)
        return m.contentTasks[taskType]
    end if

    ' Create new task on-demand
    task = createObject("roSGNode", "LoadItemsTask")
    task.itemsToLoad = taskType
    m.contentTasks[taskType] = task

    return task
end function

' getTaskContentAndCleanup: Retrieves content from a task and cleans up its observers
'
' @param {string} taskType - Type of task to retrieve content from
' @return {object} The content from the task
function getTaskContentAndCleanup(taskType as string) as object
    if not m.contentTasks.doesExist(taskType)
        return invalid
    end if

    task = m.contentTasks[taskType]
    itemData = task.content
    task.unobserveField("content")
    task.content = []

    return itemData
end function

sub updateSize()
    ' Translation is set in Home.xml, don't override it here
    itemHeight = 380

    'Set width of Rows to cut off at edge of Safe Zone
    m.top.itemSize = [1703, itemHeight]

    ' spacing between rows (increased to prevent bottom text from being cut off)
    m.top.itemSpacing = [0, 110]

    ' spacing between items in a row
    m.top.rowItemSpacing = [20, 0]

    ' Default size to movie poster (portrait primary images)
    m.top.rowItemSize = homeRowItemSizes.MOVIE_POSTER

    m.top.visible = true
end sub

' processUserSections: Loop through user's chosen home section settings and generate the content for each row
'
sub processUserSections()
    m.expectedRowCount = 0 ' the favorites row is hardcoded to always show atm
    m.processedRowCount = 0

    sessionUser = m.global.session.user

    ' Check if we should use server config or local settings
    m.useServerConfig = chainLookupReturn(m.global.session, "user.settings.homeRows.useServerConfig", true)
    if type(m.useServerConfig) = "String" or type(m.useServerConfig) = "roString"
        m.useServerConfig = (LCase(m.useServerConfig) = "true")
    end if

    ' calculate expected row count by processing homesections
    for i = 0 to 7
        ' Get section name from either server settings or local app settings
        if m.useServerConfig
            userSection = sessionUser.settings["homesection" + i.toStr()]
        else
            userSection = get_user_setting("homeScreenSection" + i.toStr())
        end if
        sectionName = userSection ?? "none"
        sectionName = LCase(sectionName)

        if sectionName = "latestmedia"
            ' expect 1 row per filtered media library
            excludeList = []
            if isValid(sessionUser.configuration) and isValid(sessionUser.configuration.LatestItemsExcludes)
                excludeList = sessionUser.configuration.LatestItemsExcludes
            end if

            ' Ensure libraryData is valid before accessing it
            if not isValid(m.libraryData)
                continue for
            end if

            m.filteredLatest = filterNodeArray(m.libraryData, "id", excludeList)
            for each latestLibrary in m.filteredLatest
                if latestLibrary.collectionType <> "boxsets" and latestLibrary.collectionType <> "livetv" and latestLibrary.json.CollectionType <> "Program"
                    m.expectedRowCount++
                end if
            end for
        else if sectionName <> "none"
            m.expectedRowCount++
        end if
    end for

    ' Add home sections in order based on user settings
    loadedSections = 0
    for i = 0 to 7
        ' Get section name from either server settings or local app settings
        if m.useServerConfig
            userSection = sessionUser.settings["homesection" + i.toStr()]
        else
            userSection = get_user_setting("homeScreenSection" + i.toStr())
        end if
        sectionName = userSection ?? "none"
        sectionName = LCase(sectionName)


        sectionLoaded = false
        if sectionName <> "none"
            sectionLoaded = addHomeSection(sectionName)
        end if

        ' Count how many sections with data are loaded
        if sectionLoaded then loadedSections++

        ' If 2 sections with data are loaded or we're at the end of the web client section data, consider the home view loaded
        if not m.global.app_loaded
            if loadedSections = 2 or i = 7
                m.top.signalBeacon("AppLaunchComplete") ' Roku Performance monitoring
                m.global.app_loaded = true
            end if
        end if
    end for

    if loadedSections = 0
        m.overhang.setfocus(true)
    end if

    ' Start the timer for creating the content rows before we set the cursor size
    m.loadingTimer.control = "start"
end sub

' onLibrariesLoaded: Handler when LoadLibrariesTask returns data
'
sub onLibrariesLoaded()
    ' save data for other functions
    m.libraryData = m.LoadLibrariesTask.content
    m.LoadLibrariesTask.unobserveField("content")
    m.LoadLibrariesTask.content = []

    processUserSections()
end sub

' getOriginalSectionIndex: Gets the index of a section from user settings and adds count of currently known latest media sections
'
' @param {string} sectionName - Name of section we're looking up
'
' @return {integer} indicating index of section taking latest media sections into account
function getOriginalSectionIndex(sectionName as string) as integer
    searchSectionName = LCase(sectionName).Replace(" ", "")

    sectionIndex = 0
    indexLatestMediaSection = 0

    sessionUser = m.global.session.user

    for i = 0 to 7
        ' Get section name from either server settings or local app settings
        if m.useServerConfig
            userSection = sessionUser.settings["homesection" + i.toStr()]
        else
            userSection = get_user_setting("homeScreenSection" + i.toStr())
        end if
        settingSectionName = userSection ?? "none"
        settingSectionName = LCase(settingSectionName)

        if settingSectionName = "latestmedia"
            indexLatestMediaSection = i
        end if

        if settingSectionName = searchSectionName
            sectionIndex = i
        end if
    end for

    ' If the latest media section is before the section we're searching for, then we need to account for how many latest media rows there are
    addLatestMediaSectionCount = (indexLatestMediaSection < sectionIndex)

    if addLatestMediaSectionCount
        for i = sectionIndex to m.top.content.getChildCount() - 1
            sectionToTest = m.top.content.getChild(i)
            if LCase(Left(sectionToTest.title, 6)) = "latest"
                sectionIndex++
            end if
        end for
    end if

    return sectionIndex
end function

' removeHomeSection: Removes a home section from the home rows
'
' @param {string} sectionToRemove - Title property of section we're removing
sub removeHomeSection(sectionTitleToRemove as string)
    if not isValid(sectionTitleToRemove) then return

    sectionTitle = LCase(sectionTitleToRemove).Replace(" ", "")
    if not sectionExists(sectionTitle) then return

    sectionIndexToRemove = getSectionIndex(sectionTitle)

    m.top.content.removeChildIndex(sectionIndexToRemove)
    setRowItemSize()
end sub

' setRowItemSize: Loops through all home sections and sets the correct item sizes per row
'
sub setRowItemSize()
    if not isValid(m.top.content) then return

    homeSections = m.top.content.getChildren(-1, 0)
    newSizeArray = CreateObject("roArray", homeSections.count(), false)

    for i = 0 to homeSections.count() - 1
        newSizeArray[i] = isValid(homeSections[i].cursorSize) ? homeSections[i].cursorSize : homeRowItemSizes.MOVIE_POSTER
    end for
    m.top.rowItemSize = newSizeArray

    ' If we have processed the expected number of content rows, stop the loading timer and run the complete function
    if m.expectedRowCount = m.processedRowCount
        m.loadingTimer.control = "stop"
        loadingTimerComplete()
    end if
end sub

' loadingTimerComplete: Event handler for when loading wait time has expired
'
sub loadingTimerComplete()
    if not m.top.showRowCounter[0]
        ' Show the row counter to prevent flicker
        m.top.showRowCounter = [true]
    end if

    ' Signal that content has been loaded
    m.top.contentLoaded = true
end sub

' onPreferThumbnailsChanged: Event handler for when the "Prefer Thumbnails" toggle is changed
' NOTE: This is not currently called via observer since observeField() doesn't work on associative array fields.
' The toggle change is handled each time rows are created/updated by reading the current setting value.
'
sub onPreferThumbnailsChanged()
    ' Update all row sizes to reflect the new thumbnail preference
    if isValid(m.top.content)
        homeSections = m.top.content.getChildren(-1, 0)
        newSizeArray = CreateObject("roArray", homeSections.count(), false)

        for i = 0 to homeSections.count() - 1
            row = homeSections[i]

            ' Determine if this row should use portrait or wide based on content type
            ' Rows with children to inspect
            if isValid(row.cursorSize)
                currentSize = row.cursorSize
                isWide = currentSize[0] > 200 ' Wide posters are 464px, portrait are 180px
            else
                isWide = false
            end if

            ' Apply thumbnail preference based on row type
            if isWide
                ' Rows that normally show wide content (On Now, Live TV, etc.)
                row.cursorSize = getPosterSize(true)
                row.imageWidth = getPosterSize(true)[0]
            else
                ' Rows that normally show portrait content (My Media, Favorites, My List, etc.)
                row.cursorSize = getPosterSize(false)
                row.imageWidth = getPosterSize(false)[0]
            end if

            newSizeArray[i] = row.cursorSize
        end for

        m.top.rowItemSize = newSizeArray
    end if
end sub' addHomeSection: Adds a new home section to the home rows.
'
' @param {string} sectionType - Type of section to add
' @return {boolean} indicating if the section was handled
function addHomeSection(sectionType as string) as boolean
    ' Check if merge setting is enabled
    mergeSetting = get_user_setting("ui.home.mergeContinueWatchingNextUp")
    mergeContinueWatching = (mergeSetting = "true")

    ' Poster size library items
    if sectionType = "livetv"
        createLiveTVRow()
        return true
    end if

    ' Poster size library items
    if sectionType = "smalllibrarytiles"
        createLibraryRow()
        return true
    end if

    ' Continue Watching items - handle merge logic
    if sectionType = "resume"
        if mergeContinueWatching
            ' Create merged row if not already created
            if not sectionExists(tr("Continue Watching"))
                createMergedContinueWatchingRow()
            end if
        else
            ' Create separate Continue Watching row
            createContinueWatchingRow()
        end if
        return true
    end if

    ' Continue Watching items
    if sectionType = "resumeaudio"
        createContinueListeningRow()
        return true
    end if

    ' Next Up items - skip if merged with Continue Watching
    if sectionType = "nextup"
        if mergeContinueWatching
            ' If merged row doesn't exist yet, create it now
            if not sectionExists(tr("Continue Watching"))
                createMergedContinueWatchingRow()
            end if
        else
            ' Create separate Next Up row
            createNextUpRow()
        end if
        return true
    end if

    ' Latest items in each library
    if sectionType = "latestmedia"
        createLatestInRows()
        return true
    end if

    ' Favorite Items
    if sectionType = "favorites"
        createFavoritesRow()
        return true
    end if

    ' My List Items
    if sectionType = "mylist"
        createMyListRow()
        return true
    end if

    ' Featured Media Carousel
    if sectionType = "featuredmedia"
        createFeaturedMediaRow()
        return true
    end if

    ' This section type isn't supported.
    ' Count it as processed since we aren't going to do anything else with it
    m.processedRowCount++
    return false
end function

' createLibraryRow: Creates a row displaying the user's libraries
'
sub createLibraryRow()
    m.processedRowCount++
    ' Ensure we have data
    if not isValidAndNotEmpty(m.libraryData) then return

    sectionName = tr("My Media")

    ' We don't refresh library data, so if section already exists, exit
    if sectionExists(sectionName)
        ' return
    end if

    row = CreateObject("roSGNode", "HomeRow")
    row.title = sectionName
    row.imageWidth = getPosterSize(true)[0]
    row.cursorSize = getPosterSize(true)

    excludeList = []
    if isValid(m.global.session.user.configuration) and isValid(m.global.session.user.configuration.MyMediaExcludes)
        excludeList = m.global.session.user.configuration.MyMediaExcludes
    end if
    filteredMedia = filterNodeArray(m.libraryData, "id", excludeList)
    for each item in filteredMedia
        row.appendChild(item)
    end for

    ' Row already exists, replace it with new content
    if sectionExists(sectionName)
        m.top.content.replaceChild(row, getSectionIndex(sectionName))
        setRowItemSize()
        return
    end if

    ' Row does not exist, insert it into the home view
    m.top.content.insertChild(row, getOriginalSectionIndex("smalllibrarytiles"))
    setRowItemSize()
end sub

' createLatestInRows: Creates a row displaying latest items in each of the user's libraries
'
sub createLatestInRows()
    ' Ensure we have data
    if not isValidAndNotEmpty(m.libraryData)
        return
    end if


    ' create a Recently Added in row for each library
    for each lib in m.filteredLatest
        if not isStringEqual(lib.collectionType, "boxsets") and not isStringEqual(lib.collectionType, "livetv") and not isStringEqual(lib.json.CollectionType, "Program")
            sectionName = `${tr("Recently Added in")} ${lib.name}`

            ' Determine poster size based on content type and user preference
            imagesize = getPosterSize(false) ' Default to portrait, but respect thumbnail toggle

            if isValid(lib.json.CollectionType)
                if isStringEqual(lib.json.CollectionType, "movies")
                    imagesize = getPosterSize(false) ' Movies use portrait, or wide if thumbnails enabled
                else if isStringEqual(lib.json.CollectionType, "music")
                    imagesize = homeRowItemSizes.MUSIC_ALBUM
                end if
            end if

            if not sectionExists(sectionName)
                nextUpRow = m.top.content.CreateChild("HomeRow")
                nextUpRow.title = sectionName
                nextUpRow.imageWidth = imagesize[0]
                nextUpRow.cursorSize = imagesize
            end if

            try
                loadLatest = createObject("roSGNode", "LoadItemsTask")
                loadLatest.itemsToLoad = "latest"
                loadLatest.itemId = lib.id

                metadata = { "title": lib.name }
                metadata.Append({ "contentType": lib.json.CollectionType })
                loadLatest.metadata = metadata

                loadLatest.observeField("content", "updateLatestItems")
                loadLatest.control = TaskControl.RUN
            catch e
                removeHomeSection(sectionName)
                m.global.sceneManager.callFunc("standardDialog", `Error creating Recently Added rows`, { data: ["<p>" + `Error Message: ${e.message}` + "</p>"] })
            end try
        end if
    end for
end sub

' sectionExists: Checks if passed section exists in home row content
'
' @param {string} sectionTitle - Title of section we're checking for
'
' @return {boolean} indicating if the section currently exists in the home row content
function sectionExists(sectionTitle as string) as boolean
    if not isAllValid([sectionTitle, m.top.content]) then return false

    searchSectionTitle = LCase(sectionTitle).Replace(" ", "")

    homeSections = m.top.content.getChildren(-1, 0)

    for each section in homeSections
        if LCase(section.title).Replace(" ", "") = searchSectionTitle
            return true
        end if
    end for

    return false
end function

' getSectionIndex: Returns index of requested section in home row content
'
' @param {string} sectionTitle - Title of section we're checking for
'
' @return {integer} indicating index of request section
function getSectionIndex(sectionTitle as string) as integer
    if not isAllValid([sectionTitle, m.top.content]) then return false

    searchSectionTitle = LCase(sectionTitle).Replace(" ", "")

    homeSections = m.top.content.getChildren(-1, 0)

    sectionIndex = homeSections.count()
    i = 0

    for each section in homeSections
        if LCase(section.title).Replace(" ", "") = searchSectionTitle
            sectionIndex = i
            exit for
        end if
        i++
    end for

    return sectionIndex
end function

' createLiveTVRow: Creates a row displaying the live tv now on section
'
sub createLiveTVRow()
    sectionName = tr("On Now")

    if not sectionExists(sectionName)
        nextUpRow = m.top.content.CreateChild("HomeRow")
        nextUpRow.title = sectionName
        nextUpRow.imageWidth = getPosterSize(false)[0]
        nextUpRow.cursorSize = getPosterSize(false)
    end if

    ' Load the On Now Data (lazy task creation)
    task = getOrCreateTask("onNow")
    task.observeField("content", "updateOnNowItems")
    task.control = "RUN"
end sub

' createContinueWatchingRow: Creates a row displaying items the user can continue watching
'
sub createContinueWatchingRow()
    sectionName = tr("Continue Watching")

    if not sectionExists(sectionName)
        nextUpRow = m.top.content.CreateChild("HomeRow")
        nextUpRow.title = sectionName
        nextUpRow.imageWidth = getPosterSize(true)[0]
        nextUpRow.cursorSize = getPosterSize(true)
    end if

    ' Load the Continue Watching Data (lazy task creation)
    task = getOrCreateTask("continue")
    task.observeField("content", "updateContinueWatchingItems")
    task.control = "RUN"
end sub

' createMergedContinueWatchingRow: Creates a row displaying merged Continue Watching and Next Up items
'
sub createMergedContinueWatchingRow()
    sectionName = tr("Continue Watching")

    if not sectionExists(sectionName)
        nextUpRow = m.top.content.CreateChild("HomeRow")
        nextUpRow.title = sectionName
        nextUpRow.imageWidth = getPosterSize(true)[0]
        nextUpRow.cursorSize = getPosterSize(true)
    end if

    ' Load the Merged Continue Watching Data (lazy task creation)
    task = getOrCreateTask("mergedContinueWatching")
    task.observeField("content", "updateMergedContinueWatchingItems")
    task.control = "RUN"
end sub

' createContinueListeningRow: Creates a row displaying items the user can continue listening
'
sub createContinueListeningRow()
    sectionName = tr("Continue Listening")

    if not sectionExists(sectionName)
        nextUpRow = m.top.content.CreateChild("HomeRow")
        nextUpRow.title = sectionName
        nextUpRow.imageWidth = getPosterSize(false)[0]
        nextUpRow.cursorSize = getPosterSize(false)
    end if

    ' Load the Continue Listening Data (lazy task creation)
    task = getOrCreateTask("continueListening")
    task.observeField("content", "updateContinueListeningItems")
    task.control = "RUN"
end sub

' createNextUpRow: Creates a row displaying next episodes up to watch
'
sub createNextUpRow()
    sectionName = tr("Next Up")

    if not sectionExists(sectionName)
        nextUpRow = m.top.content.CreateChild("HomeRow")
        nextUpRow.title = sectionName
        nextUpRow.imageWidth = getPosterSize(false)[0]
        nextUpRow.cursorSize = getPosterSize(false)
    end if

    ' Load the Next Up Data (lazy task creation)
    task = getOrCreateTask("nextUp")
    task.observeField("content", "updateNextUpItems")
    task.control = "RUN"
end sub

' createMyListRow: Creates a row displaying items from the user's personal list
'
sub createMyListRow()
    sectionName = tr("My List")

    if not sectionExists(sectionName)
        nextUpRow = m.top.content.CreateChild("HomeRow")
        nextUpRow.title = sectionName
        nextUpRow.imageWidth = getPosterSize(false)[0]
        nextUpRow.cursorSize = getPosterSize(false)
    end if

    ' Load the My List Data (lazy task creation)
    task = getOrCreateTask("mylist")
    task.observeField("content", "updateMyListItems")
    task.control = "RUN"
end sub

' createFavoritesRow: Creates a row displaying items from the user's favorites list
'
sub createFavoritesRow()
    sectionName = tr("Favorites")

    if not sectionExists(sectionName)
        nextUpRow = m.top.content.CreateChild("HomeRow")
        nextUpRow.title = sectionName
        nextUpRow.imageWidth = getPosterSize(false)[0]
        nextUpRow.cursorSize = getPosterSize(false)
    end if

    ' Load the Favorites Data (lazy task creation)
    task = getOrCreateTask("favorites")
    task.observeField("content", "updateFavoritesItems")
    task.control = "RUN"
end sub

' updateHomeRows: Update function exposed to outside components
'
sub updateHomeRows()
    ' Hide the row counter to prevent flicker. We'll show it once loading timer fires
    m.top.showRowCounter = [false]
    processUserSections()
end sub

' updateFavoritesItems: Processes LoadFavoritesTask content. Removes, Creates, or Updates favorites row as needed
'
sub updateFavoritesItems()
    m.processedRowCount++
    itemData = getTaskContentAndCleanup("favorites")

    sectionName = tr("Favorites")

    if not isValidAndNotEmpty(itemData)
        removeHomeSection(sectionName)
        return
    end if

    ' remake row using the new data
    row = CreateObject("roSGNode", "HomeRow")
    row.title = sectionName
    row.imageWidth = getPosterSize(false)[0]
    row.cursorSize = getPosterSize(false)

    for each item in itemData
        usePoster = true

        if lcase(item.type) = "episode" or lcase(item.type) = "audio" or lcase(item.type) = "musicartist"
            usePoster = false
        end if

        item.usePoster = usePoster
        item.imageWidth = row.imageWidth
        row.appendChild(item)
    end for

    if sectionExists(sectionName)
        m.top.content.replaceChild(row, getSectionIndex(sectionName))
        setRowItemSize()
        return
    end if

    m.top.content.insertChild(row, getSectionIndex(sectionName))
    setRowItemSize()
end sub

' updateMyListItems: Processes LoadMyListTask content. Removes, Creates, or Updates My List row as needed
'
sub updateMyListItems()
    m.processedRowCount++
    itemData = getTaskContentAndCleanup("mylist")

    sectionName = tr("My List")

    if not isValidAndNotEmpty(itemData)
        removeHomeSection(sectionName)
        return
    end if

    ' remake row using the new data
    row = CreateObject("roSGNode", "HomeRow")
    row.title = sectionName
    row.imageWidth = getPosterSize(false)[0]
    row.cursorSize = getPosterSize(false)

    for each item in itemData
        usePoster = true

        if lcase(item.type) = "episode" or lcase(item.type) = "audio" or lcase(item.type) = "musicartist"
            usePoster = false
        end if

        item.usePoster = usePoster
        item.imageWidth = row.imageWidth
        row.appendChild(item)
    end for

    if sectionExists(sectionName)
        m.top.content.replaceChild(row, getSectionIndex(sectionName))
        setRowItemSize()
        return
    end if

    m.top.content.insertChild(row, getSectionIndex(sectionName))
    setRowItemSize()
end sub

' updateContinueWatchingItems: Processes LoadContinueWatchingTask content. Removes, Creates, or Updates continue watching row as needed
'
sub updateContinueWatchingItems()
    m.processedRowCount++
    itemData = getTaskContentAndCleanup("continue")

    sectionName = tr("Continue Watching")

    if not isValidAndNotEmpty(itemData)
        removeHomeSection(sectionName)
        return
    end if

    ' remake row using the new data
    row = CreateObject("roSGNode", "HomeRow")
    row.title = sectionName

    ' Determine whether to use posters or thumbnails based on preference
    preferThumbnails = chainLookupReturn(m.global.session, "user.settings.ui.home.preferThumbnails", false)
    row.usePoster = not preferThumbnails ' Use thumbnails when prefer thumbnails is enabled

    ' Set sizes: wide if thumbnails, portrait if posters
    if preferThumbnails
        row.imageWidth = homeRowItemSizes.WIDE_POSTER[0]
        row.cursorSize = homeRowItemSizes.WIDE_POSTER
    else
        row.imageWidth = homeRowItemSizes.MOVIE_POSTER[0]
        row.cursorSize = homeRowItemSizes.MOVIE_POSTER
    end if

    for each item in itemData
        if isChainValid(item, "json.UserData.PlayedPercentage")
            item.PlayedPercentage = item.json.UserData.PlayedPercentage
        end if

        item.usePoster = row.usePoster
        item.imageWidth = row.imageWidth
        row.appendChild(item)
    end for

    ' Row already exists, replace it with new content
    if sectionExists(sectionName)
        m.top.content.replaceChild(row, getSectionIndex(sectionName))
        setRowItemSize()
        return
    end if

    ' Row does not exist, insert it into the home view
    m.top.content.insertChild(row, getOriginalSectionIndex("resume"))
    setRowItemSize()
end sub

' updateMergedContinueWatchingItems: Processes LoadMergedContinueWatchingTask content. Removes, Creates, or Updates merged continue watching + next up row as needed
'
sub updateMergedContinueWatchingItems()
    m.processedRowCount++
    itemData = getTaskContentAndCleanup("mergedContinueWatching")

    sectionName = tr("Continue Watching")

    if not isValidAndNotEmpty(itemData)
        removeHomeSection(sectionName)
        return
    end if

    ' remake row using the new data
    row = CreateObject("roSGNode", "HomeRow")
    row.title = sectionName

    ' Determine whether to use posters or thumbnails based on preference
    preferThumbnails = chainLookupReturn(m.global.session, "user.settings.ui.home.preferThumbnails", false)
    row.usePoster = not preferThumbnails ' Use thumbnails when prefer thumbnails is enabled

    ' Set sizes: wide if thumbnails, portrait if posters
    if preferThumbnails
        row.imageWidth = homeRowItemSizes.WIDE_POSTER[0]
        row.cursorSize = homeRowItemSizes.WIDE_POSTER
    else
        row.imageWidth = homeRowItemSizes.MOVIE_POSTER[0]
        row.cursorSize = homeRowItemSizes.MOVIE_POSTER
    end if

    for each item in itemData
        if isChainValid(item, "json.UserData.PlayedPercentage")
            item.PlayedPercentage = item.json.UserData.PlayedPercentage
        end if

        item.usePoster = row.usePoster
        item.imageWidth = row.imageWidth
        row.appendChild(item)
    end for

    ' Row already exists, replace it with new content
    if sectionExists(sectionName)
        m.top.content.replaceChild(row, getSectionIndex(sectionName))
        setRowItemSize()
        return
    end if

    ' Row does not exist, insert it into the home view
    m.top.content.insertChild(row, getOriginalSectionIndex("resume"))
    setRowItemSize()
end sub

' updateContinueListeningItems: Processes LoadContinueListeningTask content. Removes, Creates, or Updates continue listening row as needed
'
sub updateContinueListeningItems()
    m.processedRowCount++
    itemData = getTaskContentAndCleanup("continueListening")

    sectionName = tr("Continue Listening")

    if not isValidAndNotEmpty(itemData)
        removeHomeSection(sectionName)
        return
    end if

    ' remake row using the new data
    row = CreateObject("roSGNode", "HomeRow")
    row.title = sectionName
    row.imageWidth = getPosterSize(false)[0]
    row.cursorSize = getPosterSize(false)

    for each item in itemData
        if hasValidProperties(item, "json") and hasValidProperties(item.json, "UserData", "PlayedPercentage")
            item.PlayedPercentage = item.json.UserData.PlayedPercentage
        end if

        item.usePoster = row.usePoster
        item.imageWidth = row.imageWidth
        row.appendChild(item)
    end for

    ' Row already exists, replace it with new content
    if sectionExists(sectionName)
        m.top.content.replaceChild(row, getSectionIndex(sectionName))
        setRowItemSize()
        return
    end if

    ' Row does not exist, insert it into the home view
    m.top.content.insertChild(row, getOriginalSectionIndex("resumeaudio"))
    setRowItemSize()
end sub

' updateNextUpItems: Processes LoadNextUpTask content. Removes, Creates, or Updates next up row as needed
'
sub updateNextUpItems()
    m.processedRowCount++
    itemData = getTaskContentAndCleanup("nextUp")
    ' Stop the task for Next Up
    if m.contentTasks.doesExist("nextUp")
        m.contentTasks["nextUp"].control = "STOP"
    end if

    sectionName = tr("Next Up")

    if not isValidAndNotEmpty(itemData)
        removeHomeSection(sectionName)
        return
    end if

    ' remake row using the new data
    row = CreateObject("roSGNode", "HomeRow")
    row.title = tr("Next Up")
    row.imageWidth = getPosterSize(false)[0]
    row.cursorSize = getPosterSize(false)
    row.usePoster = true ' Show series/movie posters

    for each item in itemData
        item.usePoster = row.usePoster
        item.imageWidth = row.imageWidth
        row.appendChild(item)
    end for

    ' Row already exists, replace it with new content
    if sectionExists(sectionName)
        m.top.content.replaceChild(row, getSectionIndex(sectionName))
        setRowItemSize()
        return
    end if

    ' Row does not exist, insert it into the home view
    m.top.content.insertChild(row, getSectionIndex(sectionName))
    setRowItemSize()
end sub

' updateLatestItems: Processes LoadItemsTask content. Removes, Creates, or Updates Recently Added in {library} row as needed
'
' @param {dynamic} msg - LoadItemsTask
sub updateLatestItems(msg)
    m.processedRowCount++
    itemData = msg.GetData()

    node = msg.getRoSGNode()
    node.unobserveField("content")
    node.content = []

    sectionName = tr("Recently Added in") + " " + node.metadata.title

    if not isValidAndNotEmpty(itemData)
        removeHomeSection(sectionName)
        return
    end if

    imagesize = getPosterSize(false)

    if isValid(node.metadata.contentType)
        if LCase(node.metadata.contentType) = "movies"
            imagesize = getPosterSize(false)
        else if LCase(node.metadata.contentType) = "music"
            imagesize = homeRowItemSizes.MUSIC_ALBUM
        end if
    end if

    ' remake row using new data
    row = CreateObject("roSGNode", "HomeRow")
    row.title = sectionName
    row.imageWidth = imagesize[0]
    row.cursorSize = imagesize
    row.usePoster = true

    for each item in itemData
        item.usePoster = row.usePoster
        item.imageWidth = row.imageWidth
        row.appendChild(item)
    end for

    if sectionExists(sectionName)
        ' Row already exists, replace it with new content
        m.top.content.replaceChild(row, getSectionIndex(sectionName))
        setRowItemSize()
        return
    end if

    m.top.content.insertChild(row, getOriginalSectionIndex("latestmedia"))
    setRowItemSize()
end sub

' updateOnNowItems: Processes LoadOnNowTask content. Removes, Creates, or Updates Recently Added in on now row as needed
'
sub updateOnNowItems()
    m.processedRowCount++
    itemData = getTaskContentAndCleanup("onNow")

    sectionName = tr("On Now")

    if not isValidAndNotEmpty(itemData)
        removeHomeSection(sectionName)
        return
    end if

    ' remake row using the new data
    row = CreateObject("roSGNode", "HomeRow")
    row.title = tr("On Now")
    row.imageWidth = getPosterSize(true)[0]
    row.cursorSize = getPosterSize(true)

    for each item in itemData
        row.usePoster = false

        if (not isValid(item.thumbnailURL) or item.thumbnailURL = "") and isValid(item.json) and isValid(item.json.imageURL)
            item.thumbnailURL = item.json.imageURL
            row.usePoster = true
            row.imageWidth = getPosterSize(true)[0]
            row.cursorSize = getPosterSize(true)
        end if

        item.usePoster = row.usePoster
        item.imageWidth = row.imageWidth
        row.appendChild(item)
    end for

    ' Row already exists, replace it with new content
    if sectionExists(sectionName)
        m.top.content.replaceChild(row, getSectionIndex(sectionName))
        setRowItemSize()
        return
    end if

    ' Row does not exist, insert it into the home view
    m.top.content.insertChild(row, getOriginalSectionIndex("livetv"))
    setRowItemSize()
end sub

sub itemSelected()
    m.selectedRowItem = m.top.rowItemSelected
    ' Force field change notification even for same item by clearing first
    m.top.selectedItem = invalid
    m.top.selectedItem = m.top.content.getChild(m.top.rowItemSelected[0]).getChild(m.top.rowItemSelected[1])
end sub

function onKeyEvent(key as string, press as boolean) as boolean
    if press
        if key = KeyCode.PLAY
            itemToPlay = m.top.content.getChild(m.top.rowItemFocused[0]).getChild(m.top.rowItemFocused[1])
            if isValid(itemToPlay)
                m.top.quickPlayNode = itemToPlay
            end if
            return true
        end if

        if key = KeyCode.UP
            if m.top.rowItemFocused[0] = 0
                ' Check if Featured Media is the first section
                sessionUser = m.global.session.user
                isFeaturedMediaFirst = false
                if isValid(sessionUser) and isChainValid(sessionUser, "settings.homesection0")
                    firstSection = sessionUser.settings.homesection0
                    if LCase(firstSection) = "featuredmedia"
                        isFeaturedMediaFirst = true
                    end if
                end if

                ' If Featured Media is first, let the Home component handle the UP key
                ' Otherwise, go to overhang as normal
                if not isFeaturedMediaFirst
                    m.overhang.setfocus(true)

                    group = m.global.sceneManager.callFunc("getActiveScene")
                    if isValid(group)
                        group.lastFocus = m.overhang
                    end if

                    return true
                else
                    ' Return false to let parent (Home) handle the key event
                    return false
                end if
            end if
        end if

    end if

    group = m.global.sceneManager.callFunc("getActiveScene")
    if isValid(group)
        group.lastFocus = m.top
    end if
    return false
end function

function filterNodeArray(nodeArray as object, nodeKey as string, excludeArray as object) as object
    if excludeArray.IsEmpty() then return nodeArray

    newNodeArray = []
    for each node in nodeArray
        excludeThisNode = false
        for each exclude in excludeArray
            if node[nodeKey] = exclude
                excludeThisNode = true
            end if
        end for
        if excludeThisNode = false
            newNodeArray.Push(node)
        end if
    end for
    return newNodeArray
end function

' createFeaturedMediaRow: Creates a Featured Media row marker (no actual row, just tracking)
'
sub createFeaturedMediaRow()
    m.processedRowCount++

    ' Don't create a placeholder row - just signal that Featured Media section exists
    ' The MediaBar component will handle the display in the Home screen
    ' We increment processedRowCount to maintain proper row counting
end sub
