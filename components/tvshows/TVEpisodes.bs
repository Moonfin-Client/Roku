import "pkg:/source/api/baserequest.bs"
import "pkg:/source/api/Image.bs"
import "pkg:/source/api/sdk.bs"
import "pkg:/source/enums/ColorPalette.bs"
import "pkg:/source/enums/KeyCode.bs"
import "pkg:/source/enums/TaskControl.bs"
import "pkg:/source/utils/config.bs"
import "pkg:/source/utils/misc.bs"

sub init()
    m.top.optionsAvailable = false

    m.rows = m.top.findNode("picker")
    m.poster = m.top.findNode("seasonPoster")
    m.shuffle = m.top.findNode("shuffle")
    m.extras = m.top.findNode("extras")
    m.rows.focusBitmapBlendColor = chainLookupReturn(m.global.session, "user.settings.colorCursor", ColorPalette.HIGHLIGHT)

    m.playedIndicator = m.top.findNode("playedIndicator")

    m.rows.observeField("doneLoading", "updateSeason")

    m.tvExtrasTask = createObject("roSGNode", "TVExtrasTask")
    m.tvExtrasTask.observeField("results", "onExtrasDataLoaded")

    m.isFirstRun = true
end sub

sub onExtrasDataLoaded()
    m.top.extrasObjects = m.tvExtrasTask.results
    setExtraButtonVisibility()
end sub

sub setSeasonLoading()
end sub

' Updates the visibility of the Extras button based on if this season has any extra features
sub setExtraButtonVisibility()
    if isValid(m.top.extrasObjects) and isValidAndNotEmpty(m.top.extrasObjects.items)
        m.extras.visible = true
    end if
end sub

sub updateSeason()
    m.playedIndicator.data = {
        played: chainLookupReturn(m.top.seasonData, "UserData.Played", false),
        unplayedCount: chainLookupReturn(m.top.seasonData, "UserData.UnplayedItemCount", 0)
    }

    m.tvExtrasTask.seasonID = m.top.seasonData.Id
    m.tvExtrasTask.control = TaskControl.RUN

    imgParams = { "maxHeight": 450, "maxWidth": 300 }
    m.poster.uri = ImageURL(m.top.seasonData.Id, "Primary", imgParams)
    m.shuffle.visible = true
end sub

sub updateNextEpisodeRow()
    ' Find the first unwatched episode in the current season
    nextEpisode = invalid

    if isValid(m.top.episodeObjects) and isValidAndNotEmpty(m.top.episodeObjects.items)
        for each episode in m.top.episodeObjects.items
            if not chainLookupReturn(episode, "UserData.Played", false)
                nextEpisode = episode
                exit for
            end if
        end for
    end if

    ' If all episodes are watched, show the first episode
    if not isValid(nextEpisode) and isValid(m.top.episodeObjects) and isValidAndNotEmpty(m.top.episodeObjects.items)
        nextEpisode = m.top.episodeObjects.items[0]
    end if

    ' Update the next episode row
    if isValid(nextEpisode)
        m.nextEpisodeRow.nextEpisodeData = nextEpisode
        m.nextEpisodeRow.visible = true
    else
        m.nextEpisodeRow.visible = false
    end if
end sub

' get the currently focused item
function getFocusedItem() as dynamic
    if not isChainValid(m.top.focusedChild, "content") then return invalid

    focusedChild = m.top.focusedChild
    m.top.lastFocus = focusedChild

    if isValidAndNotEmpty(focusedChild.rowItemFocused)
        itemToPlay = focusedChild.content.getChild(focusedChild.rowItemFocused[0]).getChild(0)
        if isValid(itemToPlay) and isValidAndNotEmpty(itemToPlay.id)
            return itemToPlay
        end if
    end if

    return invalid
end function

' OnScreenShown: Callback function when view is presented on screen
'
sub OnScreenShown(_isReturning = false as boolean)
    if isValid(m.top.lastFocus)
        m.top.lastFocus.setFocus(true)
        group = m.global.sceneManager.callFunc("getActiveScene")
        group.lastFocus = m.top.lastFocus
    else
        m.rows.setFocus(true)
        group = m.global.sceneManager.callFunc("getActiveScene")
        group.lastFocus = m.rows
    end if

    if m.isFirstRun
        m.isFirstRun = false
        return
    end if

    m.top.refreshSeasonDetailsData = not m.top.refreshSeasonDetailsData
end sub

' Handle navigation input from the remote and act on it
function onKeyEvent(key as string, press as boolean) as boolean
    if key = "left" and m.rows.hasFocus()
        m.shuffle.setFocus(true)
        group = m.global.sceneManager.callFunc("getActiveScene")
        group.lastFocus = m.shuffle
        return true
    end if

    if key = "right" and (m.shuffle.hasFocus() or m.extras.hasFocus())
        m.rows.setFocus(true)
        group = m.global.sceneManager.callFunc("getActiveScene")
        group.lastFocus = m.rows
        return true
    end if

    if m.extras.visible and key = "up" and m.extras.hasFocus()
        m.shuffle.setFocus(true)
        group = m.global.sceneManager.callFunc("getActiveScene")
        group.lastFocus = m.shuffle
        return true
    end if

    if m.extras.visible and key = "down" and m.shuffle.hasFocus()
        m.extras.setFocus(true)
        group = m.global.sceneManager.callFunc("getActiveScene")
        group.lastFocus = m.extras
        return true
    end if

    if key = "OK"
        if m.rows.isInFocusChain()
            focusedItem = getFocusedItem()
            if isValid(focusedItem)
                m.top.selectedItem = focusedItem
                'Prevent the selected item event from double firing
                m.top.selectedItem = invalid
            end if
            return true
        end if

        if m.shuffle.hasFocus()
            episodeList = m.rows.objects.items

            for i = 0 to episodeList.count() - 1
                j = Rnd(episodeList.count() - 1)
                temp = episodeList[i]
                episodeList[i] = episodeList[j]
                episodeList[j] = temp
            end for

            m.global.queueManager.callFunc("set", episodeList)
            m.global.queueManager.callFunc("playQueue")
            return true
        end if

        if m.extras.visible and m.extras.hasFocus()
            if LCase(m.extras.text.trim()) = LCase(tr("Extras"))
                m.extras.text = tr("Episodes")
                m.top.objects = m.top.extrasObjects
            else
                m.extras.text = tr("Extras")
                m.top.objects = m.top.episodeObjects
            end if
        end if
    end if

    if press
        if isStringEqual(key, KeyCode.PLAY)
            focusedItem = getFocusedItem()
            if isValid(focusedItem)
                m.top.quickPlayNode = focusedItem
            end if
            return true
        end if
    end if

    return false
end function
