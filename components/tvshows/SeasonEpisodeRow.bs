import "pkg:/source/api/Image.bs"
import "pkg:/source/utils/config.bs"
import "pkg:/source/utils/misc.bs"

sub init()
    m.episodeThumbnail = m.top.findNode("episodeThumbnail")
    m.episodeNumber = m.top.findNode("episodeNumber")
    m.episodeRuntime = m.top.findNode("episodeRuntime")
    m.episodeTitle = m.top.findNode("episodeTitle")
    m.episodeOverview = m.top.findNode("episodeOverview")
    m.progressBackground = m.top.findNode("progressBackground")
    m.progressBar = m.top.findNode("progressBar")
    m.playedIndicator = m.top.findNode("playedIndicator")
    m.rowBackground = m.top.findNode("rowBackground")
    m.focusInAnimation = m.top.findNode("focusInAnimation")
    m.focusOutAnimation = m.top.findNode("focusOutAnimation")
end sub

sub itemContentChanged()
    itemData = m.top.itemContent
    if not isValid(itemData) then return

    m.playedIndicator.data = {
        played: chainLookupReturn(itemData, "watched", false),
        unplayedCount: chainLookupReturn(itemData, "json.UserData.UnplayedItemCount", 0)
    }

    m.episodeOverview.text = isValid(itemData.overview) ? itemData.overview : ""

    if not isChainValid(itemData, "json") then return

    indexNumber = ""
    if isValid(itemData.json.indexNumber)
        indexNumber = `Episode ${itemData.json.indexNumber}`
        if isValid(itemData.json.indexNumberEnd)
            indexNumber = `Episode ${itemData.json.indexNumber}-${itemData.json.indexNumberEnd}`
        end if
    end if
    m.episodeNumber.text = indexNumber

    m.episodeTitle.text = isValid(itemData.title) ? itemData.title : ""

    thumbUri = itemData.posterURL
    if isValidAndNotEmpty(thumbUri)
        ' Blur unwatched if setting is enabled
        if m.global.session.user.settings["ui.tvshows.blurunwatched"] = true
            if not chainLookupReturn(itemData, "json.UserData.Played", false)
                thumbUri += "&blur=15"
            end if
        end if
        m.episodeThumbnail.uri = thumbUri
    end if

    if isValid(itemData.json.LookupCI("RunTimeTicks"))
        ticks = itemData.json.RunTimeTicks
        if type(ticks) = "roInt" or type(ticks) = "LongInteger"
            runMins = int(ticks / 600000000.0)
            if runMins < 2
                m.episodeRuntime.text = "1 min"
            else
                m.episodeRuntime.text = `${stri(runMins).trim()} min`
            end if
        end if
    end if

    playedPct = chainLookupReturn(itemData, "json.UserData.PlayedPercentage", 0)
    if playedPct > 0
        m.progressBackground.visible = true
        m.progressBar.visible = true
        m.progressBar.width = int(m.episodeThumbnail.width * playedPct / 100)
    else
        m.progressBackground.visible = false
        m.progressBar.visible = false
    end if
end sub

sub focusChanged()
    if m.top.itemHasFocus
        m.focusOutAnimation.control = "stop"
        m.focusInAnimation.control = "start"

        if m.global.device.isAudioGuideEnabled = true
            txt2Speech = CreateObject("roTextToSpeech")
            txt2Speech.Flush()
            txt2Speech.Say(m.episodeTitle.text)
            txt2Speech.Say(m.episodeOverview.text)
        end if
    else
        m.focusInAnimation.control = "stop"
        m.focusOutAnimation.control = "start"
    end if
end sub
