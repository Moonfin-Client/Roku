import "pkg:/source/api/baserequest.bs"
import "pkg:/source/api/Image.bs"
import "pkg:/source/enums/ItemType.bs"
import "pkg:/source/utils/config.bs"
import "pkg:/source/utils/multiserver.bs"

sub setData()
    ' We keep json around just as a reference,
    ' but ideally everything should be going through one of the interfaces
    datum = m.top.json

    m.top.id = datum.LookupCI("Id")
    m.top.name = datum.LookupCI("name")
    m.top.type = datum.LookupCI("type")

    ' Extract server data for multi-server image URL support
    serverData = { serverUrl: datum._serverUrl }

    if datum.CollectionType = invalid
        m.top.CollectionType = datum.type
    else
        m.top.CollectionType = datum.CollectionType
    end if

    ' Set appropriate Images for Wide and Tall based on type

    if LCase(datum.type) = "collectionfolder" or LCase(datum.type) = "userview"
        if IsValid(datum.ImageTags)
            params = { "Tag": datum.ImageTags.Primary, "maxHeight": 261, "maxWidth": 464 }
            m.top.thumbnailURL = getImageURL(datum.LookupCI("Id"), "Primary", params, serverData)
            m.top.widePosterUrl = m.top.thumbnailURL
        end if

        ' Add Icon URLs for display if there is no Poster
        if LCase(m.top.CollectionType) = "livetv"
            m.top.iconUrl = "pkg:/images/media_type_icons/live_tv_white.png"
        else if LCase(m.top.CollectionType) = "folders" or LCase(m.top.CollectionType) = "nextup"
            m.top.iconUrl = "pkg:/images/media_type_icons/folder_white.png"
        else if isStringEqual(m.top.CollectionType, ItemType.MYLIST)
            m.top.iconUrl = "pkg:/images/icons/star.png"
        end if

    else if datum.type = "Episode" or LCase(datum.type) = "recording" or datum.type = "MusicVideo"
        m.top.isWatched = datum.UserData.Played

        imgParams = {}
        imgParams.Append({ "maxHeight": 261 })
        imgParams.Append({ "maxWidth": 464 })

        if datum.ImageTags.Primary <> invalid
            param = { "Tag": datum.ImageTags.Primary }
            imgParams.Append(param)
        end if

        m.top.thumbnailURL = getImageURL(datum.LookupCI("Id"), "Primary", imgParams, serverData)

        ' Set posterURL to series primary image for episodes (portrait dimensions for Next Up)
        if datum.SeriesPrimaryImageTag <> invalid and datum.SeriesId <> invalid
            seriesImgParams = { "maxHeight": 270, "maxWidth": 180, "Tag": datum.SeriesPrimaryImageTag }
            m.top.posterURL = getImageURL(datum.SeriesId, "Primary", seriesImgParams, serverData)
        else
            m.top.posterURL = m.top.thumbnailURL
        end if

        ' Add Wide Poster  (Series Backdrop)
        if datum.ParentThumbImageTag <> invalid
            imgParams["Tag"] = datum.ParentThumbImageTag
            m.top.widePosterUrl = getImageURL(datum.ParentThumbItemId, "Thumb", imgParams, serverData)
        else if datum.ParentBackdropImageTags <> invalid
            imgParams["Tag"] = datum.ParentBackdropImageTags[0]
            m.top.widePosterUrl = getImageURL(datum.ParentBackdropItemId, "Backdrop", imgParams, serverData)
        else if datum.ImageTags.Primary <> invalid
            imgParams["Tag"] = datum.SeriesPrimaryImageTag
            m.top.widePosterUrl = getImageURL(datum.LookupCI("Id"), "Primary", imgParams, serverData)
        end if

        ' If no widePosterUrl has been determined, revent to thumbnailURL
        if not isValidAndNotEmpty(m.top.widePosterUrl)
            m.top.widePosterUrl = m.top.thumbnailURL
        end if

    else if datum.type = "Series"
        m.top.isWatched = datum.UserData.Played

        ' Portrait dimensions for primary poster
        imgParams = { "maxHeight": 270 }
        imgParams.Append({ "maxWidth": 180 })

        m.top.posterURL = getImageURL(datum.LookupCI("Id"), "Primary", imgParams, serverData)

        ' Wide dimensions for backdrop
        imgParams["maxHeight"] = 261
        imgParams["maxWidth"] = 464

        ' Add Wide Poster  (Series Backdrop)
        if datum.ImageTags <> invalid and datum.imageTags.Thumb <> invalid
            imgParams["Tag"] = datum.imageTags.Thumb
            m.top.widePosterUrl = getImageURL(datum.LookupCI("Id"), "Thumb", imgParams, serverData)
        else if datum.BackdropImageTags <> invalid
            imgParams["Tag"] = datum.BackdropImageTags[0]
            m.top.widePosterUrl = getImageURL(datum.LookupCI("Id"), "Backdrop", imgParams, serverData)
        end if

    else if datum.type = "Movie"
        m.top.isWatched = datum.UserData.Played

        ' Portrait dimensions for primary poster
        imgParams = {}
        imgParams.Append({ "maxHeight": 270 })
        imgParams.Append({ "maxWidth": 180 })

        if datum.ImageTags.Primary <> invalid
            param = { "Tag": datum.ImageTags.Primary }
            imgParams.Append(param)
        end if

        m.top.posterURL = getImageURL(datum.LookupCI("Id"), "Primary", imgParams, serverData)

        ' For wide image, use backdrop
        imgParams["maxWidth"] = 464
        imgParams["maxHeight"] = 261

        if datum.ImageTags <> invalid and datum.imageTags.Thumb <> invalid
            imgParams["Tag"] = datum.imageTags.Thumb
            m.top.thumbnailUrl = getImageURL(datum.LookupCI("Id"), "Thumb", imgParams, serverData)
        else if datum.BackdropImageTags[0] <> invalid
            imgParams["Tag"] = datum.BackdropImageTags[0]
            m.top.thumbnailUrl = getImageURL(datum.LookupCI("Id"), "Backdrop", imgParams, serverData)
        end if
    else if datum.type = "Video"
        m.top.isWatched = datum.UserData.Played

        imgParams = {
            "maxHeight": 261,
            "maxWidth": 464
        }

        if datum.ImageTags <> invalid and datum.ImageTags.Primary <> invalid
            imgParams.Append({ "Tag": datum.ImageTags.Primary })
        end if

        m.top.posterURL = getImageURL(datum.LookupCI("Id"), "Primary", imgParams, serverData)
        m.top.thumbnailUrl = m.top.posterURL
    else if datum.type = "MusicAlbum"
        params = { "Tag": datum.ImageTags.Primary, "maxHeight": 261, "maxWidth": 261 }
        m.top.thumbnailURL = getImageURL(datum.LookupCI("Id"), "Primary", params, serverData)
        m.top.widePosterUrl = m.top.thumbnailURL
        m.top.posterUrl = m.top.thumbnailURL
    else if datum.type = "TvChannel" or datum.type = "Channel"
        params = { "Tag": datum.ImageTags.Primary, "maxHeight": 261, "maxWidth": 464 }
        m.top.thumbnailURL = getImageURL(datum.LookupCI("Id"), "Primary", params, serverData)
        m.top.widePosterUrl = m.top.thumbnailURL
        m.top.iconUrl = "pkg:/images/media_type_icons/live_tv_white.png"
    else if datum.type = "Photo"
        params = { "Tag": datum.ImageTags.Primary, "maxHeight": 261, "maxWidth": 464 }

        m.top.thumbnailURL = getImageURL(datum.LookupCI("Id"), "Primary", params, serverData)
        m.top.widePosterUrl = m.top.thumbnailURL
        m.top.posterUrl = m.top.thumbnailURL
    else if datum.type = "PhotoAlbum"
        params = { "Tag": datum.ImageTags.Primary, "maxHeight": 261, "maxWidth": 464 }

        m.top.thumbnailURL = getImageURL(datum.LookupCI("Id"), "Primary", params, serverData)
        m.top.widePosterUrl = m.top.thumbnailURL
        m.top.posterUrl = m.top.thumbnailURL
    end if
end sub
