import "pkg:/source/api/baserequest.bs"
import "pkg:/source/api/Image.bs"
import "pkg:/source/api/Items.bs"
import "pkg:/source/enums/ItemType.bs"
import "pkg:/source/utils/config.bs"
import "pkg:/source/utils/deviceCapabilities.bs"

sub init()
    m.top.content = getData()
end sub

function onKeyEvent(key as string, press as boolean) as boolean
    if not press then return false

    ' Handle OK/Select button press
    if key = "OK"
        ' Get the currently focused item using rowItemFocused (not itemSelected)
        focusedArray = m.top.rowItemFocused

        if isValidAndNotEmpty(focusedArray) and focusedArray.count() = 2
            rowContent = m.top.content.getChild(focusedArray[0])

            if isValid(rowContent)
                selectedItem = rowContent.getChild(focusedArray[1])

                if isValid(selectedItem)
                    ' Set our custom selectedItem field - don't clear it here
                    m.top.selectedItem = selectedItem
                    return true ' Indicate we handled the key
                end if
            end if
        end if
    end if

    return false ' Let the RowList handle other keys
end function

function getData()
    data = CreateObject("roSGNode", "ContentNode")
    m.rowItemSizes = []
    m.rowHeights = []

    itemData = m.top.itemData

    if not isValid(itemData)
        return data
    end if

    ' Do this to keep the ordering, AssociateArrays have no order
    ' Note: Jellyseerr results will be added after PERSON
    type_array = [ItemType.MOVIE, ItemType.SERIES, ItemType.TVCHANNEL, ItemType.EPISODE, ItemType.MUSICARTIST, ItemType.MUSICALBUM, ItemType.AUDIO, ItemType.PERSON, ItemType.PLAYLIST, ItemType.PROGRAM]
    content_types = {
        "movie": { "label": "Movies", "count": 0, size: [230, 405], imageDimensions: [230, 340] },
        "episode": { "label": "Episodes", "count": 0, size: [400, 330], imageDimensions: [400, 260] },
        "musicartist": { "label": "Artists", "count": 0, size: [400, 305], imageDimensions: [400, 260] },
        "program": { "label": "Programs", "count": 0, size: [400, 330], imageDimensions: [400, 260] },
        "tvChannel": { "label": "Channels", "count": 0, size: [400, 305], imageDimensions: [400, 260] },
        "musicalbum": { "label": "Albums", "count": 0, size: [330, 330], imageDimensions: [330, 260] },
        "audio": { "label": "Songs", "count": 0, size: [330, 330], imageDimensions: [330, 260] },
        "series": { "label": "Shows", "count": 0, size: [230, 405], imageDimensions: [230, 340] },
        "person": { "label": "People", "count": 0, size: [230, 385], imageDimensions: [230, 340] },
        "playlist": { "label": "Playlists", "count": 0, size: [330, 330], imageDimensions: [330, 260] }
    }

    ' Count Jellyseerr results separately
    jellyseerrCount = 0

    ' Loop through all items to get count of each item type
    for each item in chainLookup(itemData, "items")
        ' Check if this is a Jellyseerr result
        if item.isJellyseerrResult <> invalid and item.isJellyseerrResult = true
            jellyseerrCount += 1
        else if isValid(content_types.LookupCI(item.type))
            content_types.LookupCI(item.type).count += 1
        end if
    end for

    ' Loop through each item type in order and create rows if it has items
    for each ctype in type_array
        content_type = content_types.LookupCI(ctype)
        if content_type.count > 0
            addRow(data, content_type, ctype)
            m.rowItemSizes.push(content_type.size)
            m.rowHeights.push(content_type.size[1] + 100)
        end if
    end for

    ' Add Jellyseerr results row last (after cast/people)
    if jellyseerrCount > 0
        jellyseerrRowContent = { "label": "Jellyseerr", size: [180, 360], imageDimensions: [180, 335] }
        addJellyseerrRow(data, jellyseerrRowContent)
        m.rowItemSizes.push(jellyseerrRowContent.size)
        m.rowHeights.push(jellyseerrRowContent.size[1] + 100)
    end if

    m.top.rowHeights = m.rowHeights
    m.top.rowItemSize = m.rowItemSizes
    m.top.content = data
    return data
end function

sub addRow(pageData, content, type_filter)
    itemData = m.top.itemData
    row = pageData.CreateChild("ContentNode")

    row.title = content.label

    ' Loop through all items, but only get items that match the passed type
    for each item in chainLookup(itemData, "items")
        if isStringEqual(item.type, type_filter)
            item.imageDimensions = content.imageDimensions
            row.appendChild(item)
        end if
    end for
end sub

sub addJellyseerrRow(pageData, content)
    itemData = m.top.itemData
    row = pageData.CreateChild("ContentNode")

    row.title = content.label

    ' Loop through all items and get Jellyseerr results
    for each item in chainLookup(itemData, "items")
        if item.isJellyseerrResult <> invalid and item.isJellyseerrResult = true
            ' Don't try to set imageDimensions - Jellyseerr items already have their images set
            row.appendChild(item)
        end if
    end for
end sub
