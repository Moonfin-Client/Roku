import "pkg:/source/utils/misc.bs"

sub init()
    m.buttonBackground = m.top.findNode("buttonBackground")
    m.buttonText = m.top.findNode("buttonText")
    m.focusFeedback = m.top.findNode("focusFeedback")

    m.top.observeField("background", "onBackgroundChanged")
    m.top.observeField("color", "onColorChanged")
    m.top.observeField("text", "onTextChanged")
    m.top.observeField("height", "onHeightChanged")
    m.top.observeField("width", "onWidthChanged")
    m.top.observeField("focus", "onFocusChanged")
    m.top.observeField("focusedChild", "onFocusedChildChanged")
    m.top.observeField("fontSize", "onFontSizeChanged")
end sub

sub onFontSizeChanged()
    if m.top.fontSize > 0
        m.buttonText.font.size = m.top.fontSize
    end if
end sub

sub onFocusChanged()
    if m.top.focus
        m.buttonBackground.blendColor = m.top.focusBackground
        m.buttonText.color = m.top.focusColor
        m.focusFeedback.visible = true
        m.buttonText.font = "font:SmallBoldSystemFont"
        if m.top.fontSize > 0
            m.buttonText.font.size = m.top.fontSize
        end if
    else
        m.buttonBackground.blendColor = m.top.background
        m.buttonText.color = m.top.color
        m.focusFeedback.visible = false
        m.buttonText.font = "font:SmallSystemFont"
        if m.top.fontSize > 0
            m.buttonText.font.size = m.top.fontSize
        end if
    end if
end sub

sub onFocusedChildChanged()
    hasFocus = m.top.hasFocus()
    m.top.focus = hasFocus
end sub

sub onBackgroundChanged()
    m.buttonBackground.blendColor = m.top.background
    m.top.unobserveField("background")
end sub

sub onColorChanged()
    m.buttonText.color = m.top.color
    m.top.unobserveField("color")
end sub

sub onTextChanged()
    m.buttonText.text = m.top.text
end sub

sub onHeightChanged()
    m.buttonBackground.height = m.top.height
    m.buttonText.height = m.top.height
    m.focusFeedback.height = m.top.height
end sub

sub onWidthChanged()
    m.buttonBackground.width = m.top.width
    m.buttonText.width = m.top.width
    m.focusFeedback.width = m.top.width
end sub

function onKeyEvent(key as string, press as boolean) as boolean
    if not press then return false

    if key = "OK" and m.top.hasFocus()
        m.top.buttonSelected = true
        return true
    end if

    ' Let parent scene handle left/right navigation
    ' Only set escape and return true if there's specific escape handling needed
    ' For now, return false to allow parent navigation
    return false
end function
