import "pkg:/source/api/baserequest.bs"
import "pkg:/source/api/sdk.bs"
import "pkg:/source/enums/ColorPalette.bs"
import "pkg:/source/enums/KeyCode.bs"
import "pkg:/source/enums/PosterLoadStatus.bs"
import "pkg:/source/utils/config.bs"
import "pkg:/source/utils/multiserver.bs"

const ICON_SIZE = 50
const ICON_LABEL_GAP = 10
const ITEM_SPACING = 36
const PILL_PAD_H = 14
const PILL_PAD_V = 3
const PILL_HEIGHT = 56
const NAV_HEIGHT = 80
const ITEM_Y = 15
const AVATAR_RIGHT = 200
const CLOCK_LEFT = 1560
const NAV_SIDE_PAD = 30

sub init()
    m.top.id = "overhang"
    m.top.translation = [54, 0]

    m.navContainer = m.top.findNode("navContainer")
    m.navClipGroup = m.top.findNode("navClipGroup")
    m.navbarBg = m.top.findNode("navbarBackground")
    m.navItemsGroup = m.top.findNode("navItemsGroup")
    m.focusPill = m.top.findNode("focusPill")
    m.clock = m.top.findNode("overlayClock")
    m.profileImage = m.top.findNode("overlayCurrentUserProfileImage")
    m.overlayCurrentUser = m.top.findNode("overlayCurrentUser")
    m.overlayCurrentUserSelection = m.top.findNode("overlayCurrentUserSelection")

    m.slideUpAnim = m.top.findNode("slideUp")
    m.slideDownAnim = m.top.findNode("slideDown")
    m.focusPillAnim = m.top.findNode("focusPillAnim")
    m.focusPillPosInterp = m.top.findNode("focusPillPosInterp")
    m.focusPillWidthInterp = m.top.findNode("focusPillWidthInterp")
    m.scrollAnim = m.top.findNode("scrollAnim")
    m.scrollPosInterp = m.top.findNode("scrollPosInterp")

    if isValid(m.overlayCurrentUser)
        m.overlayCurrentUser.color = chainLookupReturn(m.global.session, "user.settings.colorHomeUsername", ColorPalette.WHITE)
    end if
    if isValid(m.overlayCurrentUserSelection)
        m.overlayCurrentUserSelection.blendColor = chainLookupReturn(m.global.session, "user.settings.colorCursor", ColorPalette.HIGHLIGHT)
        m.overlayCurrentUserSelection.visible = false
    end if

    m.selectedIndex = 0
    m.avatarSelected = false
    m.navHasFocus = false
    m.firstFocusShow = true
    m.scrollX = 0
    m.clipWidth = 900
    m.totalContentWidth = 0
    m.navItems = []
    m.libraryData = []
    m.libraryItems = []
    m.librariesExpanded = false
    m.multiServerTask = invalid
    m.lastNavbarSettings = { shuffle: invalid, favorites: invalid, genres: invalid, libraries: invalid, jellyseerr: invalid }

    m.homeItem = createNavItem("home", "pkg:/images/icons/home.png", "Home")
    m.searchItem = createNavItem("search", "pkg:/images/icons/search-light.png", "Search")
    m.shuffleItem = createNavItem("shuffle", "pkg:/images/icons/shuffle.png", "Shuffle", "0xFFFFFF")
    m.favoritesItem = createNavItem("favorites", "pkg:/images/icons/heart.png", "Favorites", "0xFFFFFF")
    m.genresItem = createNavItem("genres", "pkg:/images/icons/masks.png", "Genres", "0xFFFFFF")
    m.jellyseerrItem = createNavItem("jellyseerr", "pkg:/images/icons/jellyseer_jellyfish.png", "Discover")
    m.librariesItem = createNavItem("libraries", "pkg:/images/icons/library.png", "Libraries")
    m.settingsItem = createNavItem("settings", "pkg:/images/icons/settings.png", "Settings")

    loadNavbarSettings()
    m.global.observeField("session", "onSessionChanged")
    m.global.observeField("overlaySettingsChanged", "onOverlaySettingsChanged")
    m.top.observeField("focusedChild", "onFocusChanged")

    rebuildNavItems()
    applyOverlaySettings()
end sub

function createNavItem(id as string, iconUri as string, labelText as string, blendColor = "" as string) as object
    btn = createObject("roSGNode", "Group")
    btn.id = id + "Btn"

    icon = createObject("roSGNode", "Poster")
    icon.uri = iconUri
    icon.width = ICON_SIZE
    icon.height = ICON_SIZE
    icon.loadDisplayMode = "scaleToFit"
    icon.opacity = 0.6
    if blendColor <> "" then icon.blendColor = blendColor
    btn.appendChild(icon)

    label = createObject("roSGNode", "Label")
    label.text = labelText
    label.font = "font:SmallBoldSystemFont"
    label.color = "#FFFFFF"
    label.height = ICON_SIZE
    label.vertAlign = "center"
    label.translation = [ICON_SIZE + ICON_LABEL_GAP, 0]
    label.visible = false
    btn.appendChild(label)

    return { id: id, btn: btn, icon: icon, label: label, isLibChild: false }
end function

function createLibraryChildItem(id as string, labelText as string) as object
    btn = createObject("roSGNode", "Group")
    btn.id = "lib_" + id

    label = createObject("roSGNode", "Label")
    label.text = labelText
    label.font = "font:SmallBoldSystemFont"
    label.color = "#FFFFFF"
    label.height = ICON_SIZE
    label.vertAlign = "center"
    label.opacity = 0.6
    btn.appendChild(label)

    return { id: "lib_" + id, btn: btn, icon: invalid, label: label, isLibChild: true }
end function

sub rebuildNavItems()
    m.navItemsGroup.removeChildrenIndex(m.navItemsGroup.getChildCount(), 0)

    m.navItems = []
    m.navItems.push(m.homeItem)
    m.navItems.push(m.searchItem)

    if m.shuffleButtonEnabled then m.navItems.push(m.shuffleItem)
    if m.favoritesButtonEnabled then m.navItems.push(m.favoritesItem)
    if m.genresButtonEnabled then m.navItems.push(m.genresItem)

    jellyseerrEnabled = m.global.session.user.settings["jellyseerr.enabled"]
    if jellyseerrEnabled = true then m.navItems.push(m.jellyseerrItem)

    if m.librariesEnabled then m.navItems.push(m.librariesItem)

    if m.librariesExpanded
        for each libItem in m.libraryItems
            m.navItems.push(libItem)
        end for
    end if

    m.navItems.push(m.settingsItem)

    for each item in m.navItems
        m.navItemsGroup.appendChild(item.btn)
    end for

    if m.selectedIndex >= m.navItems.count() then m.selectedIndex = m.navItems.count() - 1
    if m.selectedIndex < 0 then m.selectedIndex = 0
    layoutNavbar()
end sub

sub layoutNavbar()
    for i = 0 to m.navItems.count() - 1
        item = m.navItems[i]
        isFocused = (not m.avatarSelected and i = m.selectedIndex and m.navHasFocus)

        if isValid(item.icon)
            item.icon.opacity = iif(isFocused, 1.0, 0.6)
        end if

        if item.isLibChild
            item.label.opacity = iif(isFocused, 1.0, 0.6)
        else
            item.label.visible = isFocused
        end if
    end for

    if m.avatarSelected
        highlightUser()
    else
        dehighlightUser()
    end if

    itemPositions = []
    xCursor = 0

    for i = 0 to m.navItems.count() - 1
        item = m.navItems[i]
        w = getItemWidth(item)

        itemPositions.push({ x: xCursor, width: w })
        item.btn.translation = [xCursor, ITEM_Y]

        xCursor += w
        if i < m.navItems.count() - 1 then xCursor += ITEM_SPACING
    end for

    m.totalContentWidth = xCursor
    m.itemPositions = itemPositions

    sizePillAndClip()

    if not m.avatarSelected
        scrollToFocusedItem()
    end if

    if m.navHasFocus and not m.avatarSelected
        positionFocusPill()
    else
        m.focusPill.visible = false
    end if
end sub

' Get the rendered width of a nav item based on current state
function getItemWidth(item as object) as integer
    if item.isLibChild
        return item.label.boundingRect().width
    end if

    w = ICON_SIZE
    if item.label.visible
        labelWidth = item.label.boundingRect().width
        w = ICON_SIZE + ICON_LABEL_GAP + labelWidth
    end if
    return w
end function

sub sizePillAndClip()
    maxNavWidth = CLOCK_LEFT - AVATAR_RIGHT - 40
    contentWithPad = m.totalContentWidth + (NAV_SIDE_PAD * 2)

    if contentWithPad <= maxNavWidth
        pillWidth = contentWithPad
        clipWidth = contentWithPad
    else
        pillWidth = maxNavWidth
        clipWidth = maxNavWidth
    end if

    m.clipWidth = clipWidth
    navCenterX = AVATAR_RIGHT + (maxNavWidth / 2)
    pillX = navCenterX - (pillWidth / 2)

    m.navbarBg.translation = [pillX, 40]
    m.navbarBg.width = pillWidth
    m.navbarBg.height = NAV_HEIGHT
    m.navContainer.translation = [pillX, 40]
    m.navClipGroup.clippingRect = [0, 0, clipWidth, NAV_HEIGHT]
end sub

sub scrollToFocusedItem()
    if m.selectedIndex < 0 or m.selectedIndex >= m.itemPositions.count() then return

    itemPos = m.itemPositions[m.selectedIndex]
    itemLeft = itemPos.x
    itemRight = itemPos.x + itemPos.width

    viewPad = 40
    viewLeft = -m.scrollX + viewPad
    viewRight = -m.scrollX + m.clipWidth - viewPad

    targetScroll = m.scrollX

    if m.totalContentWidth + (NAV_SIDE_PAD * 2) <= m.clipWidth
        targetScroll = (m.clipWidth - m.totalContentWidth) / 2
    else if itemLeft < viewLeft
        targetScroll = - (itemLeft - viewPad)
    else if itemRight > viewRight
        targetScroll = - (itemRight - m.clipWidth + viewPad)
    end if

    minScroll = - (m.totalContentWidth - m.clipWidth + NAV_SIDE_PAD)
    maxScroll = NAV_SIDE_PAD
    if targetScroll < minScroll then targetScroll = minScroll
    if targetScroll > maxScroll then targetScroll = maxScroll

    if targetScroll <> m.scrollX
        animateScroll(targetScroll)
        m.scrollX = targetScroll
    else
        m.navItemsGroup.translation = [m.scrollX, 0]
    end if
end sub

sub animateScroll(targetX as float)
    if isValid(m.scrollPosInterp) and isValid(m.scrollAnim)
        currentPos = m.navItemsGroup.translation
        m.scrollPosInterp.keyValue = [[currentPos[0], 0], [targetX, 0]]
        m.scrollAnim.control = "start"
    else
        m.navItemsGroup.translation = [targetX, 0]
    end if
end sub

sub positionFocusPill()
    if m.selectedIndex < 0 or m.selectedIndex >= m.itemPositions.count()
        m.focusPill.visible = false
        return
    end if

    itemPos = m.itemPositions[m.selectedIndex]

    pillX = m.scrollX + itemPos.x - PILL_PAD_H
    pillY = ITEM_Y - PILL_PAD_V
    pillWidth = itemPos.width + (PILL_PAD_H * 2)

    m.focusPill.height = PILL_HEIGHT

    if m.firstFocusShow or not m.focusPill.visible
        m.focusPill.translation = [pillX, pillY]
        m.focusPill.width = pillWidth
        m.focusPill.visible = true
        m.firstFocusShow = false
    else
        currentPos = m.focusPill.translation
        currentWidth = m.focusPill.width

        if isValid(m.focusPillPosInterp) and isValid(m.focusPillWidthInterp) and isValid(m.focusPillAnim)
            m.focusPillPosInterp.keyValue = [[currentPos[0], currentPos[1]], [pillX, pillY]]
            m.focusPillWidthInterp.keyValue = [currentWidth, pillWidth]
            m.focusPillAnim.control = "start"
        else
            m.focusPill.translation = [pillX, pillY]
            m.focusPill.width = pillWidth
        end if

        m.focusPill.visible = true
    end if
end sub

sub onFocusChanged()
    if m.top.hasFocus() and isValid(m.navItemsGroup)
        m.navItemsGroup.setFocus(true)
    end if

    navHasFocus = m.top.isInFocusChain()
    if navHasFocus <> m.navHasFocus
        m.navHasFocus = navHasFocus
        layoutNavbar()
    end if
end sub

sub loadLibraries()
    m.libraryData = []
    m.libraryItems = []
    m.librariesExpanded = false

    if isMultiServerEnabled()
        sessions = getAllServerSessions()
        if sessions.count() > 0
            loadMultiServerLibraries(sessions)
            return
        end if
    end if

    ' Use a Task to avoid creating roUrlTransfer on the render thread
    m.loadLibrariesTask = CreateObject("roSGNode", "LoadLibrariesTask")
    m.loadLibrariesTask.userId = m.global.session.user.id
    m.loadLibrariesTask.observeFieldScoped("result", "onLoadLibrariesComplete")
    m.loadLibrariesTask.control = "RUN"
end sub

sub onLoadLibrariesComplete(msg as object)
    node = msg.getRoSGNode()
    if not isValid(node) then return
    node.unobserveFieldScoped("result")

    data = node.result
    if not isChainValid(data, "items") then return

    for each item in data.LookupCI("items")
        libData = {
            id: item.LookupCI("Id"),
            name: item.LookupCI("name"),
            type: item.LookupCI("Type"),
            collectionType: item.LookupCI("CollectionType")
        }
        m.libraryData.push(libData)
        m.libraryItems.push(createLibraryChildItem(libData.id, libData.name))
    end for

    rebuildNavItems()
end sub

sub loadMultiServerLibraries(sessions as object)
    if not isValid(m.multiServerTask)
        m.multiServerTask = CreateObject("roSGNode", "MultiServerTask")
    else
        m.multiServerTask.callFunc("reset")
    end if

    m.multiServerTask.sessions = sessions
    m.multiServerTask.endpoint = "/Users/{userId}/Views"
    m.multiServerTask.params = {}
    m.multiServerTask.addFields({ metadata: { multipleServers: (sessions.count() > 1) } })
    m.multiServerTask.observeField("isComplete", "onMultiServerLibrariesLoaded")
    m.multiServerTask.control = "RUN"
end sub

sub onMultiServerLibrariesLoaded(msg as object)
    node = msg.getRoSGNode()
    if not isValid(node) then return
    node.unobserveField("isComplete")

    results = node.results
    if not isValidAndNotEmpty(results) then return

    uniqueServers = {}
    for each item in results
        if isValidAndNotEmpty(item._serverId) then uniqueServers[item._serverId] = true
    end for
    multipleServers = (uniqueServers.count() > 1)

    allLibs = []
    for each item in results
        if not isValidAndNotEmpty(item.Id) or not isValidAndNotEmpty(item.Name) then continue for

        serverSuffix = ""
        if multipleServers and isValidAndNotEmpty(item._serverName)
            serverSuffix = " (" + item._serverName + ")"
        end if

        allLibs.push({
            id: item.Id, name: item.Name,
            type: item.LookupCI("Type"), collectionType: item.LookupCI("CollectionType"),
            serverUrl: item._serverUrl, userId: item._userId, authToken: item._authToken,
            serverId: item._serverId, serverName: item._serverName, serverSuffix: serverSuffix
        })
    end for

    sortedLibs = sortLibrariesAlphabetically(allLibs)
    for each lib in sortedLibs
        m.libraryData.push(lib)
        displayName = lib.name
        if not isValidAndNotEmpty(displayName) then displayName = "Library"
        displayName += lib.serverSuffix
        m.libraryItems.push(createLibraryChildItem(lib.id, displayName))
    end for

    rebuildNavItems()
end sub

function sortLibrariesAlphabetically(libraries as object) as object
    if libraries.count() <= 1 then return libraries
    sorted = []
    for each lib in libraries
        sorted.push(lib)
    end for
    for i = 0 to sorted.count() - 2
        for j = 0 to sorted.count() - i - 2
            n1 = LCase(sorted[j].name + sorted[j].serverSuffix)
            n2 = LCase(sorted[j + 1].name + sorted[j + 1].serverSuffix)
            if n1 > n2
                temp = sorted[j]
                sorted[j] = sorted[j + 1]
                sorted[j + 1] = temp
            end if
        end for
    end for
    return sorted
end function

sub expandLibraries()
    if m.librariesExpanded or m.libraryItems.count() = 0 then return
    m.librariesExpanded = true
    rebuildNavItems()
end sub

sub collapseLibraries()
    if not m.librariesExpanded then return
    m.librariesExpanded = false
    rebuildNavItems()
end sub

sub manageLibraryExpansion()
    if not m.librariesEnabled or m.libraryItems.count() = 0 then return
    if m.avatarSelected
        if m.librariesExpanded then collapseLibraries()
        return
    end if

    libIdx = -1
    for i = 0 to m.navItems.count() - 1
        if m.navItems[i].id = "libraries"
            libIdx = i
            exit for
        end if
    end for
    if libIdx < 0 then return

    inLibArea = (m.selectedIndex >= libIdx and m.selectedIndex < m.navItems.count() - 1)

    if inLibArea and not m.librariesExpanded
        expandLibraries()
    else if not inLibArea and m.librariesExpanded
        collapseLibraries()
    end if
end sub

sub loadNavbarSettings()
    m.shuffleButtonEnabled = parseBoolSetting(m.global.session.user.settings["navbar.show_shuffle"], m.global.session.user.settings["show_shuffle_button"], true)
    m.favoritesButtonEnabled = parseBoolSetting(m.global.session.user.settings["navbar.show_favorites"], invalid, true)
    m.genresButtonEnabled = parseBoolSetting(m.global.session.user.settings["navbar.show_genres"], invalid, true)
    m.librariesEnabled = parseBoolSetting(m.global.session.user.settings["navbar.show_libraries"], invalid, true)

    m.lastNavbarSettings = {
        shuffle: m.global.session.user.settings["navbar.show_shuffle"],
        favorites: m.global.session.user.settings["navbar.show_favorites"],
        genres: m.global.session.user.settings["navbar.show_genres"],
        libraries: m.global.session.user.settings["navbar.show_libraries"],
        jellyseerr: m.global.session.user.settings["jellyseerr.enabled"]
    }
end sub

function parseBoolSetting(primary as dynamic, fallback = invalid as dynamic, defaultVal = false as boolean) as boolean
    val = primary
    if not isValid(val) then val = fallback
    if not isValid(val) then return defaultVal

    if type(val) = "String" or type(val) = "roString"
        return (LCase(val) = "true")
    else if type(val) = "Integer" or type(val) = "roInt"
        return (val = 1)
    end if
    return (val = true)
end function

sub onSessionChanged()
    currentSettings = {
        shuffle: m.global.session.user.settings["navbar.show_shuffle"],
        favorites: m.global.session.user.settings["navbar.show_favorites"],
        genres: m.global.session.user.settings["navbar.show_genres"],
        libraries: m.global.session.user.settings["navbar.show_libraries"],
        jellyseerr: m.global.session.user.settings["jellyseerr.enabled"]
    }
    if currentSettings.shuffle <> m.lastNavbarSettings.shuffle or currentSettings.favorites <> m.lastNavbarSettings.favorites or currentSettings.genres <> m.lastNavbarSettings.genres or currentSettings.libraries <> m.lastNavbarSettings.libraries or currentSettings.jellyseerr <> m.lastNavbarSettings.jellyseerr
        loadNavbarSettings()
        rebuildNavItems()
    end if
    applyOverlaySettings()
end sub

sub onOverlaySettingsChanged(_event as object)
    applyOverlaySettings()
end sub

sub applyOverlaySettings()
    if not isValid(m.global) or not isValid(m.global.session) then return

    opacity = 0.5
    if isValid(m.global.session.user) and isValid(m.global.session.user.settings)
        opacitySetting = m.global.session.user.settings["ui.overlayOpacity"]
        if isValid(opacitySetting) then opacity = opacitySetting.toFloat()
    end if

    color = "0x475569FF"
    if isValid(m.global.session.user) and isValid(m.global.session.user.settings)
        colorSetting = m.global.session.user.settings["ui.overlayColor"]
        if isValid(colorSetting) and colorSetting <> "" then color = colorSetting + "FF"
    end if

    if isValid(m.navbarBg)
        m.navbarBg.opacity = opacity
        m.navbarBg.blendColor = color
    end if
end sub

sub updateShuffleButtonVisibility()
    rebuildNavItems()
end sub

sub onVisibleChange()
    overhangContent = m.top.findNode("overhangContent")
    if m.top.disableMoveAnimation
        if isValid(overhangContent) then overhangContent.translation = [0, 0]
        return
    end if
    if m.top.isVisible
        m.slideDownAnim.control = "start"
    else
        m.slideUpAnim.control = "start"
    end if
end sub

sub updateUser()
    if isValid(m.overlayCurrentUser) then m.overlayCurrentUser.text = m.top.currentUser
end sub

sub updateUserProfileImage()
    m.profileImage.observeField("loadStatus", "onPosterLoadStatusChanged")
    if isValid(m.profileImage) then m.profileImage.uri = m.top.currentUserProfileImage
end sub

sub onPosterLoadStatusChanged()
    if m.profileImage.loadStatus <> PosterLoadStatus.LOADING
        m.profileImage.unobserveField("loadStatus")
    end if
    if m.profileImage.loadStatus = PosterLoadStatus.FAILED
        if m.profileImage.loadWidth = 0 then m.profileImage.uri = "pkg:/images/baseline_person_white_48dp.png"
    end if
end sub

sub highlightUser()
    if isValid(m.overlayCurrentUserSelection)
        m.overlayCurrentUserSelection.translation = [70, 53]
        m.overlayCurrentUserSelection.visible = true
    end if
end sub

sub dehighlightUser()
    if isValid(m.overlayCurrentUserSelection)
        m.overlayCurrentUserSelection.visible = false
    end if
end sub

sub updateOptions()
end sub

function onKeyEvent(key as string, press as boolean) as boolean
    if not press then return false

    maxIndex = m.navItems.count() - 1

    if key = KeyCode.LEFT
        if m.avatarSelected
            return true
        else if m.selectedIndex = 0
            m.avatarSelected = true
            m.selectedIndex = -1
            manageLibraryExpansion()
            layoutNavbar()
            return true
        else
            m.selectedIndex = m.selectedIndex - 1
            manageLibraryExpansion()
            layoutNavbar()
            return true
        end if

    else if key = KeyCode.RIGHT
        if m.avatarSelected
            m.avatarSelected = false
            m.selectedIndex = 0
            layoutNavbar()
            return true
        else if m.selectedIndex < maxIndex
            m.selectedIndex = m.selectedIndex + 1
            manageLibraryExpansion()
            layoutNavbar()
            return true
        end if

    else if key = KeyCode.DOWN
        return handleDownKey()

    else if key = KeyCode.OK
        return handleOKKey()
    end if

    return false
end function

function handleDownKey() as boolean
    scene = m.top.getScene()
    homeRows = scene.findNode("homeRows")
    mediaBar = scene.findNode("mediaBar")

    if isValid(mediaBar) and isValid(homeRows)
        mediaBarCheck = homeRows.callFunc("isFeaturedMediaEnabled")
        mediaBarEnabled = false
        if isValid(mediaBarCheck) and type(mediaBarCheck) = "roBoolean"
            mediaBarEnabled = mediaBarCheck
        end if

        if mediaBarEnabled
            mediaBar.visible = true
            mediaBar.hasFocus = true
            mediaBar.setFocus(true)
            homeRows.visible = false
            mediaBar.callFunc("refreshCurrentDisplay")
            return true
        end if
    end if

    if isValid(homeRows)
        if homeRows.content.getChildCount() = 0 then return false
        dehighlightUser()
        homeRows.setfocus(true)
        group = m.global.sceneManager.callFunc("getActiveScene")
        if not isValid(group.lastFocus) or not group.lastFocus.isSameNode(homeRows)
            group.lastFocus = homeRows
        end if
        return true
    else
        group = m.global.sceneManager.callFunc("getActiveScene")
        if isValid(group) and isValid(group.lastFocus)
            dehighlightUser()
            group.lastFocus.setFocus(true)
            return true
        end if
    end if

    return false
end function

function handleOKKey() as boolean
    if m.avatarSelected
        group = m.global.sceneManager.callFunc("getActiveScene")
        panel = group.findNode("options")
        if isValid(panel)
            panel.visible = true
            panel.findNode("panelList").setFocus(true)
        end if
        homeRows = m.top.getscene().findNode("homeRows")
        if isValid(homeRows)
            if homeRows.content.getChildCount() = 0 then group.lastFocus = m.top
        end if
        dehighlightUser()
        return true
    end if

    if m.selectedIndex < 0 or m.selectedIndex >= m.navItems.count() then return false

    item = m.navItems[m.selectedIndex]

    if item.id = "home"
        while m.global.sceneManager.callFunc("getSceneCount") > 1
            m.global.sceneManager.callFunc("popScene")
        end while
        m.top.itemSelected = "home"
        return true

    else if item.id = "search"
        m.top.itemSelected = "search"
        return true

    else if item.id = "shuffle"
        m.top.shufflePressed = not m.top.shufflePressed
        return true

    else if item.id = "favorites"
        m.top.favoritesPressed = not m.top.favoritesPressed
        return true

    else if item.id = "genres"
        m.top.genresPressed = not m.top.genresPressed
        return true

    else if item.id = "jellyseerr"
        jellyseerrDiscovery = CreateObject("roSGNode", "JellyseerrDiscoveryScreen")
        m.global.sceneManager.callFunc("pushScene", jellyseerrDiscovery)
        return true

    else if item.id = "settings"
        m.global.sceneManager.callFunc("settings")
        return true

    else if item.id = "libraries"
        if m.librariesExpanded
            collapseLibraries()
        else
            expandLibraries()
            if m.libraryItems.count() > 0
                m.selectedIndex = m.selectedIndex + 1
            end if
            layoutNavbar()
        end if
        return true

    else if item.isLibChild
        libIndex = getLibraryDataIndex(item.id)
        if libIndex >= 0 and libIndex < m.libraryData.count()
            libInfo = m.libraryData[libIndex]
            libraryNode = createObject("roSGNode", "LibraryData")
            libraryNode.id = libInfo.id
            libraryNode.libraryId = libInfo.id
            libraryNode.title = libInfo.name
            libraryNode.collectionType = libInfo.collectionType

            if isValidAndNotEmpty(libInfo.serverUrl)
                libraryNode.serverUrl = libInfo.serverUrl
                libraryNode.userId = libInfo.userId
                libraryNode.authToken = libInfo.authToken
                libraryNode.serverId = libInfo.serverId
                libraryNode.serverName = libInfo.serverName
            end if

            m.top.librarySelected = libraryNode
            return true
        end if
    end if

    return false
end function

function getLibraryDataIndex(itemId as string) as integer
    actualId = itemId.replace("lib_", "")
    for i = 0 to m.libraryData.count() - 1
        if m.libraryData[i].id = actualId then return i
    end for
    return -1
end function

function iif(condition as boolean, trueVal as dynamic, falseVal as dynamic) as dynamic
    if condition then return trueVal
    return falseVal
end function
