import "pkg:/source/enums/ColorPalette.bs"
import "pkg:/source/utils/misc.bs"

const LOGO_RIGHT_PADDING = 30

sub init()
    m.videoControls = m.top.findNode("videoControls")
    m.optionControls = m.top.findNode("optionControls")

    m.inactivityTimer = m.top.findNode("inactivityTimer")
    m.videoInfo = m.top.findNode("videoInfo")
    m.itemTitle = m.top.findNode("itemTitle")
    m.videoPlayPause = m.top.findNode("videoPlayPause")
    m.videoRemainingTime = m.top.findNode("videoRemainingTime")
    m.videoEndingTime = m.top.findNode("videoEndingTime")
    m.progressBar = m.top.findNode("progressBar")
    m.progressBarBackground = m.top.findNode("progressBarBackground")
    m.progressIndicator = m.top.findNode("progressIndicator")
    m.seekbarGroup = m.top.findNode("seekbarGroup")
    m.nextItem = m.top.FindNode("nextItem")
    m.previousItem = m.top.FindNode("previousItem")

    ' Track if user is seeking
    m.isSeeking = false
    m.seekPercentage = 0

    m.top.observeField("visible", "onVisibleChanged")
    m.top.observeField("hasFocus", "onFocusChanged")
    m.top.observeField("progressPercentage", "onProgressPercentageChanged")
    m.top.observeField("playbackState", "onPlaybackStateChanged")
    m.top.observeField("videoEndingTime", "setVideoEndingTime")

    m.top.observeField("itemTitleText", "onItemTitleTextChanged")
    m.top.observeField("itemSubtitleText", "onItemSubtitleTextChanged")
    m.top.observeField("seasonNumber", "onSeasonNumberChanged")
    m.top.observeField("episodeNumber", "onEpisodeNumberChanged")
    m.top.observeField("episodeNumberEnd", "onEpisodeNumberEndChanged")
    m.top.observeField("logoImage", "onLogoImageChanged")

    m.top.observeField("previousItemIcon", "onPreviousItemIconChanged")
    m.top.observeField("previousItemTitleText", "onPreviousItemTitleTextChanged")
    m.top.observeField("nextItemIcon", "onNextItemIconChanged")
    m.top.observeField("nextItemTitleText", "onNextItemTitleTextChanged")

    ' Default focus on play/pause button (index 0 in primary row)
    m.defaultButtonIndex = 0
    m.focusedButtonIndex = 0
    m.currentButtonGroup = "video"
    m.logoMoved = false

    m.videoControls.buttonFocused = m.defaultButtonIndex
    m.optionControls.buttonFocused = 0
    m.videoControls.getChild(m.defaultButtonIndex).focus = true
    m.deviceInfo = CreateObject("roDeviceInfo")

    m.progressBarBackground.color = ColorPalette.BLACK77
    m.progressBar.color = "#00a4dc"

    ' Style all buttons in both control rows
    videoButtons = m.videoControls.getChildren(-1, 0)
    for each button in videoButtons
        button.background = ColorPalette.DARKGREY
        button.focusBackground = chainLookupReturn(m.global.session, "user.settings.colorCursor", ColorPalette.HIGHLIGHT)
    end for

    optionButtons = m.optionControls.getChildren(-1, 0)
    for each button in optionButtons
        button.background = ColorPalette.DARKGREY
        button.focusBackground = chainLookupReturn(m.global.session, "user.settings.colorCursor", ColorPalette.HIGHLIGHT)
    end for
end sub

' onProgressPercentageChanged: Handler for changes to m.top.progressPercentage param
'
sub onProgressPercentageChanged()
    totalTime = m.top.positionTime + m.top.remainingPositionTime
    m.videoRemainingTime.text = secondsToHuman(m.top.positionTime, true) + " / " + secondsToHuman(totalTime, true)
    m.progressBar.width = m.progressBarBackground.width * m.top.progressPercentage

    ' Update circle indicator position (centered on progress, offset by circle radius)
    indicatorX = 103 + (m.progressBarBackground.width * m.top.progressPercentage) - 10
    m.progressIndicator.translation = [indicatorX, 912]
end sub

sub setVideoEndingTime()
    m.videoEndingTime.text = "Ends at " + m.top.videoEndingTime
end sub


' onPlaybackStateChanged: Handler for changes to m.top.playbackState param
'
sub onPlaybackStateChanged()
    if LCase(m.top.playbackState) = "playing"
        m.videoPlayPause.icon = "pkg:/images/icons/pause.png"
        return
    end if

    m.videoPlayPause.icon = "pkg:/images/icons/play.png"
end sub

' onItemTitleTextChanged: Handler for changes to m.top.itemTitleText param.
'
sub onItemTitleTextChanged()
    m.itemTitle.text = m.top.itemTitleText
end sub

sub onItemSubtitleTextChanged()
    itemSeason = m.top.findNode("itemSeason")
    itemSeason.font.size = 32
    itemSeason.text = m.top.itemSubtitleText
end sub

' onSeasonNumberChanged: Handler for changes to m.top.seasonNumber param.
'
sub onSeasonNumberChanged()
    m.top.unobserveField("seasonNumber")
    itemSeason = m.top.findNode("itemSeason")
    itemSeason.font.size = 32
    itemSeason.text = `S${m.top.seasonNumber}`
end sub

' onEpisodeNumberChanged: Handler for changes to m.top.episodeNumber param.
'
sub onEpisodeNumberChanged()
    m.top.unobserveField("episodeNumber")
    itemEpisode = m.top.findNode("itemEpisode")
    itemEpisode.font.size = 32
    itemEpisode.text = `E${m.top.episodeNumber}`
end sub

' onEpisodeNumberEndChanged: Handler for changes to m.top.episodeNumberEnd param.
'
sub onEpisodeNumberEndChanged()
    m.top.unobserveField("episodeNumberEnd")
    itemEpisodeEnd = m.top.findNode("itemEpisodeEnd")
    itemEpisodeEnd.font.size = 32
    itemEpisodeEnd.text = `-${m.top.episodeNumberEnd}`
end sub

' onLogoLoadStatusChanged: Handler for changes to logo image's status.
'
' @param {dynamic} event - field change event
sub onLogoLoadStatusChanged(event)
    if LCase(event.GetData()) = "ready"
        logoImage = event.getRoSGNode()
        logoImage.unobserveField("loadStatus")

        ' Move video info below the logo based on the logo height if not already moved
        if not m.logoMoved
            m.videoInfo.translation = `[${m.videoInfo.translation[0]}, ${m.videoInfo.translation[1] + logoImage.bitmapHeight + LOGO_RIGHT_PADDING}]`
            m.logoMoved = true
        end if
    end if
end sub

' onLogoImageChanged: Handler for changes to m.top.logoImage param.
'
sub onLogoImageChanged()
    if isValidAndNotEmpty(m.top.logoImage)
        logoImage = createObject("roSGNode", "Poster")
        logoImage.Id = "logoImage"
        logoImage.observeField("loadStatus", "onLogoLoadStatusChanged")
        logoImage.uri = m.top.logoImage
        logoImage.translation = [103, 61]
        m.top.appendChild(logoImage)
    end if
end sub

' onPreviousItemIconChanged: Handler for changes to m.top.previousItemIcon param.
'
sub onPreviousItemIconChanged()
    m.previousItem.icon = m.top.previousItemIcon
end sub

' onPreviousItemTitleTextChanged: Handler for changes to m.top.previousItemTitleText param.
'
sub onPreviousItemTitleTextChanged()
    m.previousItem.text = m.top.previousItemTitleText
end sub

' onNextItemIconChanged: Handler for changes to m.top.nextItemIcon param.
'
sub onNextItemIconChanged()
    m.nextItem.icon = m.top.nextItemIcon
end sub

' onNextItemTitleTextChanged: Handler for changes to m.top.nextItemTitleText param.
'
sub onNextItemTitleTextChanged()
    m.nextItem.text = m.top.nextItemTitleText
end sub

' resetFocusToDefaultButton: Reset focus back to the default button
'
sub resetFocusToDefaultButton()
    ' Remove focus from all buttons
    for each child in m.videoControls.getChildren(-1, 0)
        if isValid(child.focus)
            child.focus = false
        end if
    end for

    for each child in m.optionControls.getChildren(-1, 0)
        if isValid(child.focus)
            child.focus = false
        end if
    end for

    ' Set focus back to the default button (play/pause)
    m.videoControls.setFocus(true)
    m.optionControls.setFocus(false)
    m.currentButtonGroup = "video"
    m.focusedButtonIndex = m.defaultButtonIndex
    m.videoControls.getChild(m.defaultButtonIndex).focus = true
    m.videoControls.buttonFocused = m.defaultButtonIndex
end sub

' onVisibleChanged: Handler for changes to the visibility of this menu.
'
sub onVisibleChanged()
    if m.top.visible
        resetFocusToDefaultButton()
        m.inactivityTimer.observeField("fire", "inactiveCheck")
        m.inactivityTimer.control = "start"
        return
    end if

    m.inactivityTimer.unobserveField("fire")
    m.inactivityTimer.control = "stop"
end sub' onFocusChanged: Handler for changes to the focus of this menu.
'
sub onFocusChanged()
    if m.top.hasfocus
        if m.currentButtonGroup = "option"
            m.optionControls.setFocus(true)
        else
            m.videoControls.setFocus(true)
        end if
    end if
end sub

' inactiveCheck: Checks if the time since last keypress is greater than or equal to the allowed inactive time of the menu.
'
sub inactiveCheck()
    ' If user is currently seeing a dialog box, ignore inactive check
    if m.global.sceneManager.callFunc("isDialogOpen")
        return
    end if

    ' If inactiveTimeout is 0, keep OSD visible (don't auto-hide)
    if m.top.inactiveTimeout = 0
        return
    end if

    if m.deviceInfo.timeSinceLastKeypress() >= m.top.inactiveTimeout
        checkDisplaySiblingItem("")
        m.top.action = "hide"
    end if
end sub

' onButtonSelected: Handler for selection of buttons from the menu.
'
sub onButtonSelected()
    if m.currentButtonGroup = "option"
        selectedButton = m.optionControls.getChild(m.focusedButtonIndex)
    else
        selectedButton = m.videoControls.getChild(m.focusedButtonIndex)
    end if

    if LCase(selectedButton.id) = "chapterlist"
        m.top.showChapterList = not m.top.showChapterList
    end if

    checkDisplaySiblingItem("")

    m.top.action = selectedButton.id
end sub

' checkDisplaySiblingItem: Determine visible state for both previous and next item popups
'
sub checkDisplaySiblingItem(selectedButtonID as string)
    if not isValidAndNotEmpty(selectedButtonID) then selectedButtonID = ""

    selectedButtonID = LCase(selectedButtonID)

    showNextItem = false

    if selectedButtonID = "itemnext"
        showNextItem = true
    end if

    if not isValidAndNotEmpty(m.nextItem.text)
        showNextItem = false
    end if

    m.nextItem.visible = showNextItem

    showPreviousItem = false

    if selectedButtonID = "itemback"
        showPreviousItem = true
    end if

    if not isValidAndNotEmpty(m.previousItem.text)
        showPreviousItem = false
    end if

    m.previousItem.visible = showPreviousItem

end sub

function onKeyEvent(key as string, press as boolean) as boolean
    if not press then return false

    if key = "OK"
        onButtonSelected()
        return true
    end if

    if key = "play"
        checkDisplaySiblingItem("")
        m.top.action = "videoplaypause"
        return true
    end if

    if key = "right"
        ' Handle seeking if on seekbar
        if m.currentButtonGroup = "seekbar"
            totalTime = m.top.positionTime + m.top.remainingPositionTime

            ' If just entered seeking mode, initialize with current position
            if not m.isSeeking
                m.isSeeking = true
                m.seekPercentage = m.top.progressPercentage
            end if

            ' Increment by 1% of total video
            m.seekPercentage = m.seekPercentage + 0.01
            if m.seekPercentage > 1.0 then m.seekPercentage = 1.0

            newPosition = totalTime * m.seekPercentage

            ' Update visual position immediately
            m.progressBar.width = m.progressBarBackground.width * m.seekPercentage
            indicatorX = 103 + (m.progressBarBackground.width * m.seekPercentage) - 10
            m.progressIndicator.translation = [indicatorX, 912]

            ' Send seek action
            m.top.action = "seek," + newPosition.ToStr()
            return true
        end if

        ' Get current button group
        if m.currentButtonGroup = "option"
            buttonGroup = m.optionControls
        else
            buttonGroup = m.videoControls
        end if

        ' Navigate right in current row
        if m.focusedButtonIndex + 1 >= buttonGroup.getChildCount()
            return true
        end if

        focusedButton = buttonGroup.getChild(m.focusedButtonIndex)
        focusedButton.focus = false

        ' Skip spacer elements until next button is found
        for i = m.focusedButtonIndex + 1 to buttonGroup.getChildCount()
            m.focusedButtonIndex = i
            focusedButton = buttonGroup.getChild(m.focusedButtonIndex)

            if isValid(focusedButton.focus)
                buttonGroup.buttonFocused = m.focusedButtonIndex
                focusedButton.focus = true

                ' Check if we should display sibling item info (only for video controls)
                if m.currentButtonGroup = "video"
                    checkDisplaySiblingItem(focusedButton.id)
                end if
                exit for
            end if
        end for

        return true
    end if

    if key = "left"
        ' Handle seeking if on seekbar
        if m.currentButtonGroup = "seekbar"
            totalTime = m.top.positionTime + m.top.remainingPositionTime

            ' If just entered seeking mode, initialize with current position
            if not m.isSeeking
                m.isSeeking = true
                m.seekPercentage = m.top.progressPercentage
            end if

            ' Decrement by 1% of total video
            m.seekPercentage = m.seekPercentage - 0.01
            if m.seekPercentage < 0 then m.seekPercentage = 0

            newPosition = totalTime * m.seekPercentage

            ' Update visual position immediately
            m.progressBar.width = m.progressBarBackground.width * m.seekPercentage
            indicatorX = 103 + (m.progressBarBackground.width * m.seekPercentage) - 10
            m.progressIndicator.translation = [indicatorX, 912]

            ' Send seek action
            m.top.action = "seek," + newPosition.ToStr()
            return true
        end if

        ' Navigate left in current row
        if m.focusedButtonIndex = 0
            return true
        end if

        ' Get current button group
        if m.currentButtonGroup = "option"
            buttonGroup = m.optionControls
        else
            buttonGroup = m.videoControls
        end if

        focusedButton = buttonGroup.getChild(m.focusedButtonIndex)
        focusedButton.focus = false

        ' Skip spacer elements until next button is found
        for i = m.focusedButtonIndex - 1 to 0 step -1
            m.focusedButtonIndex = i
            focusedButton = buttonGroup.getChild(m.focusedButtonIndex)

            if isValid(focusedButton.focus)
                buttonGroup.buttonFocused = m.focusedButtonIndex
                focusedButton.focus = true

                ' Check if we should display sibling item info (only for video controls)
                if m.currentButtonGroup = "video"
                    checkDisplaySiblingItem(focusedButton.id)
                end if
                exit for
            end if
        end for

        return true
    end if

    if key = "down"
        ' Move from video controls to seekbar, or seekbar to option controls
        if m.currentButtonGroup = "video"
            focusedButton = m.videoControls.getChild(m.focusedButtonIndex)
            focusedButton.focus = false
            m.videoControls.setFocus(false)

            ' Switch to seekbar
            m.currentButtonGroup = "seekbar"
            m.seekbarGroup.setFocus(true)
            m.progressIndicator.blendColor = "0xFFFFFFFF"
            checkDisplaySiblingItem("")
        else if m.currentButtonGroup = "seekbar"
            ' Switch to option controls
            m.seekbarGroup.setFocus(false)
            m.progressIndicator.blendColor = "0xFFFFFFFF"
            m.isSeeking = false
            m.currentButtonGroup = "option"

            m.focusedButtonIndex = 0
            focusedButton = m.optionControls.getChild(m.focusedButtonIndex)
            focusedButton.focus = true
            m.optionControls.setFocus(true)
            m.optionControls.buttonFocused = m.focusedButtonIndex
        end if

        return true
    end if

    if key = "up"
        ' Move from seekbar to video controls, or option controls to seekbar
        if m.currentButtonGroup = "seekbar"
            ' Switch to video controls
            m.seekbarGroup.setFocus(false)
            m.progressIndicator.blendColor = "0xFFFFFFFF"
            m.isSeeking = false
            m.currentButtonGroup = "video"

            m.focusedButtonIndex = m.defaultButtonIndex
            focusedButton = m.videoControls.getChild(m.focusedButtonIndex)
            focusedButton.focus = true
            m.videoControls.setFocus(true)
            m.videoControls.buttonFocused = m.focusedButtonIndex

            focusedButtonID = focusedButton.id
            checkDisplaySiblingItem(focusedButtonID)
        else if m.currentButtonGroup = "option"
            focusedButton = m.optionControls.getChild(m.focusedButtonIndex)
            focusedButton.focus = false
            m.optionControls.setFocus(false)

            ' Switch to seekbar
            m.currentButtonGroup = "seekbar"
            m.seekbarGroup.setFocus(true)
            m.progressIndicator.blendColor = "0xFFFFFFFF"
        end if

        return true
    end if

    ' All other keys hide the menu
    checkDisplaySiblingItem("")
    m.top.action = "hide"
    return true
end function
