import "pkg:/source/utils/misc.bs"

sub init()
    m.ratingsLayout = m.top.findNode("ratingsLayout")
end sub

sub onRatingsDataChanged()
    ratingsData = m.top.ratingsData

    m.ratingsLayout.removeChildrenIndex(m.ratingsLayout.getChildCount(), 0)

    if not isValid(ratingsData)
        m.top.visible = false
        return
    end if

    displayRatings = ratingsData.data
    if not isValidAndNotEmpty(displayRatings)
        m.top.visible = false
        return
    end if

    iconHeight = m.top.iconHeight
    imdbIconWidth = iconHeight * 2
    labelFont = m.top.labelFont
    labelColor = m.top.labelColor
    m.ratingsLayout.itemSpacings = [m.top.ratingSpacing]

    for each rating in displayRatings
        ratingGroup = createObject("roSGNode", "LayoutGroup")
        ratingGroup.layoutDirection = "horiz"
        ratingGroup.itemSpacings = [5]
        ratingGroup.horizAlignment = "center"
        ratingGroup.vertAlignment = "center"

        iconPath = rating.iconPath ?? ""
        if iconPath <> ""
            icon = createObject("roSGNode", "Poster")
            icon.uri = iconPath
            if rating.source = "imdb"
                icon.width = imdbIconWidth
            else
                icon.width = iconHeight
            end if
            icon.height = iconHeight
            icon.loadDisplayMode = "scaleToFit"
            ratingGroup.appendChild(icon)
        end if

        formatted = rating.formatted ?? ""
        if formatted <> ""
            label = createObject("roSGNode", "Label")
            label.text = formatted
            label.font = labelFont
            label.color = labelColor
            ratingGroup.appendChild(label)
        end if

        m.ratingsLayout.appendChild(ratingGroup)
    end for

    m.top.visible = true
end sub
