import "pkg:/source/enums/ColorPalette.bs"
import "pkg:/source/utils/misc.bs"

sub init()
    m.background = m.top.findNode("background")
    m.unplayedNumber = m.top.findNode("unplayedNumber")
    m.checkmark = m.top.findNode("checkmark")
    m.checkmark.font.size = 30

    m.background.visible = false
    m.checkmark.visible = false
    m.unplayedNumber.visible = false

    onSizeChange()
end sub

sub onSizeChange()
    w = m.top.width
    h = m.top.height
    m.background.width = w
    m.background.height = h
    m.checkmark.width = w
    m.checkmark.height = h
    m.unplayedNumber.width = w
    m.unplayedNumber.height = h
end sub

sub onDataChange()
    m.background.visible = false

    if isValid(m.checkmark)
        m.checkmark.color = chainLookupReturn(m.global.session, "user.settings.colorPlayedCheckmarkIcon", ColorPalette.WHITE)
        m.checkmark.visible = false
    end if

    if isValid(m.unplayedNumber)
        m.unplayedNumber.color = chainLookupReturn(m.global.session, "user.settings.colorUnplayedCountTextColor", ColorPalette.WHITE)
        m.unplayedNumber.visible = false
    end if

    if not isValidAndNotEmpty(m.top.data) then return

    if chainLookupReturn(m.top.data, "unplayedCount", 0) > 0
        disableUnwatchedEpisodeCount = chainLookupReturn(m.global.session, "user.settings.`ui.tvshows.disableUnwatchedEpisodeCount`", false)
        if disableUnwatchedEpisodeCount then return

        unplayedCount = m.top.data.unplayedCount

        ' Show 99+ for counts over 99
        if unplayedCount > 99
            unplayedCountText = "99+"
        else
            unplayedCountText = unplayedCount.ToStr()
        end if

        unplayedCountLength = len(unplayedCountText)

        m.background.blendColor = "#0066CCFF"
        m.background.visible = true

        if isValid(m.unplayedNumber)
            m.unplayedNumber.font.size = unplayedCountLength >= 3 ? 16 : 20
            m.unplayedNumber.visible = true
            m.unplayedNumber.text = unplayedCountText
        end if
        return
    end if

    if not chainLookupReturn(m.top.data, "showWatchedCheckmark", true) then return

    if chainLookupReturn(m.top.data, "played", false)
        m.background.blendColor = chainLookupReturn(m.global.session, "user.settings.colorPlayedCheckmarkBackground", "#00A4DC")
        m.background.visible = true
        if isValid(m.checkmark)
            m.checkmark.color = "#FFFFFF"
            m.checkmark.visible = true
        end if
        return
    end if
end sub
