import "pkg:/source/enums/ColorPalette.bs"
import "pkg:/source/enums/ItemType.bs"
import "pkg:/source/enums/String.bs"
import "pkg:/source/utils/misc.bs"

sub init()
    m.mediaTypeIcon = m.top.findNode("mediaTypeIcon")
    m.itemTitle = m.top.findNode("itemTitle")
    m.itemSubtitle = m.top.findNode("itemSubtitle")
    m.itemInfo = m.top.findNode("itemInfo")
    m.itemRuntime = m.top.findNode("itemRuntime")
    m.playedIndicator = m.top.findNode("playedIndicator")
    m.rowBackground = m.top.findNode("rowBackground")
    m.focusInAnimation = m.top.findNode("focusInAnimation")
    m.focusOutAnimation = m.top.findNode("focusOutAnimation")
end sub

sub itemContentChanged()
    itemData = m.top.itemContent
    if not isValid(itemData) then return

    m.itemTitle.text = isValid(itemData.title) ? itemData.title : ""

    playlistItemType = LCase(itemData.LookupCI("type") ?? "")
    setMediaTypeInfo(itemData, playlistItemType)

    if inArray([ItemType.MOVIE, ItemType.MUSICVIDEO, ItemType.EPISODE, ItemType.RECORDING, ItemType.VIDEO, ItemType.PROGRAM], playlistItemType)
        m.playedIndicator.data = {
            played: chainLookupReturn(itemData, "played", false)
        }
        m.playedIndicator.visible = true
    else
        m.playedIndicator.visible = false
    end if

    if isValid(itemData.LookupCI("RunTimeTicks"))
        ticks = itemData.RunTimeTicks
        if type(ticks) = "roFloat" or type(ticks) = "Float" or type(ticks) = "roInt" or type(ticks) = "LongInteger"
            m.itemRuntime.text = ticksToHuman(ticks)
        end if
    else
        m.itemRuntime.text = ""
    end if
end sub

sub setMediaTypeInfo(itemData as object, playlistItemType as string)
    subtitleText = ""
    infoText = ""

    if isStringEqual(playlistItemType, ItemType.AUDIO)
        m.mediaTypeIcon.uri = "pkg:/images/icons/musicNote.png"

        ' Artist - Album
        if isValid(itemData.LookupCI("artists"))
            subtitleText = itemData.artists.join(", ")
        end if
        if isValidAndNotEmpty(itemData.LookupCI("album"))
            if isValidAndNotEmpty(subtitleText)
                infoText = itemData.album
            else
                subtitleText = itemData.album
            end if
        end if

    else if isStringEqual(playlistItemType, ItemType.EPISODE)
        m.mediaTypeIcon.uri = "pkg:/images/media_type_icons/tv.png"

        ' Series name
        if isValidAndNotEmpty(itemData.LookupCI("seriesname"))
            subtitleText = itemData.seriesname
        end if

        ' Season/Episode info
        episodeInfo = ""
        if isValid(itemData.LookupCI("ParentIndexNumber"))
            if itemData.ParentIndexNumber <> 0
                episodeInfo = `S${itemData.ParentIndexNumber}`
            end if
        end if
        if isValid(itemData.LookupCI("IndexNumber"))
            episodeInfo += `E${itemData.IndexNumber}`
            if isValid(itemData.LookupCI("IndexNumberEnd"))
                if itemData.IndexNumberEnd <> 0
                    episodeInfo += `-${itemData.IndexNumberEnd}`
                end if
            end if
        end if
        if isValidAndNotEmpty(episodeInfo)
            infoText = episodeInfo
        end if

    else if isStringEqual(playlistItemType, ItemType.MOVIE) or isStringEqual(playlistItemType, ItemType.MUSICVIDEO)
        m.mediaTypeIcon.uri = "pkg:/images/media_type_icons/movie.png"

        ' Year - Rating
        parts = []
        if isValid(itemData.LookupCI("ProductionYear"))
            parts.push(`${itemData.ProductionYear}`)
        end if
        if isValidAndNotEmpty(itemData.LookupCI("OfficialRating"))
            parts.push(itemData.OfficialRating)
        end if
        if parts.count() > 0
            subtitleText = parts.join(" Â· ")
        end if

    else
        ' Generic fallback
        m.mediaTypeIcon.uri = "pkg:/images/icons/play.png"
    end if

    m.itemSubtitle.text = subtitleText
    m.itemInfo.text = infoText

    ' Hide empty lines
    m.itemSubtitle.visible = isValidAndNotEmpty(subtitleText)
    m.itemInfo.visible = isValidAndNotEmpty(infoText)
end sub

sub focusChanged()
    if m.top.itemHasFocus
        m.focusOutAnimation.control = "stop"
        m.focusInAnimation.control = "start"

        if m.global.device.isAudioGuideEnabled = true
            txt2Speech = CreateObject("roTextToSpeech")
            txt2Speech.Flush()
            txt2Speech.Say(m.itemTitle.text)
            if m.itemSubtitle.visible then txt2Speech.Say(m.itemSubtitle.text)
        end if
    else
        m.focusInAnimation.control = "stop"
        m.focusOutAnimation.control = "start"
    end if
end sub
