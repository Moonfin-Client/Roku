import "pkg:/source/api/baserequest.bs"
import "pkg:/source/api/Image.bs"
import "pkg:/source/enums/AnimationControl.bs"
import "pkg:/source/enums/ColorPalette.bs"
import "pkg:/source/enums/ItemType.bs"
import "pkg:/source/enums/PosterLoadStatus.bs"
import "pkg:/source/enums/String.bs"
import "pkg:/source/utils/config.bs"
import "pkg:/source/utils/misc.bs"

sub init()
    findNodes()
    m.itemPoster.observeField("loadStatus", "onPosterLoadStatusChanged")
    setColors()

    ' Track focus change timing to detect and prevent oscillations
    m.lastFocusChangeTime = 0
    m.focusChangeCount = 0
    m.isInOscillation = false
    m.lastStableFocusState = false
    m.initTime = UpTime(0)
    m.initCooldownPeriod = 0.5 ' Block animations for 500ms after creation
end sub

sub setColors()
    if not isAllValid([m.itemProgress, m.itemProgressBackground, m.backdrop]) then findNodes()

    if isValid(m.itemProgress)
        m.itemProgress.color = chainLookupReturn(m.global.session, "user.settings.colorHomeRowItemProgress", ColorPalette.TRIADBLUE)
    end if

    if isValid(m.itemProgressBackground)
        m.itemProgressBackground.color = chainLookupReturn(m.global.session, "user.settings.colorHomeRowItemProgressBackground", ColorPalette.BLACK90)
    end if

    if isValid(m.backdrop)
        m.backdrop.color = chainLookupReturn(m.global.session, "user.settings.colorHomeRowItemBackground", ColorPalette.ELEMENTBACKGROUND)
    end if
end sub

sub findNodes()
    initItemText()
    initItemPoster()
    initBackdrop()
    initItemIcon()
    m.itemProgress = m.top.findNode("progress")
    m.itemProgressBackground = m.top.findNode("progressBackground")
    m.playedIndicator = m.top.findNode("playedIndicator")
    m.showProgressBarAnimation = m.top.findNode("showProgressBar")
    m.showProgressBarField = m.top.findNode("showProgressBarField")
    m.scaleGroup = m.top.findNode("scaleGroup")
    m.focusAnimation = m.top.findNode("focusAnimation")
    m.unfocusAnimation = m.top.findNode("unfocusAnimation")
end sub

sub initItemPoster()
    m.itemPoster = m.top.findNode("itemPoster")
end sub

sub initBackdrop()
    m.backdrop = m.top.findNode("backdrop")
end sub

sub initItemIcon()
    m.itemIcon = m.top.findNode("itemIcon")
    if not isValid(m.itemIcon) then return

    m.itemIcon.blendcolor = chainLookupReturn(m.global.session, "user.settings.colorHomeMyListIcon", ColorPalette.WHITE)
end sub

sub initItemText()
    m.itemText = m.top.findNode("itemText")
    if isValid(m.itemText)
        m.itemText.color = chainLookupReturn(m.global.session, "user.settings.colorHomeRowItemTitle", ColorPalette.WHITE)
    end if
end sub

sub itemContentChanged()
    itemData = m.top.itemContent

    if not isValid(itemData) then return

    if isValid(m.playedIndicator)
        m.playedIndicator.data = {
            played: chainLookupReturn(itemData, "json.UserData.Played", false),
            unplayedCount: chainLookupReturn(itemData, "json.UserData.UnplayedItemCount", 0)
        }
    end if

    setColors()
    initItemIcon()

    ' validate to prevent crash
    if not isValid(m.itemPoster) then initItemPoster()
    if not isValid(m.backdrop) then initBackdrop()
    if not isValid(m.itemIcon) then initItemIcon()

    m.itemPoster.width = itemData.imageWidth
    m.backdrop.width = itemData.imageWidth

    if isValid(m.itemText)
        m.itemText.text = itemData.LookupCI("Title")
        m.itemText.maxWidth = itemData.imageWidth
    end if

    if isAllValid([m.itemIcon, itemData.iconUrl])
        m.itemIcon.uri = itemData.iconUrl
    end if

    if itemData.LookupCI("PlayedPercentage") > 0
        drawProgressBar(itemData)
    end if

    ' Set poster image - prefer primary (posterURL) first, then thumbnail
    if isValidAndNotEmpty(itemData.posterURL)
        m.itemPoster.uri = itemData.posterURL
    else if isValidAndNotEmpty(itemData.thumbnailURL)
        m.itemPoster.uri = itemData.thumbnailURL
    else if isValidAndNotEmpty(itemData.widePosterURL)
        m.itemPoster.uri = itemData.widePosterURL
    end if
end sub

'
' Draws and animates item progress bar
sub drawProgressBar(itemData)
    if not isValid(itemData.LookupCI("imageWidth")) then return

    m.itemProgressBackground.width = itemData.LookupCI("imageWidth")
    m.itemProgressBackground.visible = true
    m.showProgressBarField.keyValue = [0, m.itemPoster.width * (itemData.PlayedPercentage / 100)]
    m.showProgressBarAnimation.control = AnimationControl.START
end sub

sub focusChanged()
    currentTime = UpTime(0)

    ' Block ALL animations during initialization cooldown
    timeSinceInit = currentTime - m.initTime
    if timeSinceInit < m.initCooldownPeriod
        ' Still in cooldown - just update text scrolling and exit
        if isValid(m.itemText)
            m.itemText.repeatCount = m.top.itemHasFocus ? - 1 : 0
        end if
        return
    end if

    timeSinceLastChange = currentTime - m.lastFocusChangeTime

    ' Detect rapid focus oscillation (multiple changes within 200ms)
    if timeSinceLastChange < 0.2
        m.focusChangeCount++
        if m.focusChangeCount >= 2
            m.isInOscillation = true
        end if
    else
        ' Reset counter if enough time has passed
        m.focusChangeCount = 0
        if m.isInOscillation
            ' Exiting oscillation - save the stable state
            m.lastStableFocusState = m.top.itemHasFocus
        end if
        m.isInOscillation = false
    end if

    m.lastFocusChangeTime = currentTime

    ' Always update text scrolling
    if isValid(m.itemText)
        m.itemText.repeatCount = m.top.itemHasFocus ? - 1 : 0
    end if

    ' Only animate if NOT in oscillation
    if not m.isInOscillation
        ' Save stable state before animating
        m.lastStableFocusState = m.top.itemHasFocus

        ' Handle focus/unfocus animations
        if m.top.itemHasFocus
            if isValid(m.focusAnimation)
                m.unfocusAnimation.control = "stop"
                m.focusAnimation.control = "start"
            end if
        else
            if isValid(m.unfocusAnimation)
                m.focusAnimation.control = "stop"
                m.unfocusAnimation.control = "start"
            end if
        end if
    end if
    ' During oscillation, do nothing - keep whatever scale we had from last stable state
    ' This prevents the visual bounce
end sub'Hide backdrop and icon when poster loaded
sub onPosterLoadStatusChanged()
    if m.itemPoster.loadStatus = PosterLoadStatus.FAILED
        ' Primary poster failed to load, try thumbnail
        if isStringEqual(m.itemPoster.uri, m.top.itemContent.posterURL)
            m.itemPoster.uri = m.top.itemContent.thumbnailURL
            return
        end if

        ' Thumbnail failed to load, try wide poster
        if isStringEqual(m.itemPoster.uri, m.top.itemContent.thumbnailURL)
            m.itemPoster.uri = m.top.itemContent.widePosterURL
            return
        end if

        ' All images failed. Show blank backdrop
        m.backdrop.visible = true
        m.itemIcon.visible = true

        return
    end if

    if m.itemPoster.loadStatus = PosterLoadStatus.READY and m.itemPoster.uri <> string.EMPTY
        m.backdrop.visible = false
        m.itemIcon.visible = false
    else
        m.backdrop.visible = true
        m.itemIcon.visible = true
    end if
end sub
