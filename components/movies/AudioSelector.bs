import "pkg:/source/enums/ColorPalette.bs"
import "pkg:/source/enums/KeyCode.bs"
import "pkg:/source/utils/misc.bs"

sub init()
    m.audioMenu = m.top.findNode("audioMenu")
    m.audioMenu.focusBitmapBlendColor = chainLookupReturn(m.global.session, "user.settings.colorCursor", ColorPalette.HIGHLIGHT)
    m.audioMenu.focusedColor = ColorPalette.WHITE
    m.audioMenu.setFocus(true)

    m.selectedAudioIndex = 0
end sub

sub audioTracksSet()
    if isValid(m.top.audioTracks)
        audioContent = CreateObject("roSGNode", "ContentNode")
        index = 0
        selectedAudioIndex = 0

        for each audio in m.top.audioTracks
            entry = audioContent.CreateChild("ContentNode")
            entry.title = audio.Title
            entry.description = audio.Title
            entry.StreamIndex = audio.StreamIndex
            if isValid(audio.Selected) and audio.Selected
                selectedAudioIndex = index
                entry.selected = true
            end if
            index = index + 1
        end for

        m.audioMenu.content = audioContent
        m.audioMenu.jumpToItem = selectedAudioIndex
        m.audioMenu.checkedItem = selectedAudioIndex
        m.selectedAudioIndex = selectedAudioIndex
    end if
end sub

function onKeyEvent(key as string, press as boolean) as boolean
    if not press then return false

    if key = KeyCode.OK
        ' User selected an audio track
        selectedItem = m.audioMenu.content.getChild(m.audioMenu.itemFocused)
        if isValid(selectedItem)
            m.top.audioStreamIndex = selectedItem.StreamIndex
            m.top.audioStreamName = selectedItem.title
            m.top.visible = false
        end if
        return true
    else if key = KeyCode.BACK
        m.top.visible = false
        return true
    end if

    return false
end function
