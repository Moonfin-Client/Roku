import "pkg:/source/enums/ColorPalette.bs"
import "pkg:/source/enums/KeyCode.bs"
import "pkg:/source/enums/SubtitleSelection.bs"
import "pkg:/source/utils/misc.bs"

sub init()
    m.subtitleMenu = m.top.findNode("subtitleMenu")
    m.subtitleMenu.focusBitmapBlendColor = chainLookupReturn(m.global.session, "user.settings.colorCursor", ColorPalette.HIGHLIGHT)
    m.subtitleMenu.focusedColor = ColorPalette.WHITE
    m.subtitleMenu.setFocus(true)

    m.selectedSubtitleIndex = SubtitleSelection.NONE
end sub

sub subtitleTracksSet()
    if isValid(m.top.subtitleTracks)
        subtitleContent = CreateObject("roSGNode", "ContentNode")
        index = 0
        selectedSubtitleIndex = 0

        for each subtitle in m.top.subtitleTracks
            entry = subtitleContent.CreateChild("SubtitleTrackListData")
            entry.json = subtitle.json
            entry.title = subtitle.Title
            entry.description = subtitle.Title
            entry.StreamIndex = subtitle.StreamIndex
            if isValid(subtitle.Selected) and subtitle.Selected
                selectedSubtitleIndex = index
                entry.selected = true
            end if
            index = index + 1
        end for

        m.subtitleMenu.content = subtitleContent
        m.subtitleMenu.jumpToItem = selectedSubtitleIndex
        m.subtitleMenu.checkedItem = selectedSubtitleIndex
        m.selectedSubtitleIndex = selectedSubtitleIndex
    end if
end sub

function onKeyEvent(key as string, press as boolean) as boolean
    if not press then return false

    if key = KeyCode.OK
        ' User selected a subtitle track
        selectedItem = m.subtitleMenu.content.getChild(m.subtitleMenu.itemFocused)
        if isValid(selectedItem)
            m.top.subtitleStream = selectedItem
            m.top.visible = false
        end if
        return true
    else if key = KeyCode.BACK
        m.top.visible = false
        return true
    end if

    return false
end function
