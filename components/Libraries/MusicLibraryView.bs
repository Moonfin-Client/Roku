
import "pkg:/source/api/baserequest.bs"
import "pkg:/source/api/Image.bs"
import "pkg:/source/enums/AnimationControl.bs"
import "pkg:/source/enums/AnimationState.bs"
import "pkg:/source/enums/ColorPalette.bs"
import "pkg:/source/enums/ImageLayout.bs"
import "pkg:/source/enums/ItemType.bs"
import "pkg:/source/enums/KeyCode.bs"
import "pkg:/source/enums/SearchButtonState.bs"
import "pkg:/source/enums/String.bs"
import "pkg:/source/enums/TaskControl.bs"
import "pkg:/source/utils/config.bs"
import "pkg:/source/utils/deviceCapabilities.bs"
import "pkg:/source/utils/misc.bs"

function getCollectionType() as string
    if m.top.parentItem.collectionType = invalid
        return LCase(m.top.parentItem.Type)
    else
        return LCase(m.top.parentItem.CollectionType)
    end if
end function

sub setupNodes()
    m.toggleDropdownOptionsAnimation = m.top.findNode("toggleDropdownOptionsAnimation")
    m.dropdownOptionsMove = m.top.findNode("dropdownOptionsMove")
    m.itemGridMove = m.top.findNode("itemGridMove")
    m.genreListMove = m.top.findNode("genreListMove")
    m.dropdownOptionsFade = m.top.findNode("dropdownOptionsFade")
    m.dropdownOptionsContainer = m.top.findNode("dropdownOptionsContainer")
    m.options = m.top.findNode("options")
    m.itemGrid = m.top.findNode("itemGrid")
    m.backdrop = m.top.findNode("backdrop")
    m.newBackdrop = m.top.findNode("backdropTransition")
    m.emptyText = m.top.findNode("emptyText")
    m.selectedArtistName = m.top.findNode("selectedArtistName")
    m.selectedArtistSongCount = m.top.findNode("selectedArtistSongCount")
    m.selectedArtistAlbumCount = m.top.findNode("selectedArtistAlbumCount")
    m.selectedArtistGenres = m.top.findNode("selectedArtistGenres")
    m.artistLogo = m.top.findNode("artistLogo")
    m.swapAnimation = m.top.findNode("backroundSwapAnimation")
    m.alpha = m.top.findNode("alpha")
    m.alphaMenu = m.alpha.findNode("alphaMenu")
    m.overhang = m.top.getScene().findNode("overhang")
    m.genreList = m.top.findNode("genrelist")
    m.dropdownOptions = m.top.findNode("dropdownOptions")

    m.sortButton = m.top.findNode("sortButton")
    m.sortButton.textColor = ColorPalette.WHITE
    m.sortButton.focusTextColor = "#000000"
    m.sortButton.background = ColorPalette.DARKGREY
    m.sortButton.focusBackground = ColorPalette.WHITE

    m.sortOrderButton = m.top.findNode("sortOrderButton")
    m.sortOrderButton.textColor = ColorPalette.WHITE
    m.sortOrderButton.focusTextColor = "#000000"
    m.sortOrderButton.background = ColorPalette.DARKGREY
    m.sortOrderButton.focusBackground = ColorPalette.WHITE

    m.filterButton = m.top.findNode("filterButton")
    m.filterButton.textColor = ColorPalette.WHITE
    m.filterButton.focusTextColor = "#000000"
    m.filterButton.background = ColorPalette.DARKGREY
    m.filterButton.focusBackground = ColorPalette.WHITE

    m.viewButton = m.top.findNode("viewButton")
    m.viewButton.textColor = ColorPalette.WHITE
    m.viewButton.focusTextColor = "#000000"
    m.viewButton.background = ColorPalette.DARKGREY
    m.viewButton.focusBackground = ColorPalette.WHITE


end sub

sub init()
    m.top.imageDisplayMode = "scaleToZoom"
    m.top.showItemTitles = "showonhover"

    setupNodes()
    m.bypassSearchEvent = false

    m.itemGrid.focusBitmapBlendColor = chainLookupReturn(m.global.session, "user.settings.colorCursor", ColorPalette.HIGHLIGHT)
    m.genrelist.focusBitmapBlendColor = chainLookupReturn(m.global.session, "user.settings.colorCursor", ColorPalette.HIGHLIGHT)

    m.overhang.isVisible = false

    m.options.observeField("visible", "onOptionsVisibleChange")
    m.swapAnimation.observeField("state", "swapDone")

    m.loadedRows = 0
    m.loadedItems = 0

    m.data = createSGNode("ContentNode")

    m.itemGrid.content = m.data

    m.genreData = createSGNode("ContentNode")
    m.genreList.observeField("itemSelected", "onGenreItemSelected")
    m.genreList.observeField("itemFocused", "onGenreItemFocused")
    m.genreList.content = m.genreData

    m.itemGrid.observeField("itemFocused", "onItemFocused")
    m.itemGrid.observeField("itemSelected", "onItemSelected")

    'backdrop
    m.newBackdrop.observeField("loadStatus", "newBGLoaded")

    'Background Image Queued for loading
    m.queuedBGUri = ""

    'Item sort - maybe load defaults from user prefs?
    m.sortField = "SortName"
    m.sortAscending = true

    m.filter = "All"

    m.loadItemsTask = createObject("roSGNode", "LoadItemsTask2")
    m.loadLogoTask = createObject("roSGNode", "LoadItemsTask2")
    m.getFiltersTask = createObject("roSGNode", "GetFiltersTask")

    'set inital counts for overhang before content is loaded.
    m.loadItemsTask.totalRecordCount = 0
end sub

sub onSortChange()
    m.global.sceneManager.unobserveFieldScoped("returnData")
    sortChoice = m.global.sceneManager.returnData
    if not isChainValid(sortChoice, "name") then return
    if isStringEqual(sortChoice.LookupCI("name"), m.sortField) then return

    m.sortButton.focus = false
    m.itemGrid.setFocus(m.itemGrid.opacity = 1)
    m.genreList.setFocus(m.genreList.opacity = 1)

    m.sortField = sortChoice.LookupCI("name")
    m.sortButton.text = tr(sortChoice.LookupCI("title"))

    m.sortAscending = true

    set_user_setting("display." + m.settingID + ".sortField", m.sortField)

    m.loadedRows = 0
    m.loadedItems = 0
    m.data = createSGNode("ContentNode")
    m.itemGrid.content = m.data
    loadInitialItems()
end sub

sub onViewChange()
    m.global.sceneManager.unobserveFieldScoped("returnData")
    viewChoice = m.global.sceneManager.returnData
    if not isChainValid(viewChoice, "name") then return
    if isStringEqual(viewChoice.LookupCI("name"), m.view) then return

    m.viewButton.focus = false
    m.itemGrid.setFocus(m.itemGrid.opacity = 1)
    m.genreList.setFocus(m.genreList.opacity = 1)

    m.view = viewChoice.LookupCI("name")
    m.viewButton.text = tr(viewChoice.LookupCI("title"))

    set_user_setting("display." + m.settingID + ".landing", m.view)

    ' Reset any filtering or search terms
    m.bypassSearchEvent = true
    m.top.searchTerm = string.EMPTY
    m.loadItemsTask.nameStartsWith = " "
    m.loadItemsTask.searchTerm = string.EMPTY
    m.filter = "All"
    m.filterOptions = {}
    m.sortField = "SortName"
    m.sortAscending = true

    ' Reset view to defaults
    set_user_setting("display." + m.settingID + ".sortField", m.sortField)
    set_user_setting("display." + m.settingID + ".sortAscending", "true")
    set_user_setting("display." + m.settingID + ".filter", m.filter)
    set_user_setting("display." + m.settingID + ".filterOptions", FormatJson(m.filterOptions))

    m.loadedRows = 0
    m.loadedItems = 0
    m.data = createSGNode("ContentNode")
    m.itemGrid.content = m.data
    loadInitialItems()
end sub

sub onSortOrderChange()
    m.global.sceneManager.unobserveFieldScoped("returnData")
    sortOrderChoice = m.global.sceneManager.returnData
    if not isChainValid(sortOrderChoice, "name") then return
    if m.sortAscending = isStringEqual(sortOrderChoice.LookupCI("name"), "Ascending") then return

    m.sortOrderButton.focus = false
    m.itemGrid.setFocus(m.itemGrid.opacity = 1)
    m.genreList.setFocus(m.genreList.opacity = 1)

    m.sortAscending = isStringEqual(sortOrderChoice.LookupCI("name"), "Ascending")
    m.sortOrderButton.text = tr(sortOrderChoice.LookupCI("title"))

    set_user_setting("display." + m.settingID + ".sortAscending", m.sortAscending.toStr())

    m.loadedRows = 0
    m.loadedItems = 0
    m.data = createSGNode("ContentNode")
    m.itemGrid.content = m.data
    loadInitialItems()
end sub

sub OnScreenHidden()
    if not m.overhang.isVisible
        m.overhang.disableMoveAnimation = true
        m.overhang.isVisible = true
        m.overhang.disableMoveAnimation = false
    end if
end sub

sub OnScreenShown(_isReturning = false as boolean)

    if isValid(m.top.lastFocus)
        m.top.lastFocus.setFocus(true)
    else
        m.top.setFocus(true)
        setLastFocus(m.top)
    end if
end sub

'
'Load initial set of Data
sub loadInitialItems()
    m.loadItemsTask.control = TaskControl.STOP
    startLoadingSpinner(false)

    if isValid(m.top.parentItem) and isValid(m.top.parentItem.json) and LCase(m.top.parentItem.json.Type) = "collectionfolder"
        m.top.HomeLibraryItem = m.top.parentItem.Id
    end if

    if m.top.parentItem.backdropUrl <> invalid
        SetBackground(m.top.parentItem.backdropUrl)
    else
        SetBackground("")
    end if

    if m.dropdownOptionsContainer.opacity < 1
        toggleDropdownOptions(true)
    end if

    if isValid(m.top.parentItem.json.jumpToFilter)
        m.settingID = "jumpToFilter"
    else
        m.settingID = m.top.parentItem.Id
    end if

    m.sortField = m.global.session.user.settings["display." + m.settingID + ".sortField"]
    m.sortAscending = m.global.session.user.settings["display." + m.settingID + ".sortAscending"]
    m.filter = m.global.session.user.settings["display." + m.settingID + ".filter"]
    m.filterOptions = m.global.session.user.settings["display." + m.settingID + ".filterOptions"]
    m.view = m.global.session.user.settings["display." + m.settingID + ".landing"]

    if not isValidAndNotEmpty(m.sortField) then m.sortField = "SortName"
    if not isValidAndNotEmpty(m.filter) then m.filter = "All"
    if not isValidAndNotEmpty(m.filterOptions) then m.filterOptions = "{}"
    if not isValidAndNotEmpty(m.view) then m.view = "AllMusic"
    if not isValid(m.sortAscending) then m.sortAscending = true
    if not isStringEqual(type(m.sortAscending), "roBoolean") then m.sortAscending = true

    ' We're inside a genre
    if isValid(m.top.parentItem) and isValid(m.top.parentItem.json) and isStringEqual(m.top.parentItem.json.type, "musicgenre")
        if not isStringEqual(m.view, "albums") then m.view = "albums"
    end if

    m.filterOptions = ParseJson(m.filterOptions)

    m.top.showItemTitles = m.global.session.user.settings["itemgrid.gridTitles"]

    m.loadItemsTask.searchTerm = m.top.searchTerm
    m.emptyText.visible = false
    m.loadItemsTask.sortField = m.sortField
    m.loadItemsTask.sortAscending = m.sortAscending
    m.loadItemsTask.filter = m.filter
    m.loadItemsTask.filterOptions = m.filterOptions
    m.loadItemsTask.startIndex = 0

    ' Load Item Types
    if getCollectionType() = "music"
        ' Default to showing all music items
        m.loadItemsTask.itemType = "MusicAlbum,MusicArtist,Audio"
        m.loadItemsTask.itemId = m.top.parentItem.Id
    end if

    ' By default we load all music view
    m.loadItemsTask.view = "AllMusic"
    m.loadItemsTask.genreIds = ""
    m.itemGrid.translation = "[100, 290]"
    m.itemGrid.numRows = "4"
    m.dropdownOptionsContainer.translation = "[260, 140]"
    if m.dropdownOptionsContainer.opacity < 1
        m.itemGrid.translation = "[100, 140]"
    else
        m.itemGrid.translation = "[100, 290]"
    end if

    m.dropdownOptionsMove.keyValue = "[[260, 140], [260, 80]]"
    m.itemGridMove.keyValue = "[[100, 290], [100, 140]]"
    m.emptyText.translation = "[0, 540]"

    ' We're inside a genre
    if isValid(m.top.parentItem) and isValid(m.top.parentItem.json) and isStringEqual(m.top.parentItem.json.type, "musicgenre")
        m.loadItemsTask.itemType = "MusicAlbum"
        m.loadItemsTask.recursive = true
        m.loadItemsTask.genreIds = m.top.parentItem.id
        m.loadItemsTask.itemId = m.top.parentItem.parentFolder
    end if

    if isStringEqual(m.view, "AllMusic")
        m.loadItemsTask.itemType = "MusicAlbum,MusicArtist,Audio"
        m.loadItemsTask.recursive = true
        m.top.imageDisplayMode = "scaleToFit"
    else if isStringEqual(m.view, "albums")
        m.loadItemsTask.itemType = "MusicAlbum"
        m.top.imageDisplayMode = "scaleToFit"
    else if isStringEqual(m.view, "artists")
        m.loadItemsTask.itemType = "MusicArtist"
    else if isStringEqual(m.view, "songs")
        m.loadItemsTask.itemType = "Audio"
        m.loadItemsTask.recursive = true
    else if isStringEqual(m.view, "FavoriteAlbums")
        m.loadItemsTask.itemType = "MusicAlbum"
        m.filter = "Favorites"
        m.top.imageDisplayMode = "scaleToFit"
    else if isStringEqual(m.view, "FavoriteArtists")
        m.loadItemsTask.itemType = "MusicArtist"
        m.filter = "Favorites"
    else if isStringEqual(m.view, "FavoriteSongs")
        m.loadItemsTask.itemType = "Audio"
        m.loadItemsTask.recursive = true
        m.filter = "Favorites"
    else if isStringEqual(m.view, "albumartistsgrid")
        m.loadItemsTask.itemType = "AlbumArtists"
    else if isStringEqual(m.view, "albumartistspresentation")
        m.emptyText.translation = "[0, 750]"
        m.loadItemsTask.itemType = "AlbumArtists"
        m.itemGrid.translation = "[100, 750]"
        m.itemGrid.numRows = "2"
        m.dropdownOptionsContainer.translation = "[260, 620]"
        if m.dropdownOptionsContainer.opacity < 1
            m.itemGrid.translation = "[100, 600]"
        else
            m.itemGrid.translation = "[100, 750]"
        end if
        m.dropdownOptionsMove.keyValue = "[[260, 620], [260, 560]]"
        m.itemGridMove.keyValue = "[[100, 750], [100, 600]]"
    else if isStringEqual(m.view, "ArtistsPresentation")
        m.emptyText.translation = "[0, 750]"
        m.itemGrid.translation = "[100, 750]"
        m.itemGrid.numRows = "2"
        m.dropdownOptionsContainer.translation = "[260, 620]"
        if m.dropdownOptionsContainer.opacity < 1
            m.itemGrid.translation = "[100, 600]"
        else
            m.itemGrid.translation = "[100, 750]"
        end if
        m.dropdownOptionsMove.keyValue = "[[260, 620], [260, 560]]"
        m.itemGridMove.keyValue = "[[100, 750], [100, 600]]"
    else if isStringEqual(m.view, "genres")
        m.loadItemsTask.itemType = ""
        m.loadItemsTask.recursive = true
        m.loadItemsTask.view = "Genres"
        m.artistLogo.visible = false
        m.selectedArtistName.visible = false
    end if

    setColumnSizes()

    m.loadItemsTask.observeField("content", "ItemDataLoaded")
    m.loadItemsTask.control = TaskControl.RUN

    m.getFiltersTask.observeField("filters", "FilterDataLoaded")
    m.getFiltersTask.params = {
        userid: m.global.session.user.id,
        parentid: m.top.parentItem.Id
    }
    m.getFiltersTask.control = TaskControl.RUN
end sub

sub setColumnSizes()
    numberOfColumns = chainLookupReturn(m.global.session, "user.settings.numberOfColumnsSquare", "6")
    imageWidthData = val(chainLookupReturn(m.global.session, "user.settings.numberOfColumnsSquareData", "270"))

    defaultSize = [imageWidthData, imageWidthData]

    m.itemGrid.itemSize = defaultSize
    m.itemGrid.rowHeights = [defaultSize[1]]
    m.itemGrid.numRows = abs(1230 / (defaultSize[1] + m.itemGrid.itemSpacing[1]))
    m.itemGrid.numColumns = numberOfColumns

    m.genreList.itemSize = defaultSize
    m.genreList.rowHeights = [defaultSize[1]]
    m.genreList.numRows = abs(1230 / (defaultSize[1] + m.itemGrid.itemSpacing[1]))
    m.genreList.numColumns = numberOfColumns

    m.loadItemsTask.numberOfColumns = numberOfColumns
end sub

'
'Check if options updated and any reloading required
sub onOptionsVisibleChange()
    if m.options.visible then return

    reload = false

    ' Nothing changed
    if isStringEqual(m.options.filter, m.filter)
        if AssocArrayEqual(m.options.filterOptions, m.filterOptions)
            m.filterButton.focus = true
            m.filterButton.setfocus(true)
            return
        end if
    end if

    if m.options.filter <> m.filter
        m.filter = m.options.filter
        reload = true
        set_user_setting("display." + m.settingID + ".filter", m.options.filter)
    end if

    if not isValid(m.options.filterOptions)
        m.filterOptions = {}
    end if

    if not AssocArrayEqual(m.options.filterOptions, m.filterOptions)
        m.filterOptions = m.options.filterOptions
        reload = true
        set_user_setting("display." + m.settingID + ".filterOptions", FormatJson(m.options.filterOptions))
    end if

    if reload
        m.loadedRows = 0
        m.loadedItems = 0
        m.data = createSGNode("ContentNode")
        m.itemGrid.content = m.data
        loadInitialItems()
    end if

    m.filterButton.focus = false

    m.itemGrid.setFocus(m.itemGrid.opacity = 1)
    m.genreList.setFocus(m.genreList.opacity = 1)

    group = m.global.sceneManager.callFunc("getActiveScene")
    group.lastFocus = m.itemGrid.opacity = 1 ? m.itemGrid : m.genreList
end sub

'
' Filter Data Loaded Event Handler
sub FilterDataLoaded(msg)
    options = setMusicOptions()

    data = msg.GetData()
    m.getFiltersTask.unobserveField("filters")

    if not isValid(data) then return

    ' Add Music filters from the API data
    if inArray(["musicartist", "albumartists"], LCase(m.loadItemsTask.itemType))
        if isValid(data.genres)
            options.filter.push({ "Title": tr("Genres"), "Name": "Genres", "Options": data.genres, "Delimiter": "|", "CheckedState": [] })
        end if
    end if

    if LCase(m.loadItemsTask.itemType) = "musicalbum"
        if isValid(data.genres)
            options.filter.push({ "Title": tr("Genres"), "Name": "Genres", "Options": data.genres, "Delimiter": "|", "CheckedState": [] })
        end if
        if isValid(data.Years)
            options.filter.push({ "Title": tr("Years"), "Name": "Years", "Options": data.Years, "Delimiter": ",", "CheckedState": [] })
        end if
    end if

    setSelectedOptions(options)

    m.options.options = options
end sub

' Data to display when options button selected
sub setSelectedOptions(options)

    ' Set selected view option
    for each o in options.views
        if isStringEqual(o.Name, m.view)
            m.viewButton.text = tr(o.LookupCI("title"))
            o.Selected = true
        end if
    end for

    ' Set selected sort option
    for each o in options.sort
        if isStringEqual(o.Name, m.sortField)
            m.sortButton.text = tr(o.LookupCI("title"))
            o.Selected = true
        end if
    end for

    ' Set selected filter
    for each o in options.filter
        if isStringEqual(o.Name, m.filter)
            o.Selected = true
            m.filterButton.text = tr(o.LookupCI("title"))
            m.options.filter = o.Name
        end if

        ' Select selected filter options
        if isValid(o.options) and isValid(m.filterOptions)
            if o.options.Count() > 0 and m.filterOptions.Count() > 0
                if LCase(o.Name) = LCase(m.filterOptions.keys()[0])
                    selectedFilterOptions = m.filterOptions[m.filterOptions.keys()[0]].split(o.delimiter)
                    checkedState = []

                    for each availableFilterOption in o.options
                        matchFound = false

                        for each selectedFilterOption in selectedFilterOptions
                            if LCase(toString(availableFilterOption).trim()) = LCase(selectedFilterOption.trim())
                                matchFound = true
                            end if
                        end for

                        checkedState.push(matchFound)
                    end for

                    o.checkedState = checkedState
                end if
            end if
        end if
    end for

    m.options.options = options
    m.sortOrderButton.text = m.sortAscending ? tr("Ascending") : tr("Descending")
end sub

' Set Music view, sort, and filter options
function setMusicOptions() as object
    options = {
        filter: []
    }

    options.views = [
        { "Title": tr("All Music"), "Name": "AllMusic", "Track": { "description": tr("All Music") } },
        { "Title": tr("Albums"), "Name": "Albums", "Track": { "description": tr("Albums") } },
        { "Title": tr("Artists"), "Name": "Artists", "Track": { "description": tr("Artists") } },
        { "Title": tr("Songs"), "Name": "Songs", "Track": { "description": tr("Songs") } },
        { "Title": tr("Favorite Albums"), "Name": "FavoriteAlbums", "Track": { "description": tr("Favorite Albums") } },
        { "Title": tr("Favorite Artists"), "Name": "FavoriteArtists", "Track": { "description": tr("Favorite Artists") } },
        { "Title": tr("Favorite Songs"), "Name": "FavoriteSongs", "Track": { "description": tr("Favorite Songs") } },
        { "Title": tr("Genres"), "Name": "Genres", "Track": { "description": tr("Genres") } }
    ]

    options.sort = [
        { "Title": tr("Name (A-Z)"), "Name": "SortName", "Track": { "description": tr("Name (A-Z)") } },
        { "Title": tr("Album (A-Z)"), "Name": "Album,SortName", "Track": { "description": tr("Album (A-Z)") } },
        { "Title": tr("Artist (A-Z)"), "Name": "AlbumArtist,SortName", "Track": { "description": tr("Artist (A-Z)") } },
        { "Title": tr("Date Added (Newest)"), "Name": "DateCreated,SortName", "Track": { "description": tr("Date Added (Newest)") } },
        { "Title": tr("Release Date (Newest)"), "Name": "ProductionYear,PremiereDate,SortName", "Track": { "description": tr("Release Date (Newest)") } },
        { "Title": tr("Rating (Highest)"), "Name": "CommunityRating,SortName", "Track": { "description": tr("Rating (Highest)") } },
        { "Title": tr("Random"), "Name": "Random", "Track": { "description": tr("Random") } }
    ]

    options.filter = [
        { "Title": tr("All"), "Name": "All", "Track": { "description": tr("All") } },
        { "Title": tr("Favorites"), "Name": "Favorites", "Track": { "description": tr("Favorites") } }
    ]

    if isStringEqual(m.view, "Artists") or isStringEqual(m.view, "FavoriteArtists")
        options.sort = [
            { "Title": tr("Name (A-Z)"), "Name": "SortName", "Track": { "description": tr("Name (A-Z)") } },
            { "Title": tr("Date Added (Newest)"), "Name": "DateCreated,SortName", "Track": { "description": tr("Date Added (Newest)") } },
            { "Title": tr("Random"), "Name": "Random", "Track": { "description": tr("Random") } }
        ]
    end if

    if isValid(m.top.parentItem) and isValid(m.top.parentItem.json) and isStringEqual(m.top.parentItem.json.type, "musicgenre")
        options.views = [
            { "Title": tr("Albums"), "Name": "Albums", "Track": { "description": tr("Albums") } },
        ]
    end if

    if isStringEqual(m.view, "genres")
        options.sort = [
            { "Title": tr("TITLE"), "Name": "SortName", "Track": { "description": tr("TITLE") } },
            { "Title": tr("Random"), "Name": "Random", "Track": { "description": tr("Random") } },
        ]
        options.filter = [
            { "Title": tr("All"), "Name": "All", "Track": { "description": tr("All") } }
        ]
    end if

    if isStringEqual(m.view, "Albums") or isStringEqual(m.view, "FavoriteAlbums")
        options.sort = [
            { "Title": tr("Album (A-Z)"), "Name": "SortName", "Track": { "description": tr("Album (A-Z)") } },
            { "Title": tr("Artist (A-Z)"), "Name": "AlbumArtist,SortName", "Track": { "description": tr("Artist (A-Z)") } },
            { "Title": tr("Date Added (Newest)"), "Name": "DateCreated,SortName", "Track": { "description": tr("Date Added (Newest)") } },
            { "Title": tr("Release Date (Newest)"), "Name": "ProductionYear,PremiereDate,SortName", "Track": { "description": tr("Release Date (Newest)") } },
            { "Title": tr("Rating (Highest)"), "Name": "CommunityRating,SortName", "Track": { "description": tr("Rating (Highest)") } },
            { "Title": tr("Random"), "Name": "Random", "Track": { "description": tr("Random") } }
        ]
    end if

    if isStringEqual(m.view, "Songs") or isStringEqual(m.view, "FavoriteSongs")
        options.sort = [
            { "Title": tr("Name (A-Z)"), "Name": "SortName", "Track": { "description": tr("Name (A-Z)") } },
            { "Title": tr("Album (A-Z)"), "Name": "Album,SortName", "Track": { "description": tr("Album (A-Z)") } },
            { "Title": tr("Artist (A-Z)"), "Name": "AlbumArtist,SortName", "Track": { "description": tr("Artist (A-Z)") } },
            { "Title": tr("Date Added (Newest)"), "Name": "DateCreated,SortName", "Track": { "description": tr("Date Added (Newest)") } },
            { "Title": tr("Release Date (Newest)"), "Name": "ProductionYear,PremiereDate,SortName", "Track": { "description": tr("Release Date (Newest)") } },
            { "Title": tr("Rating (Highest)"), "Name": "CommunityRating,SortName", "Track": { "description": tr("Rating (Highest)") } },
            { "Title": tr("Random"), "Name": "Random", "Track": { "description": tr("Random") } }
        ]
    end if

    return options
end function

' Return parent collection type

'
' Logo Image Loaded Event Handler
sub LogoImageLoaded(msg)
    data = msg.GetData()
    m.loadLogoTask.unobserveField("content")
    m.loadLogoTask.content = []

    if data.Count() > 0
        m.artistLogo.uri = data[0]
        m.artistLogo.visible = true
    else
        m.selectedArtistName.visible = true
    end if
end sub

'
'Handle loaded data, and add to Grid
sub ItemDataLoaded(msg)
    itemData = msg.GetData()
    m.loadItemsTask.unobserveField("content")
    m.loadItemsTask.content = []

    if itemData = invalid
        ' Check if this is initial load or pagination
        if m.loadedItems = 0
            ' Initial load failed - show error message
            m.emptyText.text = tr("Error loading items. Please check your connection and try again.")
            m.emptyText.visible = true
        end if
        stopLoadingOperation()
        return
    end if

    if LCase(m.loadItemsTask.view) = "genres"
        for each item in itemData
            m.genreData.appendChild(item)
        end for

        m.itemGrid.opacity = "0"
        m.genreList.opacity = "1"

        m.itemGrid.setFocus(false)
        m.genreList.setFocus(true)
        group = m.global.sceneManager.callFunc("getActiveScene")
        group.lastFocus = m.genreList

        m.loadedItems = m.genreList.content.getChildCount()
        m.loadedRows = m.loadedItems / m.genreList.numColumns

        if m.loadedItems = 0
            m.emptyText.text = tr("NO_ITEMS").Replace("%1", m.top.parentItem.Type)
            m.emptyText.visible = true
        end if

        stopLoadingOperation()
        return
    end if

    ' keep focus on alpha menu if it's active
    if m.top.alphaActive
        m.alphaMenu.setFocus(true)
        setLastFocus(m.alphaMenu)
    else
        m.itemGrid.opacity = "1"
        m.genreList.opacity = "0"

        m.alphaMenu.setFocus(false)
        m.itemGrid.setFocus(true)
        m.genreList.setFocus(false)
        setLastFocus(m.itemGrid)
    end if

    m.data.appendChildren(itemData)

    'Update the stored counts
    m.loadedItems = m.itemGrid.content.getChildCount()
    m.loadedRows = m.loadedItems / m.itemGrid.numColumns

    'If there are no items to display, show message
    if m.loadedItems = 0
        m.artistLogo.visible = false
        m.selectedArtistName.visible = false
        m.selectedArtistGenres.visible = false
        m.selectedArtistSongCount.visible = false
        m.selectedArtistAlbumCount.visible = false

        m.emptyText.text = tr("NO_ITEMS").Replace("%1", m.top.parentItem.Type)
        m.emptyText.visible = true
    end if

    stopLoadingOperation()
end sub

'
'Set Selected Artist Name
sub SetName(artistName as string)
    m.selectedArtistName.text = artistName
end sub

'
'Set Selected Artist Song Count
sub SetSongCount(totalCount)
    if not isValid(totalCount) or totalCount = "" or totalCount = 0
        m.selectedArtistSongCount.text = ""
        return
    end if

    appendText = " " + tr("Songs")
    if totalCount = 1
        appendText = " " + tr("Song")
    end if

    m.selectedArtistSongCount.text = totalCount.tostr() + appendText
end sub
'
'Set Selected Artist Album Count
sub SetAlbumCount(totalCount)
    if not isValid(totalCount) or totalCount = "" or totalCount = 0
        m.selectedArtistAlbumCount.text = ""
        return
    end if

    appendText = " " + tr("Albums")
    if totalCount = 1
        appendText = " " + tr("Album")
    end if

    m.selectedArtistAlbumCount.text = totalCount.tostr() + appendText
end sub

'
'Set Selected Artist Genres
sub SetGenres(artistGenres)
    m.selectedArtistGenres.text = artistGenres.join(", ")
end sub

'
'Set Background Image
sub SetBackground(backgroundUri as string)
    if backgroundUri = ""
        m.backdrop.opacity = 0
    end if

    setBackgroundWithQueue(backgroundUri, m.newBackdrop, m.swapAnimation)
end sub

'
'Handle new item being focused
sub onItemFocused()
    focusedRow = m.itemGrid.currFocusRow

    if m.itemGrid.isinFocusChain() or m.dropdownOptions.isinFocusChain()
        if focusedRow = 0
            if m.dropdownOptionsContainer.opacity < 1
                toggleDropdownOptions(true)
            end if
        else if focusedRow > 0
            if m.dropdownOptionsContainer.opacity > 0
                toggleDropdownOptions(false)
            end if
        end if
    end if

    itemInt = m.itemGrid.itemFocused

    ' If no selected item, set background to parent backdrop
    if itemInt = -1
        return
    end if

    m.artistLogo.visible = false
    m.selectedArtistName.visible = false
    m.selectedArtistGenres.visible = false
    m.selectedArtistSongCount.visible = false
    m.selectedArtistAlbumCount.visible = false

    ' Load more data if focus is within last 3 rows, and there are more items to load
    if m.loadItemsTask.totalRecordCount > m.loadedItems
        if focusedRow >= m.loadedRows - 3
            loadMoreData()
        end if
    end if

    m.selectedFavoriteItem = getItemFocused()

    if isStringEqual(m.view, "albums") then return
    if isChainValid(m.top.parentItem, "json.type") and isStringEqual(m.top.parentItem.json.type, "musicgenre") then return

    if isStringEqual(m.view, "artistsgrid")
        return
    end if

    if isStringEqual(m.view, "albumartistsgrid")
        return
    end if

    if not m.selectedArtistGenres.visible
        m.selectedArtistGenres.visible = true
    end if

    if not m.selectedArtistSongCount.visible
        m.selectedArtistSongCount.visible = true
    end if

    if not m.selectedArtistAlbumCount.visible
        m.selectedArtistAlbumCount.visible = true
    end if

    if not isChainValid(m.selectedFavoriteItem, "json") then return

    itemData = m.selectedFavoriteItem.json

    if isValid(itemData.SongCount)
        SetSongCount(itemData.SongCount)
    else
        SetSongCount("")
    end if

    if isValid(itemData.AlbumCount)
        SetAlbumCount(itemData.AlbumCount)
    else
        SetAlbumCount("")
    end if

    if isValid(itemData.Genres)
        SetGenres(itemData.Genres)
    else
        SetGenres([])
    end if

    if isValid(itemData.Name)
        SetName(itemData.Name)
    else
        SetName("")
    end if

    m.loadLogoTask.itemId = itemData.id
    m.loadLogoTask.itemType = "LogoImage"
    m.loadLogoTask.observeField("content", "LogoImageLoaded")
    m.loadLogoTask.control = TaskControl.RUN

    ' Set Background to item backdrop
    SetBackground(m.selectedFavoriteItem.backdropUrl)
end sub

sub setFieldText(field, value)
    node = m.top.findNode(field)
    if node = invalid or value = invalid then return

    ' Handle non strings... Which _shouldn't_ happen, but hey
    if type(value) = "roInt" or type(value) = "Integer"
        value = str(value)
    else if type(value) = "roFloat" or type(value) = "Float"
        value = str(value)
    else if type(value) <> "roString" and type(value) <> "String"
        value = ""
    end if

    node.text = value
end sub

'
'When Image Loading Status changes
sub newBGLoaded()
    'If image load was sucessful, start the fade swap
    if LCase(m.newBackdrop.loadStatus) = "ready"
        m.swapAnimation.control = "start"
    end if
end sub

'
'Swap Complete
sub swapDone()
    if LCase(m.swapAnimation.state) = "stopped"
        'Set main BG node image and hide transitioning node
        m.backdrop.uri = m.newBackdrop.uri
        m.backdrop.opacity = 1
        m.newBackdrop.opacity = 0

        'If there is another one to load
        if m.newBackdrop.uri <> m.queuedBGUri and m.queuedBGUri <> ""
            SetBackground(m.queuedBGUri)
            m.queuedBGUri = ""
        end if
    end if
end sub

'
'Load next set of items
sub loadMoreData()
    if m.Loading = true then return
    ' Stop if we've already loaded all items
    if m.loadedItems >= m.loadItemsTask.totalRecordCount then return

    startLoadingSpinner(false)
    m.Loading = true
    m.loadItemsTask.startIndex = m.loadedItems
    m.loadItemsTask.observeField("content", "ItemDataLoaded")
    m.loadItemsTask.control = TaskControl.RUN
end sub

'
'Item Selected
sub onItemSelected()
    selectedItem = m.itemGrid.content.getChild(m.itemGrid.itemSelected)
    print "[MusicLibraryView] onItemSelected - Item type: "; selectedItem.type
    if isValid(selectedItem.json)
        print "[MusicLibraryView] onItemSelected - JSON type: "; selectedItem.json.type
    end if
    print "[MusicLibraryView] onItemSelected - Item ID: "; selectedItem.id

    ' Audio items should use quickPlayNode for direct playback
    if selectedItem.type = "audio"
        print "[MusicLibraryView] Setting quickPlayNode for audio item"
        m.top.quickPlayNode = selectedItem
    else
        m.top.selectedItem = selectedItem
    end if
end sub

'
'Returns Focused Item
function getItemFocused()
    if m.itemGrid.isinFocusChain()
        return getItemGridFocusedItem(m.itemGrid)
    else if m.genreList.isinFocusChain() and isValid(m.genreList.itemFocused)
        return m.genreList.content.getChild(m.genreList.itemFocused)
    end if
    return invalid
end function

'
'Genre Item Selected
sub onGenreItemSelected()
    m.top.selectedItem = m.genreList.content.getChild(m.genreList.itemSelected)
end sub

'
'Genre Item Focused
sub onGenreItemFocused()
    focusedRow = m.genreList.currFocusRow

    if m.genreList.isinFocusChain()
        if focusedRow = 0
            if m.dropdownOptionsContainer.opacity < 1
                toggleDropdownOptions(true)
            end if
        else if focusedRow > 0
            if m.dropdownOptionsContainer.opacity > 0
                toggleDropdownOptions(false)
            end if
        end if
    end if

    ' Load more data if focus is within last 5 rows, and there are more items to load
    if focusedRow >= m.loadedRows - 5 and m.loadeditems < m.loadItemsTask.totalRecordCount
        loadMoreData()
    end if
end sub

sub toggleDropdownOptions(runInReverse = false as boolean)
    m.dropdownOptionsFade.reverse = runInReverse
    m.dropdownOptionsMove.reverse = runInReverse
    m.itemGridMove.reverse = runInReverse
    m.genreListMove.reverse = runInReverse

    if not isStringEqual(m.toggleDropdownOptionsAnimation.state, AnimationState.RUNNING)
        m.toggleDropdownOptionsAnimation.control = AnimationControl.START
    end if
end sub

sub processDropdownState(searchTerm as string)
    if not isStringEqual(searchTerm, string.EMPTY)
        resetDropdownsToDefaultState()
        setDropdownDisabledState(true)
        return
    end if

    setDropdownDisabledState(false)
end sub

sub setDropdownDisabledState(disabledState = false as boolean)
    m.sortButton.disabled = disabledState
    m.sortOrderButton.disabled = disabledState
    m.filterButton.disabled = disabledState
end sub

sub resetDropdownsToDefaultState()
    m.filter = "All"
    m.filterOptions = {}
    if isStringEqual(getCollectionType(), ItemType.BOXSET)
        m.sortField = "PremiereDate,SortName"
    else if isStringEqual(getCollectionType(), ItemType.MYLIST)
        m.sortField = "OrderAdded"
    else
        m.sortField = "SortName"
    end if
    m.sortAscending = true

    ' Reset view to defaults
    set_user_setting("display." + m.settingID + ".sortField", m.sortField)
    set_user_setting("display." + m.settingID + ".sortAscending", "true")
    set_user_setting("display." + m.settingID + ".filter", m.filter)
    set_user_setting("display." + m.settingID + ".filterOptions", FormatJson(m.filterOptions))

    if not isStringEqual(m.view, "Genres")
        set_user_setting(`display.${m.top.mediaType}library.defaultview`, m.view)
    end if

    m.getFiltersTask.control = "RUN"
end sub

sub onSearchTermChanged()
    if isStringEqual(m.loadItemsTask.searchTerm, m.top.searchTerm) then return



    processDropdownState(m.top.searchTerm)

    if m.bypassSearchEvent
        m.bypassSearchEvent = false
        return
    end if

    m.loadedRows = 0
    m.loadedItems = 0

    m.data = createSGNode("ContentNode")
    m.itemGrid.content = m.data

    m.genreData = createSGNode("ContentNode")
    m.genreList.content = m.genreData

    loadInitialItems()
end sub

sub alphaSelectedChanged()
    ' Prevent voicebox change double firing
    ' Allow user to toggle by clicking letter twice
    if isStringEqual(m.loadItemsTask.nameStartsWith, m.top.alphaSelected)
        m.top.alphaSelected = string.EMPTY
    end if

    m.loadedRows = 0
    m.loadedItems = 0

    m.data = createSGNode("ContentNode")
    m.itemGrid.content = m.data

    m.genreData = createSGNode("ContentNode")
    m.genreList.content = m.genreData

    m.loadItemsTask.nameStartsWith = m.top.alphaSelected
    m.top.searchTerm = string.EMPTY
    loadInitialItems()
end sub

sub unfocusAllButtons()

    m.sortButton.focus = false
    m.sortOrderButton.focus = false
    m.filterButton.focus = false
    m.viewButton.focus = false
end sub

' Using the user's cursor position, determine which dropdown is above them
function getOverhangingButton()
    gridNode = m.itemGrid.isinFocusChain() ? m.itemGrid : m.genreList

    buttonList = [m.sortButton, m.sortOrderButton, m.filterButton, m.viewButton]

    if gridNode.numColumns = 4
        return buttonList[gridNode.currFocusColumn]
    end if

    columnDisplacement = (gridNode.numColumns / buttonList.count())

    calculatedButtonIndex = CInt(((gridNode.currFocusColumn + 1) / columnDisplacement) - 1)
    if calculatedButtonIndex < 0 then calculatedButtonIndex = 0
    if calculatedButtonIndex >= buttonList.count() then calculatedButtonIndex = buttonList.count() - 1
    return buttonList[calculatedButtonIndex]
end function

function onKeyEvent(key as string, press as boolean) as boolean
    if not press then return false

    if isStringEqual(key, KeyCode.LEFT)
        if m.itemGrid.isinFocusChain() or m.genreList.isinFocusChain()
            m.top.alphaActive = true
            m.alphaMenu.setFocus(true)
            group = m.global.sceneManager.callFunc("getActiveScene")
            group.lastFocus = m.alphaMenu
            return true
        end if
    end if

    if isStringEqual(key, KeyCode.RIGHT)
        if m.alphaMenu.isinFocusChain()
            m.top.alphaActive = false
            m.alphaMenu.setFocus(false)
            m.itemGrid.setFocus(m.itemGrid.opacity = 1)
            m.genreList.setFocus(m.genreList.opacity = 1)
            setLastFocus(m.itemGrid.opacity = 1 ? m.itemGrid : m.genreList)
            return true
        end if
    end if

    if isStringEqual(key, KeyCode.UP)
        if m.itemGrid.isinFocusChain() or m.genreList.isinFocusChain()

            ' In case user quickly moves out of grid without focused row getting updated
            if m.dropdownOptionsContainer.opacity < 1
                toggleDropdownOptions(true)
            end if

            overhangButton = getOverhangingButton()
            if overhangButton.isSubType("VoiceTextEditBox")

            else
                overhangButton.focus = true
            end if
            overhangButton.setFocus(true)
            group = m.global.sceneManager.callFunc("getActiveScene")
            group.lastFocus = overhangButton
            return true
        end if
    end if

    if isStringEqual(key, KeyCode.OK)
        if m.sortButton.isinFocusChain()
            if m.sortButton.disabled then return false

            sortData = {
                data: m.options.options.sort
            }

            m.global.sceneManager.callFunc("radioDialog", tr("Sort By"), sortData)
            m.global.sceneManager.observeFieldScoped("returnData", "onSortChange")
            return true
        end if

        if m.sortOrderButton.isinFocusChain()
            if m.sortOrderButton.disabled then return false

            sortOrderData = {
                data: [
                    { Title: tr("Ascending"), Name: "ascending", Track: { description: tr("Ascending") } },
                    { Title: tr("Descending"), Name: "descending", Track: { description: tr("Descending") } },
                ]
            }

            if m.sortAscending
                sortOrderData.data[0].selected = true
            else
                sortOrderData.data[1].selected = true
            end if

            m.global.sceneManager.callFunc("radioDialog", tr("Sort Order"), sortOrderData)
            m.global.sceneManager.observeFieldScoped("returnData", "onSortOrderChange")
            return true
        end if

        if m.filterButton.isinFocusChain()
            if m.filterButton.disabled then return false

            m.options.visible = true
            m.options.findNode("filterMenu").setFocus(true)
            group = m.global.sceneManager.callFunc("getActiveScene")
            group.lastFocus = m.options
            return true
        end if

        if m.viewButton.isinFocusChain()
            viewData = {
                data: m.options.options.views
            }

            m.global.sceneManager.callFunc("radioDialog", tr("TAB_VIEW"), viewData)
            m.global.sceneManager.observeFieldScoped("returnData", "onViewChange")
            return true
        end if

        if m.dropdownOptions.isinFocusChain()
            if isStringEqual(key, KeyCode.LEFT)

                m.sortButton.focus = false
                m.sortOrderButton.focus = false
                m.viewButton.focus = false
                m.filterButton.focus = false
                return true
            end if

            if m.sortButton.isinFocusChain()

                m.sortButton.focus = false
                group = m.global.sceneManager.callFunc("getActiveScene")
                return true
            end if

            if m.sortOrderButton.isinFocusChain()
                m.sortButton.focus = true
                m.sortButton.setFocus(true)
                m.sortOrderButton.focus = false
                group = m.global.sceneManager.callFunc("getActiveScene")
                group.lastFocus = m.sortButton
                return true
            end if

            if m.filterButton.isinFocusChain()
                m.filterButton.focus = false
                m.sortOrderButton.focus = true
                m.sortOrderButton.setFocus(true)
                group = m.global.sceneManager.callFunc("getActiveScene")
                group.lastFocus = m.sortOrderButton
                return true
            end if

            if m.viewButton.isinFocusChain()
                m.viewButton.focus = false
                m.filterButton.focus = true
                m.filterButton.setFocus(true)
                group = m.global.sceneManager.callFunc("getActiveScene")
                group.lastFocus = m.filterButton
                return true
            end if
        end if
    end if

    if isStringEqual(key, KeyCode.Right)
        if m.sortButton.isinFocusChain()
            m.sortButton.focus = false
            m.sortOrderButton.focus = true
            m.sortOrderButton.setFocus(true)
            group = m.global.sceneManager.callFunc("getActiveScene")
            group.lastFocus = m.sortOrderButton
            return true
        end if

        if m.sortOrderButton.isinFocusChain()
            m.sortOrderButton.focus = false
            m.filterButton.focus = true
            m.filterButton.setFocus(true)
            group = m.global.sceneManager.callFunc("getActiveScene")
            group.lastFocus = m.filterButton
            return true
        end if

        if m.filterButton.isinFocusChain()
            m.filterButton.focus = false
            m.viewButton.focus = true
            m.viewButton.setFocus(true)
            group = m.global.sceneManager.callFunc("getActiveScene")
            group.lastFocus = m.viewButton
            return true
        end if
    end if

    if isStringEqual(key, KeyCode.DOWN)
        if m.loadedItems = 0 then return false


        m.sortButton.focus = false
        m.sortOrderButton.focus = false
        m.viewButton.focus = false
        m.filterButton.focus = false

        m.itemGrid.setFocus(m.itemGrid.opacity = 1)
        m.genreList.setFocus(m.genreList.opacity = 1)

        group = m.global.sceneManager.callFunc("getActiveScene")
        group.lastFocus = m.itemGrid
        return true
    end if

    if isStringEqual(key, KeyCode.OPTIONS)
        focusedItem = getItemFocused()
        if not isValid(focusedItem) then return false

        popupTitle = focusedItem.LookupCI("title")

        if isValid(popupTitle)
            if isChainValid(focusedItem, "json.AlbumArtist")
                popupTitle = `${chainLookup(focusedItem, "json.AlbumArtist")} - ${popupTitle}`
            end if
        end if

        if isStringEqual(focusedItem.LookupCI("type"), ItemType.MUSICALBUM)
            dialogData = [tr("Play Album"), tr("Shuffle Play Album"), tr("Instant Mix Album"), tr("Add To Playlist")]
            m.global.sceneManager.callFunc("optionDialog", "libraryitem", popupTitle, [], dialogData, { id: focusedItem.LookupCI("id") })
        end if

        return true
    end if

    if isStringEqual(key, KeyCode.BACK)
        ' If user pressed back whole scrolled down the grid, reset to item 1
        if m.itemGrid.isinFocusChain() or m.genreList.isinFocusChain()
            gridComponent = m.itemGrid.isinFocusChain() ? m.itemGrid : m.genreList

            if gridComponent.itemFocused = 0
                reclaimResources()
                m.global.sceneManager.callfunc("popScene")
                return true
            end if

            gridComponent.jumpToItem = 0
            return true
        end if

        reclaimResources()
        m.global.sceneManager.callfunc("popScene")
        return true
    end if

    if key = KeyCode.PLAY
        itemToPlay = getItemFocused()
        if itemToPlay <> invalid
            m.top.quickPlayNode = itemToPlay
            return true
        end if
    end if

    return false
end function

sub reclaimResources()
    m.loadItemsTask.control = TaskControl.STOP
    m.loadItemsTask.content = []

    m.data = createSGNode("ContentNode")
    m.itemGrid.content = m.data

    m.genreData = createSGNode("ContentNode")
    m.genreList.content = m.genreData
end sub

